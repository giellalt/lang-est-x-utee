# -*- cg-pre-pipe: "hfst-tokenise -g ../../tools/tokenisers/tokeniser-gramcheck-gt-desc.pmhfst" -*-
# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2017 UiT The arctic University of Norway
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@hum.uit.no or divvun@hum.uit.no

# ==================================================================== #
#!! !!!       [ L A N G U A G E ]  G R A M M A R   C H E C K E R
# ==================================================================== #
# ====================================================================

#  Sámi language technology 2000-2017, UiT The arctic University of Norway #

# ---------------------- #
#   Development setup:   #
# ---------------------- #
#
# cd $GTHOME/langs/??? # replace ??? with your language code
# ./autogen.sh
# ./configure --enable-apertium --with-hfst --enable-syntax --enable-grammarchecker --enable-tokenisers --enable-alignment --enable-reversed-intersect
# make
# cd tools/grammarcheckers
# make dev
#
# ---------------------- #
#   Then edit/test as:   #
# ---------------------- #
#
# emacs grammarchecker.cg3  # and C-c C-i / C-c C-c if you have cg-mode installed
# echo "Sun ij puátá." | sh modes/???gram.mode  # from the terminal, replacing ??? with your language code
#
# ---------------------- #
#   Other debug-pipes:   #
# ---------------------- #
#
# ls modes/
#
# ---------------------- #


# The pipeline (but use modes, above)
#  echo "Sun ij puátá." | hfst-tokenise —giella-cg tools/tokenisers/tokeniser-gramcheck-gt-desc.pmhfst | vislcg3 -g src/syntax/disambiguation.cg3 -t | vislcg3 -g tools/grammarcheckers/grammarchecker.cg3 -t



      # =========================================== #
      #      Short table of contents                #
      # =========================================== #

      # Delimiters
      # Tags and sets
      # Grammar checking rules

# =============== #
#!! !!!DELIMITERS
# =============== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" sent ;

# ================== #
#!! !!!TAGS AND SETS
# ================== #

SETS


#!! !!Tags


#!! This section lists all the tags inherited from the fst, and used as tags
#!! in the syntactic analysis. The next section, __Sets__, contains sets defined
#!! on the basis of the tags listed here, those set names are not visible in the output.



# Tags declared as single-membered LISTs
# ======================================

#!! !Beginning and end of sentence
LIST BOS = (>>>) (<s>) ;  #!! BOS
LIST EOS = (<<<) (</s>) ; #!! EOS

# We define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.
# CG3 doesn´t function without >>> and <<< !


#!! !Parts of speech tags

LIST N = N ;		    #!! N
LIST A = A ;		    #!! A
LIST Adv = Adv ;	    #!! Adv
LIST V = V ;		    #!! V
LIST Pron = Pron ;	    #!! Pron
LIST CS = CS ;		    #!! CS
LIST CC = CC ;		    #!! CC
SET CC-CS = CC OR CS ;  #!! CC-CS
LIST Po = Po ;		    #!! Po
LIST Pr = Pr ;		    #!! Pr
LIST Pcle = Pcle ;	    #!! Pcle
LIST Num = Num ;	    #!! Num
LIST Interj = Interj ;  #!! Interj
LIST ABBR = ABBR ;	    #!! ABBR
LIST ACR = ACR ;	    #!! ACR
LIST CLB = CLB ;	    #!! CLB
LIST LEFT = LEFT ;	    #!! LEFT
LIST RIGHT = RIGHT ;    #!! RIGHT
LIST WEB = WEB ;	    #!! WEB
LIST QMARK = """ ; # "  #!! QMARK
LIST PPUNCT = PUNCT ;   #!! PPUNCT
SET PUNCT = PPUNCT - QMARK ;  #!! PUNCT

LIST COMMA = "," ;  #!! COMMA
LIST ¶ = ¶;   #!! ¶



#!! !Tags for POS sub-categories

LIST Pers = Pers ;	    #!! Pers
LIST Dem = Dem ;	    #!! Dem
LIST Interr = Interr ;  #!! Interr
LIST Indef = Indef ;    #!! Indef
LIST Recipr = Recipr ;  #!! Recipr
LIST Refl = Refl ;	    #!! Refl
LIST Rel = Rel ;	    #!! Rel
LIST Coll = Coll ;	    #!! Coll
LIST NomAg = NomAg ;    #!! NomAg
LIST Prop = Prop ;	    #!! Prop
LIST Allegro = Allegro ; #!! Allegro
LIST Arab = Arab ; #!! Arab
LIST Rom = Rom ; #!! Romertall


#!! !Tags for morphosyntactic properties

LIST Nom = Nom ; #!! Nom
LIST Gen = Gen ; #!! Gen
LIST Ill = Ill ; #!! Ill
LIST Ine = Ine ;
LIST Ela = Ela ;
LIST All = All ;
LIST Ade = Ade ; 
LIST Abl = Abl ; 
LIST Tra = Tra ; 
LIST Trm = Trm ; 
LIST Abe = Abe ; 
LIST Com = Com ; #!! Com
LIST Ess = Ess ; #!! Ess
LIST Par = Par ; #!! Ess

LIST Sg = Sg ;   #!! Sg
LIST Du = Du ;   #!! Du
LIST Pl = Pl ;   #!! Pl
LIST Cmp/SplitR = Cmp/SplitR ;	   #!! Cmp/SplitR
LIST Cmp/SgNom = Cmp/SgNom ; #!! Cmp/SgNom Cmp/SgGen
LIST Cmp/SgGen = Cmp/SgGen ; #!! Cmp/SgGen

LIST Comp = Comp ;		   #!! Comp
LIST Superl = Superl ;	   #!! Superl
LIST Attr = Attr ;		   #!! Attr
LIST Ord = Ord ;		   #!! Ord
LIST Qst = Qst ;		   #!! Qst
LIST IV = IV ; #!! IV
LIST TV = TV ; #!! TV
LIST Prt = Prt;				   #!! Prt
LIST Prs = Prs ;			   #!! Prs
LIST Ind = Ind ;			   #!! Ind
LIST Pot = Pot ;			   #!! Pot
LIST Cond = Cond ;			   #!! Cond
LIST Imprt = Imprt ;		   #!! Imprt

LIST Sg1 = Sg1 (p1 sg) ;			   #!! Sg1
LIST Sg2 = Sg2 (p2 sg) ;			   #!! Sg2
LIST Sg3 = Sg3 (p3 sg) ;			   #!! Sg3
LIST Pl1 = Pl1 (p1 pl) ;			   #!! Pl1
LIST Pl2 = Pl2 (p2 pl);			   #!! Pl2
LIST Pl3 = Pl3 (p3 pl);			   #!! Pl3

LIST Inf = Inf ;			   #!! Inf
LIST ConNeg = ConNeg ;		   #!! ConNeg
LIST Neg = Neg ;			   #!! Neg
LIST PrfPrc = PrfPrc ;		   #!! PrfPrc
LIST PrsPrc = PrsPrc ;		   #!! PrsPrc
LIST Ger = Ger ;			   #!! Ger
LIST Sup = Sup ;			   #!! Sup




LIST Err/Orth = Err/Orth Err/Orth-a/á Err/Orth-nom/gen Err/Orth-nom/acc Err/DerSub Err/CmpSub Err/UnspaceCmp Err/HyphSub Err/SpaceCmp Err/Spellrelax err_orth_mt ; #!! Err/Orth



#!! !Semantic tags

LIST Sem/Act = Sem/Act ;				    #!! Sem/Act
LIST Sem/Ani = Sem/Ani ;				    #!! Sem/Ani
LIST Sem/Atr = Sem/Atr ;				    #!! Sem/Atr
LIST Sem/Body = Sem/Body ;				    #!! Sem/Body
LIST Sem/Clth = Sem/Clth ;				    #!! Sem/Clth
LIST Sem/Domain = Sem/Domain ;			    #!! Sem/Domain
LIST Sem/Feat-phys = Sem/Feat-phys ;	    #!! Sem/Feat-phys
LIST Sem/Fem = Sem/Fem ;				    #!! Sem/Fem
LIST Sem/Group = Sem/Group ;			    #!! Sem/Group
LIST Sem/Lang = Sem/Lang ;				    #!! Sem/Lang
LIST Sem/Mal = Sem/Mal ;				    #!! Sem/Mal
LIST Sem/Measr = Sem/Measr ;			    #!! Sem/Measr
LIST Sem/Money = Sem/Money ;			    #!! Sem/Money
LIST Sem/Obj = Sem/Obj ;				    #!! Sem/Obj
LIST Sem/Obj-el = Sem/Obj-el ;			    #!! Sem/Obj-el
LIST Sem/Org = Sem/Org ;				    #!! Sem/Org
LIST Sem/Perc-emo = Sem/Perc-emo ;		    #!! Sem/Perc-emo
LIST Sem/Plc = Sem/Plc ;				    #!! Sem/Plc
LIST Sem/Sign = Sem/Sign ;				    #!! Sem/Sign
LIST Sem/State-sick = Sem/State-lang-sick ; #!! Sem/State-sick
LIST Sem/Sur = Sem/Sur ;				    #!! Sem/Sur
LIST Sem/Time = Sem/Time ;				    #!! Sem/Time
LIST Sem/Txt = Sem/Txt ;				    #!! Sem/Txt

LIST HUMAN = Sem/Fem Sem/Mal Sem/Sur ;	    #!! HUMAN

SET HAB-ACTOR = HUMAN ; # Goal: make this like the sme one. #!! HAB-ACTOR
SET HAB-ACTOR-NOT-HUMAN = Sem/Org ; # make this like the sme one #!! HAB-ACTOR-NOT-HUMAN


LIST PROP-ATTR = Sem/Mal Sem/Sur Sem/Fem ; #!! PROP-ATTR
LIST PROP-SUR = Sem/Sur Sem/Mal Sem/Fem ; #!! PROP-SUR



SET TIME-N-SET = N + Sem/Time ;  #!! TIME-N-SET


#!! ! Syntactic tags

LIST @+FAUXV = @+FAUXV ;	         #!! @+FAUXV
LIST @+FMAINV = @+FMAINV ;	         #!! @+FMAINV
LIST @-FAUXV = @-FAUXV ;	         #!! @-FAUXV
LIST @-FMAINV = @-FMAINV ;	         #!! @-FMAINV
LIST @-FSUBJ> = @-FSUBJ> ;	         #!! @-FSUBJ>
LIST @-F<OBJ = @-F<OBJ ;	         #!! @-F<OBJ
LIST @-FOBJ> = @-FOBJ> ;	         #!! @-FOBJ>
LIST @SPRED<OBJ = @SPRED<OBJ ;       #!! @-FSPRED<OBJ
LIST @-F<ADVL = @-F<ADVL ;	         #!! @-F<ADVL
LIST @-FADVL> = @-FADVL> ;	         #!! @-FADVL>
LIST @-F<SPRED = @-F<SPRED ;         #!! @-F<SPRED
LIST @-F<OPRED = @-F<OPRED ;         #!! @-F<OPRED
LIST @-FSPRED> = @-FSPRED> ;         #!! @-FSPRED>
LIST @-FOPRED> = @-FOPRED> ;         #!! @-FOPRED>
SET FOBJ = @-F<OBJ OR @-FOBJ> ;
SET FMAINV = @-FMAINV OR @+FMAINV ;
SET FAUXV = @-FAUXV OR @+FAUXV ;
LIST @>ADVL = @>ADVL ;               #!! @>ADVL
LIST @ADVL< = @ADVL< ;               #!! @ADVL<
LIST @<ADVL = @<ADVL ;               #!! @<ADVL
LIST @ADVL> = @ADVL> ;               #!! @ADVL>
LIST ADVL = @ADVL @ADVL> @ADVL< @<ADVL @>ADVL @-F<ADVL @-FADVL> ;  #!! @ADVL
LIST @HAB> = @HAB> ;                    #!! @HAB>
LIST @<HAB = @<HAB ;                    #!! @<HAB
SET @HAB = @<HAB OR @HAB> ;
LIST @>N = @>N ;		      #!! @>N
LIST @Interj = @Interj ;    #!! @Interj
LIST @N< = @N< ;		      #!! @N<
LIST @>A = @>A ;		      #!! @>A
LIST @P< = @P< ;		      #!! @P<
LIST @>P = @>P ;		      #!! @>P
LIST @HNOUN = @HNOUN ;	      #!! @HNOUN
LIST @INTERJ = @INTERJ ;      #!! @INTERJ
LIST @>Num = @>Num;		      #!! @>Num
LIST @Pron< = @Pron< ;	      #!! @Pron<
LIST @>Pron = @>Pron ;	      #!! @>Pron
LIST @Num< = @Num< ;	      #!! @Num<
LIST @OBJ = @OBJ ;			  #!! @OBJ
LIST @<OBJ = @<OBJ ;		  #!! @<OBJ
LIST @OBJ> = @OBJ> ;		  #!! @OBJ>
LIST @OPRED = @OPRED ;		  #!! @OPRED
LIST @<OPRED = @<OPRED ;	  #!! @<OPRED
LIST @OPRED> = @OPRED> ;	  #!! @OPRED>
LIST @PCLE = @PCLE ;		  #!! @PCLE
LIST @COMP-CS< = @COMP-CS< ;  #!! @COMP-CS<
LIST @SPRED = @SPRED ;		  #!! @SPRED
LIST @<SPRED = @<SPRED ;	  #!! @<SPRED
LIST @SPRED> = @SPRED> ;	  #!! @SPRED>
LIST @SUBJ = @SUBJ ;		  #!! @SUBJ
LIST @<SUBJ = @<SUBJ ;		  #!! @<SUBJ
LIST @SUBJ> = @SUBJ> ;		  #!! @SUBJ>
SET SUBJ = @<SUBJ OR @SUBJ> OR @SUBJ ; #!! SUBJ
SET SPRED = @<SPRED OR @SPRED> OR @SPRED ; #!! SPRED
SET OPRED = @<OPRED OR @OPRED> OR @OPRED ; #!! OPRED
LIST @PPRED = @PPRED ;        #!! @PPRED
LIST @APP = @APP ;			   #!! @APP
LIST @APP-N< = @APP-N< ;	   #!! @APP-N<
LIST @APP-Pron< = @APP-Pron< ; #!! @APP-Pron<
LIST @APP>Pron = @APP>Pron ;   #!! @APP>Pron
LIST @APP-Num< = @APP-Num< ;   #!! @APP-Num<
LIST @APP-ADVL< = @APP-ADVL< ; #!! @APP-ADVL<
LIST @VOC = @VOC ;			   #!! @VOC
LIST @CVP = @CVP ;			   #!! @CVP
LIST @CNP = @CNP ;			   #!! @CNP
SET OBJ = (@<OBJ) OR (@OBJ>) OR (@OBJ) OR (@-F<OBJ) OR (@-FOBJ>) ; #!! OBJ
LIST <OBJ = @-F<OBJ @<OBJ ;     #!! <OBJ
LIST OBJ> = @OBJ> @-FOBJ> ;    #!! OBJ>
SET <OBJ-OTHERS = <OBJ OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ; #!! <OBJ-OTHERS
SET OBJ>-OTHERS = OBJ> OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ; #!! OBJ>-OTHERS
SET NOT-FAUXV = FMAINV OR OBJ + V OR ADVL + V ;
# Works after the mapping rules for verbs.
SET SYN-V = FMAINV OR FAUXV OR V + SUBJ OR OBJ + V OR ADVL + V OR (V @>N) OR (V @N<) OR (V @A<) ; #!! SYN-V
LIST @X = @X ;    #!! @X



# ========
  SETS
# ========


#!! !!Sets containing sets of lists and tags

#!! This part of the file lists a large number of sets based partly upon the tags defined above, and
#!! partly upon lexemes drawn from the lexicon.
#!! See the sourcefile itself to inspect the sets, what follows here is an overview of the set types.



#!! !Sets for Single-word sets

LIST INITIAL = "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
        "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
        "á" "æ" "ø" "å" "ö" "ä" ; #!! INITIAL


#!! !Sets for word or not

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR  \? ; #!! WORD
 # any word
# SET REAL-WORD = WORD - Num - Ord ; #!! REAL-WORD
SET REAL-WORD-NOT-ABBR = WORD - Num - Ord - (ABBR N) ; # This is former REALWORD-NOTABBR #!! REAL-WORD-NOT-ABBR
SET NOT-COMMA = WORD - COMMA ;  #!! NOT-COMMA


#!! !Case sets
# ---------

LIST ADVLCASE =  Ill  Ine  Ela  All  Ade  Abl  Tra  Trm  Abe  Com  Ess ; #!! ADLVCASE

LIST CASE-AGREEMENT =  Nom  Gen  Ill  Ine  Ela  All  Ade  Abl  Abe  Par  ; #!! CASE-AGREEMENT
LIST CASE =  Nom  Gen  Ill  Ine  Ela  All  Ade  Abl  Tra  Trm  Abe  Com  Ess  Par  ; #!! CASE

SET NOT-NOM = CASE - Nom ; #!! NOT-NOM
SET NOT-GEN = CASE - Gen ; #!! NOT-GEN


#!! !Verb sets
# ---------

# Verbs and their complements
# - - - - - - - - - - - - - -

SET NOT-V = WORD - V ; #!! NOT-V

#!! !Sets for finiteness and mood
# - - - - - - - - - -

SET REAL-NEG = Neg - Sup ; #!! REAL-NEG

SET MOOD-V = Ind OR Pot OR Imprt OR Cond OR (Neg Sup) ; #!! MOOD-V

SET NOT-PRFPRC = WORD - PrfPrc ; #!! NOT-PRFPRC


#!! !Sets for person
# - - - -

LIST SG1-V = (V Sg1)  ; #!! SG1-V
LIST SG2-V = (V Sg2)  ;  #!! SG2-V
LIST SG3-V = (V Sg3)  ;  #!! SG3-V
LIST DU1-V = (V Du1)  ;  #!! DU1-V
LIST DU2-V = (V Du2)  ;  #!! DU2-V
LIST DU3-V = (V Du3)  ;  #!! DU3-V
LIST PL1-V = (V Pl1) ;   #!! PL1-V
LIST PL2-V = (V Pl2) ;   #!! PL2-V
LIST PL3-V = (V Pl3)  ;  #!! PL3-V

 # Note that imperative verbs are not included in these sets!

# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -
SET SG-V = SG1-V OR SG2-V OR SG3-V ;
SET DU-V = DU1-V OR DU2-V OR DU3-V ;
SET PL-V = PL1-V OR PL2-V OR PL3-V ;

SET DU-PL-V = DU1-V OR DU2-V OR DU3-V OR PL1-V OR PL2-V OR PL3-V ;

SET 1-2-V = SG1-V OR SG2-V OR DU1-V OR DU2-V OR PL1-V OR PL2-V ;

#!! !Pronoun sets
# ------------

LIST MUN = (Pron Pers Sg1 Nom) (pers p1 sg nom) ;
LIST DON = (Pron Pers Sg2 Nom) (pers p2 sg nom) ;
LIST SON = (Pron Pers Sg3 Nom) (pers p3 sg nom) ;
LIST MOAI = (Pron Pers Du1 Nom) (pers p1 du nom) ;
LIST DOAI = (Pron Pers Du2 Nom) (pers p2 du nom) ;
LIST SOAI = (Pron Pers Du3 Nom) (pers p3 du nom) ;
LIST MII-PERS = (Pron Pers Pl1 Nom) (pers p1 pl nom) ;
LIST DII = (Pron Pers Pl2 Nom) (pers p2 pl nom) ;
LIST SII = (Pron Pers Pl3 Nom) (pers p3 pl nom) ;

SET PPRON-NOM-NOT-DAT = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;

SET PPRON-DU-PL = MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;
SET PPRON-PL = MII-PERS OR DII OR SII ;

SET PRON-DU = MOAI OR DOAI OR SOAI ;

SET PPRON-NOT-SII = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen)
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen) (p1 sg gen) (p2 sg gen) (p3 sg gen) (p1 du gen) (p2 du gen) (p3 du gen)
	(p1 pl gen) (p2 pl gen) (p3 pl gen) ;

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

LIST SG-DEM = (Pron Dem Sg Nom);
LIST PL-DEM = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;

LIST SG-PRON = (Pron Sg1) (Pron Sg2) (Pron Sg3) (Pron Sg) (Pron PxSg1) (Pron PxSg2) (Pron PxSg3) (pron p1 sg) (pron p2 sg) (pron p3 sg);
LIST DU-PRON = (Pron Du1) (Pron Du2) (Pron Du3)  (Pron PxDu1) (Pron PxDu2) (Pron PxDu3) (pron p1 du) (pron p2 du) (pron p3 du);
LIST PL-PRON = (Pron Pl1) (Pron Pl2) (Pron Pl3) (Pron Pl) (Pron PxPl1) (Pron PxPl2) (Pron PxPl3) (pron p1 pl) (pron p2 pl) (pron p3 pl);
LIST DU-PRON-NOTPX = (Pron Du1) (Pron Du2) (Pron Du3) (pron p1 du) (pron p2 du) (pron p3 du) ;


LIST FIRST-PX = PxSg1 PxDu1 PxPl1 ;
LIST SECOND-PX = PxSg2 PxDu2 PxPl2 ;
LIST THIRD-PX = PxSg3 PxDu3 PxPl3 ;

LIST SG-PX = PxSg1 PxSg2 PxSg3 ;
LIST DU-PX = PxDu1 PxDu2 PxDu3 ;
LIST PL-PX = PxPl1 PxPl2 PxPl3 ;

LIST PX = PxSg1 PxDu1 PxPl1 PxSg2 PxDu2 PxPl2 PxSg3 PxDu3 PxPl3 ;

LIST DU-NR = Du1 Du2 Du3 ;

SET NOT-SG-PRON = DU-PRON OR PL-PRON ;


#!! !Adjectival sets and their complements
# -------------------------------------
SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-A = WORD - A ; # This is former NOT-ADJ
SET NOT-A-COMMA = WORD - A - COMMA ;
SET NOT-Attr = WORD - Attr ;
SET NOT-A-PCLE = WORD - A - Pcle ;
SET NOT-A-CC = WORD - A-CC ;

 # and many others

#!! !Adverbial sets and their complements
# ------------------------------------
SET LEX-ADV = Adv - (AA) ;

SET NOT-ADV-DE            = WORD - Adv ;
SET NOT-ADV            = NOT-ADV-DE OR CLB ;
SET NOT-ADV-N          = NOT-ADV - N;
SET NOT-ADV-PCLE       = NOT-ADV - Pcle ;
SET NOT-ADV-INDEF      = NOT-ADV - Indef ;
SET NOT-A-ADV-PCLE = WORD - Pcle - A - Adv ; #



#!! !Sets of elements with common syntactic behaviour
# ================================================

SETS

#!! !NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

LIST N-SG-NOM = (N Sg Nom);

LIST PROP = (N Prop);

SET COMMON-N = N - Prop;

SET HEAD-N = N - Cmp/SplitR ;

SET HEAD-N-NOM = (N Nom) - Cmp/SplitR ;

# SET SUBJECTHEAD = N OR A OR Pron - Refl ; # These, can be subject heads

SET NP = N OR A ;                      # anything that can take except numerals
SET NP-HEAD        = Pron OR HEAD-N ;
SET NP-HEAD-SG     = SG-PRON OR (N Sg) OR (A Sg) - Cmp/SplitR  ;
SET NP-HEAD-PL     = PL-PRON OR (N Pl) OR (A Pl) - Cmp/SplitR  ;
SET NP-HEAD-SG-NOM = SG-PRON + Nom OR (N Sg Nom) OR (A Sg Nom) - Cmp/SplitR ;
SET NP-HEAD-PL-NOM = PL-PRON + Nom OR (N Pl Nom) OR (N Coll Nom) OR (A Pl Nom) - Cmp/SplitR ;
SET NP-HEAD-NOM    = NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM ;
SET NP-HEAD-GEN    = (Pron Gen) OR (N Gen) OR (A Gen) - Cmp/SplitR - (Dem Attr)   ;

#!! !The PRE-NP-HEAD family of sets

#!! These sets model noun phrases (NPs). The idea is to first define whatever can
#!! occur in front of the head of the NP, and thereafter negate that with the
#!! expression __WORD - premodifiers__.


SET PRE-NP-HEAD = (Prop Attr) OR (Prop @>N) OR (A Attr) OR (ABBR Attr)
         OR (Pron Pers Gen) OR (N Gen) OR
         Num OR Cmp/SplitR OR CC OR (Pron Dem) OR (Pron Refl Gen) OR (Indef Attr) OR
         (PrfPrc @>N) OR PrsPrc OR (A Ord) ;
         # The strict version of items that can only be premodifiers, not parts of the predicate

SET PRE-A-N     = (Pron Pers Gen) OR (Pron Indef) OR Num OR (A Ord) OR (Pron Dem) OR (Pron Refl Gen) ;

SET NOT-PRE-A-N = WORD - PRE-A-N ;

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;



SET NOT-NPMOD     = WORD - PRE-NP-HEAD OR ABBR OR @CVP ;
# This is the previous NPNH (npnh) set.
# NOT-NPMOD  = "NOT-PRE-NP-HEAD"

SET NOT-NPMOD-ACC     = NOT-NPMOD OR ABBR ;
SET NOT-NPMOD-ACC-ADV     = NOT-NPMOD - Adv OR ABBR ;

SET NOT-NPMODADV    = WORD - PRE-NP-HEAD - Adv ;
SET NOT-NPMODADV-INDEF   = WORD - PRE-NP-HEAD - Adv - Indef ;


SET NOT-N = WORD - N ;

SET NOT-N-A = WORD - N - A ;

SET NOT-NP = Inf OR Pcle OR Interj OR CS ;

SET NUM = Num ;

SET NOT-NUM = WORD - Num ;

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

SET REAL-CLB = CLB - COMMA ;


#!! !Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) OR (Pron Rel) ;

LIST BOUNDARYSYMBOLS =  "\;" ":" "-" "–" ;

SET S-BOUNDARY = BOUNDARYSYMBOLS OR (@CVP) ;

SET BOC = S-BOUNDARY OR BOS ;
SET BOC-PUNCT = BOC - ("-") - ("–") ;
SET EOC = S-BOUNDARY OR EOS ;

SET NP-BOUNDARY = BOS OR EOS OR REAL-CLB OR Inf OR Sup OR PPRON-NOT-GEN OR Recipr OR Po OR Pr OR Pcle OR Interj OR CS OR CP OR @CVP ;

SET SV-BOUNDARY = S-BOUNDARY OR Inf OR Sup OR FMAINV ;

SET CCCOMMA = CC OR COMMA ;
	# remember that those are potential sentence boundaries, too





# ===================================== #
#!! !!! Grammarchecker rules begin here 
# ===================================== #




#!! !Grammarchecker sets

LIST &err-agr = &err-agr ;
LIST &err-gen-po = &err-gen-po ;
LIST &err-no-conneg = &err-no-conneg ;
LIST &err-orrood-inf = &err-orrood-inf ;
LIST &err-ext-agr = &err-ext-agr ;
LIST &err-ext-vagr = &err-ext-vagr ;
LIST &err-conneg-not-3rdsg = &err-conneg-not-3rdsg ;
LIST &err-comma = &err-comma ;

MAPPING-PREFIX = & ;


#!! !!Grammarchecker rules

SECTION


#!! !Speller rules

# Speller suggestions rule – add &SUGGESTWF to any spelling suggestion
# that we actually want to suggest to the user.
# The simplest is to just add it to all spelled words:
ADD:spell-it-all (&typo &SUGGESTWF) (<spelled>) ;
# But perhaps you want to only suggest spellings of words that are not inside "quotes":
ADD:spell-conservatively (&typo &SUGGESTWF) (<spelled>) IF (NEGATE -1 QMARK LINK 2 QMARK);


#!! !Agreement rules


# A simple grammar checker rule without suggestions: Ensure preceding nominal agrees with the verb
#ADD:err-agr (&err-agr) TARGET V IF (-1C Sg + Nom) (0 V) (NOT 0 Sg3)(NOT 0 (Neg));
#ADD:err-agr (&err-agr) TARGET V IF (-1C Pron + Sg1 + Nom) (0 V) (NOT 0 Sg1);
#ADD:err-agr (&err-agr) TARGET V IF (-1C Pron + Sg2 + Nom) (0 V) (NOT 0 Sg2);
#ADD:err-agr (&err-agr) TARGET V IF (-1C Pron + Pl1 + Nom) (0 V) (NOT 0 Pl1);
#ADD:err-agr (&err-agr) TARGET V IF (-1C Pron + Pl2 + Nom) (0 V) (NOT 0 Pl2);

LIST PersPronSing1 = "mina" ;
LIST PersPronSing2 = "sina" ;
LIST PersPronSing3 = "tema" ;
LIST PersPronPlur1 = "meie" ;
LIST PersPronPlur2 = "teie"  ;
LIST PersPronPlur3 = "nemad" ;
LIST MaSaMeTeNad = "mina" "sina" "meie" "teie" "nemad" ;
LIST EiEksEga = "ei" "eks" "ega" ;
LIST JaNingKui = "ja" "ning" "kui" "või" ;

# A simple grammar checker rule without suggestions: Ensure preceding nominal agrees with the verb
# correct expressions are:
# 1) mina elan, sina elad, tema elab, meie elame, teie elate, nemad elavad
# 2) (ega, eks, ei) ma ela, (ega, eks, ei) sa ela,...
# 3) X (ja, ning, kui, või (ka)) mina elame, X (ja, ...) sina elate, X (ja, ...) tema elavad

# mina elad (NOT eks ma ela), elab, elame (NOT X ja mina elame), elate, elavad
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing1 + Nom)(0 V)(0 Sg2) (NEGATE -2 EiEksEga LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing1 + Nom)(0 V)(0 Sg3);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing1 + Nom)(0 V)(0 Pl1) (NEGATE -3 Nom LINK 1 JaNingKui) ;
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing1 + Nom)(0 V)(0 Pl2);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing1 + Nom)(0 V)(0 Pl3);

# sina elan, elab, elame, elate, elavad
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing2 + Nom)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing2 + Nom)(0 V)(0 Sg3);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing2 + Nom)(0 V)(0 Pl1) (NEGATE -3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing2 + Nom)(0 V)(0 Pl2) (NEGATE -3 Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing2 + Nom)(0 V)(0 Pl3);

# tema elan, elad, elame, elate, elavad
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing3 + Nom)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing3 + Nom)(0 V)(0 Sg2) (NEGATE -2 ("eks") OR ("ega") LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing3 + Nom)(0 V)(0 Pl1) (NEGATE -3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing3 + Nom)(0 V)(0 Pl2) (NEGATE -3 PersPronSing2 OR PersPronPlur2 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronSing3 + Nom)(0 V)(0 Pl3) (NEGATE -3 Nom LINK 1 JaNingKui);

# rebane elan, elad, ...
ADD:err-agr (&err-agr) TARGET V  (-1C Sg + Nom)(NOT -1 MaSaMeTeNad)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V  (-1C Sg + Nom)(NOT -1 MaSaMeTeNad)(0 V)(0 Sg2) (NEGATE -2 ("eks") OR ("ega") LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V  (-1C Sg + Nom)(NOT -1 MaSaMeTeNad)(0 V)(0 Pl1) (NEGATE -3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V  (-1C Sg + Nom)(NOT -1 MaSaMeTeNad)(0 V)(0 Pl2) (NEGATE -3 PersPronSing2 OR PersPronPlur2 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V  (-1C Sg + Nom)(NOT -1 MaSaMeTeNad)(0 V)(0 Pl3) (NEGATE -3 Nom LINK 1 JaNingKui);

# meie elan, elad, elab, elate, elavad
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur1 + Nom)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur1 + Nom)(0 V)(0 Sg2) (NEGATE -2 ("eks") OR ("ega") LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur1 + Nom)(0 V)(0 Sg3);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur1 + Nom)(0 V)(0 Pl2);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur1 + Nom)(0 V)(0 Pl3);

# teie elan, elad, elab, elame, elavad
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Sg2) (NEGATE -2 ("eks") OR ("ega") LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Sg3);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Pl1) (NEGATE -3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Pl2) (-3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur2 + Nom)(0 V)(0 Pl3) ;

# nemad elan, elad, elab, elame, elate
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Sg1);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Sg2) (NEGATE -2 ("eks") OR ("ega") LINK 0 Imprt);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Sg3);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Pl1) (NEGATE -3 PersPronSing1 OR PersPronPlur1 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Pl2) (NEGATE -3 PersPronSing2 OR PersPronPlur2 + Nom LINK 1 JaNingKui);
ADD:err-agr (&err-agr) TARGET V IF (-1C PersPronPlur3 + Nom)(0 V)(0 Pl3) (-3 PersPronSing1 OR PersPronPlur1 OR PersPronSing2 OR PersPronPlur2 + Nom LINK 1 JaNingKui);

# modal verb government error
LIST InfMod = "võima" "saama" "tohtima" ;
LIST SupMod = "pidama" ;
LIST Inf = Inf ;
LIST Sup = Sup ;
LIST Kvm = "," "." "!" "?" ";" ":" ;

ADD:err-gov (&err-gov) TARGET V IF (0 (Sup)) (*-1C InfMod BARRIER Kvm)(NEGATE *0 Inf);
ADD:err-gov (&err-gov) TARGET V IF (0 (Inf)) (*-1C SupMod BARRIER Kvm)(NEGATE *0 Sup);


#!! !Negation verb rules

ADD:err-conneg-not-3rdsg (&err-conneg-not-3rdsg) TARGET (V Prs Sg3) IF (0 Ind OR Cond)(-1 ("ei") LINK NOT 0 Sup);	


LIST Komaga = "vaid" "kuid" "sest" "siis" ;
LIST PoordV = (V Sg1) (V Sg2) (V Sg3) (V Pl1) (V Pl2) (V Pl3) ;

LIST Sides = CC SC;

LIST Komata = "aga" "vaid" "siis";
LIST KomataCC = "ja" "ning" "või" "ehk" "ega";
LIST KomagaCC = "ent" "kuid" ;
LIST Vordlus = "kui" "nagu" ;
LIST RelPron = "kes" "mis" ;
LIST SP = N Pron ;

ADD:err-comma (&err-comma) TARGET Sides IF (*-1C PoordV LINK NEGATE *1 Kvm) (NEGATE *1 PoordV) ;
ADD:err-comma (&err-comma) TARGET Sides IF (*-1C PoordV LINK NEGATE *1 Kvm) (*1 PoordV LINK NEGATE *-1 Kvm) ;

ADD:err-comma (&err-comma) TARGET Komata (-1 (",")) (-2 ("mina")) (1 (V Sg1));
ADD:err-comma (&err-comma) TARGET Komata (-1 (",")) (-2 ("sina")) (1 (V Sg2));
ADD:err-comma (&err-comma) TARGET Komata (-1 (",")) (-2 ("tema")) (1 (V Sg3));


ADD:err-comma (&err-comma) TARGET Vordlus IF (*-1C PoordV LINK NEGATE *1 Kvm) (NEGATE *1 PoordV) ;
ADD:err-comma (&err-comma) TARGET Vordlus IF (1 ("ka")) (NEGATE *1 PoordV) ; 

LIST Kaanded = Nom Gen Par Add Ine Ill Ela All Ade Abl;

ADD:err-comma (&err-comma) TARGET Vordlus IF (1 ("ka"))(-1 (",")) (-2 $$Kaanded) (2 $$Kaanded) ;


ADD:err-comma (&err-comma) TARGET KomataCC IF (-1 (",")) (-2 $$Kaanded) (1 $$Kaanded) ;
ADD:err-comma (&err-comma) TARGET KomagaCC IF (NOT -1 (",")) ;


#Ajamäärus
LIST Nadalapaev = "esmaspäev" "teisipäev" "kolmapäev" "neljapäev" "reede" "laupäev" "pühapäev" ;
LIST Kuu = "jaanuar" "veebruar" "märts" "aprill" "mai" "juuni" "juuli" "august" "september" "oktoober" "november" "detsember" ;

ADD:err-comma (&err-comma) TARGET Nadalapaev (NOT 1 (","))(1 (Ord))(2 Kuu) ;

#Relatiivlaused

ADD:err-comma (&err-comma) TARGET RelPron IF (NOT -1 Kvm) (-1C SP) (*-1C PoordV LINK NEGATE *1 Kvm); 
ADD:err-comma (&err-comma) TARGET RelPron IF (NOT -1 Kvm) (-1C SP) (*-1C PoordV BARRIER Kvm); 

#!! !Postposition rules

# And one with a suggestion where we simply change the tag Sg into Attr:
ADD:attr-not-pred (&attr-not-pred) TARGET (A Sg) IF (NOT 0 Attr OR Gen) (1 CC LINK *1 COMMON-N) ;
COPY:attr-not-pred (Attr &SUGGEST) EXCEPT (Sg) TARGET (&attr-not-pred) ;
# The method is: Add &SUGGEST to a copied reading to *generate* a
# suggestion form from that reading. The copy should contain the error
# tag too – &-prefixed error tags are ignored when generating, but
# used to create human-readable messages.


#!! !Punctuation rules

# NB! This is just for demo; we would NOT want to change " to « or »
# Simple punctuation rules showing how to change the lemma in the suggestions:
ADD:use-guillemets (&guillemets) TARGET (""");
COPY:left-guillemet ("«" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;
COPY:right-guillemet ("»" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;

ADD:use-ellipsis (&ellipsis) TARGET ("...");
COPY:use-ellipsis ("…" &SUGGEST) EXCEPT ("...") TARGET (&ellipsis) ;

# This is copied from the sme grammarchecker.cg3
# ----------------------------------------------
    # Spacing errors
    # --------------

# Note that some of these rules some times have to span windows – but
# still go exactly one word – which is a little complicated in
# CG. Therefore, we define some templates for going exactly one word
# to the left/right:
TEMPLATE prevWordCrossSent = (-1 (*)) OR (-1 (>>>) LINK *-2< (<<<) BARRIER (<<<)) ;
TEMPLATE nextWordCrossSent = (1 (*))  OR (0  (<<<) LINK *1>  (>>>) BARRIER (>>>) LINK 1 (*)) ;
# In the above template, the first test is the regular, non-spanning
# case – it just goes exactly one word to the right and matches
# anything.
#
# TODO: vislcg3 1.3.0 has new @< and @> positions to jump to the
# absolute position in the respectively previous and next window. Once
# https://github.com/TinoDidriksen/cg3/issues/47 is solved, we can
# change the above to the simpler
#   TEMPLATE prevWordCrossSent = (-1 (*)) OR (-1 (>>>) LINK @<-1 (*)) ;
# where the part after OR means "I am the first word of this window,
# link the the very last word of the previous window". Similarly,
#   TEMPLATE nextWordCrossSent = (1 (*))  OR (0  (<<<) LINK @>1  (*)) ;
# says "I am the last word of this window, link to the very first word
# of the next window".
#
# The second test, if the first one didn't match, will only run from
# the last cohort of a window, and will link to the first word of the
# following window. The "0 (<<<)" means "we're the last word", while
# the (>>>) matches the invisible first cohort of the following window
# (and the BARRIER ensures we only cross exactly one window
# boundary). Then we match anything on the following cohort, which is
# the first real cohort of the window.
#
# See http://beta.visl.sdu.dk/cg3/single/#test-spanning for some
# documentation on the < and > modifiers, and
# http://beta.visl.sdu.dk/cg3/single/#templates on templates. The
# below rules use these templates like
# (T:nextWordCrossSent LINK 0 (<someTag>))

## ( áddejumi


ADD:space-after-paren-beg      (&space-after-paren-beg)       TARGET (<spaceAfterParenBeg>) ;
ADD:space-after-paren-beg-link (&space-after-paren-beg &LINK) TARGET (*) IF (-1 (<spaceAfterParenBeg>) ) ;
ADDRELATION:space-after-paren-beg-rel (RIGHT) (&space-after-paren-beg) TO (1 (&LINK) LINK 0 (&space-after-paren-beg)) ;
COPY:space-after-paren-beg-sugg ("<$1$2>"v &SUGGESTWF) TARGET ("<(.*)>"r &space-after-paren-beg) IF (1 ("<(.*)>"r)) (NOT 0 (&LINK)) ;
	## áddejumi )
	
ADD:space-before-paren-end      (&space-before-paren-end)       TARGET (<spaceBeforeParenEnd>);
ADD:space-before-paren-end-link (&space-before-paren-end &LINK) TARGET (*) IF (1 (<spaceBeforeParenEnd>) );
ADDRELATION:space-before-paren-end-rel (LEFT) (&space-before-paren-end) TO (-1 (&LINK) LINK 0 (&space-before-paren-end) );
COPY:space-before-paren-end-sugg ("<$2$1>"v &SUGGESTWF) TARGET ("<(.*)>"r &space-before-paren-end) IF (-1 ("<(.*)>"r)) (NOT 0 (&LINK)) ;
	## Son gii njeaidá skáluid šaddá eret vuolgit . De galgá fas cegget skáluid ja álgit gaikut.
		
ADD:space-before-punct-mark      (&space-before-punct-mark)       TARGET (<SpaceBeforePunctMark>);
ADD:space-before-punct-mark-link (&space-before-punct-mark &LINK) TARGET (*) IF (1 (<SpaceBeforePunctMark>) );
ADDRELATION:space-before-punct-mark-rel (LEFT) (&space-before-punct-mark) TO (-1 (&LINK) LINK 0 (&space-before-punct-mark) );
COPY:space-before-punct-mark-sugg ("<$2$1>"v &SUGGESTWF) TARGET ("<(.*)>"r &space-before-punct-mark) IF (-1 ("<(.*)>"r)) (NOT 0 (&LINK)) ;
	## vuosttu  ceggejit
	## álgit gaikut.  Stoagus joatká
	## maid lea bargu.  – Mun barggan

ADD:double-space-before (&double-space-before)     (<doubleSpaceBefore>);
ADD:double-space-before-link (&double-space-before &LINK) TARGET (*) IF (T:nextWordCrossSent LINK 0 (<doubleSpaceBefore>)) ;
ADDRELATION:double-space-before-rel ($2) (*) FROM (T:nextWordCrossSent LINK 0 (<doubleSpaceBefore>)) ;
ADDRELATION:double-space-before-rel (LEFT) (&LINK) FROM (T:nextWordCrossSent LINK 0 (<doubleSpaceBefore>)) ;
COPY:double-space-before ("<$2 $1>"v &SUGGESTWF) TARGET ("<(.*)>"r &double-space-before) IF (T:prevWordCrossSent LINK 0 ("<(.*)>"r)) (NOT 0 (&LINK)) ;
	# Test sentence: 'Sanner,gii ráđđehusas.gii lávvardaga?Bonju dán!Lassin.'
	## Sanner,gii                       - works
	## ráđđehusas.Europride-festivála   - does not work
	## lávvardaga?Bonju                 - does not work
	## ráidovázzimis!Lassin             - does not work
	
LIST no-space-after-punct-OK-before = ")" ; # «jna.)» is OK
	# 1. Add error tag on punctuation
	
ADD:no-space-after-punct      (&no-space-after-punct-mark)       TARGET (<NoSpaceAfterPunctMark>) IF (T:nextWordCrossSent LINK NOT 0 no-space-after-punct-OK-before)(NEGATE 0 (A Arab)) ;
	#$ ILS árvvoštalai buot geahččalemiid mannan giđa maid {4.}, 7. ja 10. luohkáid ja vuođđokurssa oahppit čađahedje.	
	
	# 2. Add error tag + &LINK on the following word (part 1 of extending the blue underline to the following word):

ADD:no-space-after-punct-link (&no-space-after-punct-mark &LINK) TARGET (*) IF (T:prevWordCrossSent LINK 0 (&no-space-after-punct-mark));

	# 3. Add RIGHT relation on the punctuation (part 2 of extending the blue underline to the following word,
	#    also ensures that the correction will replace the word, not be appended to it):
	
ADDRELATION:no-space-after-punct-rel (RIGHT) (&LINK) FROM (T:prevWordCrossSent LINK 0 (<NoSpaceAfterPunctMark>)) ;

	# 4. Add suggestions
	
COPY:no-space-after-punct-sugg ("<$1 $2>"v &SUGGESTWF) TARGET ("<(.*)>"r &no-space-after-punct-mark) IF (T:nextWordCrossSent LINK 0 ("<(.*)>"r)) (NOT 0 (&LINK));

# End of copy from sme
# --------------------
