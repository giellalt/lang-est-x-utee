! stem variants and affix rules from Concise Morphological Dictionary of Ülle Viks
! tüvevariantide moodustus ja kasutus

LEXICON 01	!vallatu
An_SgN;
An_SgG;
An_SgIll;
An_SgG_der;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON 02_U	!õpik
An_SgN;
:u TP_02b0;

LEXICON 02_A 	!tänav
:X2 An_SgN;
:a TP_02b0;
An_SgG_der;

LEXICON 02_I	!number
:X2 An_SgN;
:i TP_02b0;

LEXICON 02_E	!kevad
An_SgN;
:e TP_02b0;

LEXICON 02_DA	!tore
An_SgN;
:da TP_02b0;

LEXICON 02_NE-SE  !raudne
:ne An_SgN;
:se TP_02b0;
:s An_SgG_der;
An_Prefix;

LEXICON 02_Vok_NE-SE  !põline
:ne An_SgN;
:se TP_02b0;
:s An_SgG_der;
:s An_Prefix;

LEXICON 02_NE-SA  !lihtsa
:ne An_SgN;
:s An_SgG_der;
:sa TP_02b0;

LEXICON 02_S-NDA  !kümnes
:s An_SgN;
:nda TP_02b0;

LEXICON 02_S-JA  !paraja
:s An_SgN;
:j An_SgG_der;
:ja TP_02b0;

LEXICON 02_T-NDE  !tuhat
:t An_SgN;
:nde TP_02b0;

LEXICON 02_GAS_S-0	!sipelgas (3-silbilised)
:as An_SgN;
An_Prefix;
:a TP_02b0;

LEXICON 02_S-0	!elajas
:s An_SgN;
TP_02b0;

LEXICON 03_A	!vaher
:$ An_SgN;
:a TP_03bt;

LEXICON 03_I	!manner
:$ An_SgN;
:i TP_03bt;

LEXICON 04		!ase
An_SgN;
An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_I-ME	!võti
:i$ An_SgN;
:i$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_E-ME	!seade, liige
:e$ An_SgN;
:e$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_U-ME	!mitu
:u$ An_SgN;
:u$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_A-ME	!luka
:a$ An_SgN;
:a$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_E-NE	!kümme, seeme
:X2e$ An_SgN;
:X2e$ An_SgP_t;
:ne TP_04b0-05bt;

LEXICON 05_S-0	!kallis
:s$ An_SgN;
:s$ An_SgP_t;
TP_04b0-05bt;

LEXICON 05_A	!valvas
:$ An_SgN;
:a TP_03bt;

LEXICON 06	!mõte
:$ TP_09a0;
TP_07bt;

LEXICON 07_S-0  !hammas
:S1$ TP_07an;
TP_07bt;

LEXICON 08_A  !kukal
:$ TP_08an;
:a TP_07bt;

LEXICON 08_E  !tütar
:$ TP_08an;
:e TP_07bt;

LEXICON 09_E	!katus
TP_09a0;
:e TP_09b0;

LEXICON 09_S-KSE !vennas
:s TP_09a0;
:kse TP_09b0;

LEXICON 10_0-SE-S	!väike-väikese
 An_SgN;
 :se An_SgG;
 :se An_SgIll;
 :s An_SgG_der;
 10_;

LEXICON 10_NE-SE-S	!soolane, hiline
 :ne An_SgN;
 :se An_SgG;
 :se An_SgIll;
 :s An_SgG_der;
 :s An_Prefix;
 10_;

LEXICON 10_KSE		!väike-väikse
 e An_SgN;
 :se An_SgG;
 :se An_SgIll;
 :s An_SgG_der;
 e 10_;

LEXICON 10_
:s An_SgP_t;
:s An_PlG_te;
:se An_PlP_id;

LEXICON 11		!harjutus
An_SgN;
:e An_SgG;
:e An_SgIll;
!:e An_SgG_der;
An_SgP_t;
An_SgAdt_se;
An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 12_NE-SE-S  !tavaline
:si 12_PlV;
12_NE-SE-S';

LEXICON 12_NE-SE-S' !teine (ja mitte teisisse)
:ne An_SgN;
:s An_Prefix;
:s An_SgG_der;
:se An_SgIll_NG;
:s An_SgAdt_se_NG;
12_;

LEXICON 12_0-SE-S   !kleenuke
An_SgN;
:se An_SgIll_NG;
:s An_SgAdt_se; 
12_;
:i 12_PlV;

LEXICON 12_
:se An_SgG;
:s TP_12c0';
:si TP_12b0v;

LEXICON 13	!suur
An_SgN;
:e An_SgG;
:e An_SgIll_NG;
An_SgG_der;
An_SgP_t;
An_SgAdt_de;
An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 14_S-NE	!laas
:s An_SgN;
:ne$ An_SgIll_NG;
:ne$ An_SgG;
! :ne$ An_SgG_der;
:n$ An_SgP_t;
:n$ An_SgAdt_de;
:n$ An_PlG_te;
:si An_PlP_V;
:si An_Pl_Vok; ! ???

LEXICON 14	!uus
:$ An_SgAdt_de;
14';

LEXICON 14'	! uks, laps
An_SgN;
:e$ An_SgG;
:e$ An_SgIll;
! :e$ An_SgG_der;
:$ An_SgP_t;
:$ An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 15	!käsi
:i An_SgN;
:e$ An_SgG;
:e$ An_SgIll_NG;
!:e$ An_SgG_der;
:$ An_SgP_tt;
:$ An_SgAdt_tte;
:$ An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 16	!ratsu
TP_16a0;

LEXICON 16_Adt	!kõne ?? kere, pere, tare
TP_16a0;
:X2 An_SgAdt_0;

LEXICON 16_GRI_Adt	!pogri
:gri TP_16a0;
:kri An_SgAdt_0;

LEXICON 17	!tuvi
TP_17a0;

LEXICON 17_Adt	!saba, kivi
TP_17a0;
:X2 An_SgAdt_0;

LEXICON 18	!lagi, ladu, jõgi, lugu
TP_18at;
:$ TP_18an;
:X2 An_SgAdt_0;

LEXICON 18_PlPV
18;
:+W An_PlP_V;

! ???
!LEXICON 18_PlV
!18;
!:$+W An_Pl_Vok;

LEXICON 18_PlPV_PlV
18;
:+W An_PlP_V;
:$+W An_Pl_Vok;

LEXICON 19	!seminar
TP_19a0;
:i TP_19b0;

LEXICON 20	!nimi
:i TP_20a0;
:e TP_20b0;

LEXICON 21	!jõgi
 :i An_SgN;
 :e$ TP_21bn;
 :e TP_21bt;
 :X2e An_SgAdt_0;

LEXICON 22_A  !jalg, pikk, sepp
An_SgN;
:a$ TP_22bn;
:a TP_22bt;

LEXICON 22_E  !hetk
An_SgN;
:e$ TP_22bn;
:e TP_22bt;

LEXICON 22_I  !tekk
An_SgN;
:i$ TP_22bn;
:i TP_22bt;

LEXICON 22_KPT_I !sigaret, kompvek, piiskop
:$ An_SgN; !!!
:i$ TP_22bn;
:i TP_22bt;

LEXICON 22_FI  !proff, nišš
 :X2 An_SgN;
 :i$ TP_22bn;
 :X2i TP_22bt;


LEXICON 22_U  !tikk
An_SgN;
:u$ TP_22bn;
:u TP_22bt;

LEXICON 22_0-U ! jõud-jõu-jõudu
An_SgN;
:$ TP_22bn;
:u TP_22bt;

LEXICON 22_KS-HE ! üks, kaks
:ks An_SgN;
:he$ TP_22bn'; ! ilma vokaalmitmuseta
:hte TP_22bt';

LEXICON 22_B-E-BE ! kuub-kuue-kuube
:b An_SgN;
:e$ TP_22bn;
:be TP_22bt;

LEXICON 22_OOD-OE ! pood-poe
:od An_SgN;
:e$ TP_22bn;
:odi TP_22bt;

LEXICON 22_AAD-AE ! raad-rae
:ad An_SgN;
:e$ TP_22bn;
:adi TP_22bt;

LEXICON 22_AAS-AE ! paas-pae
:as An_SgN;
:e$ TP_22bn;
:asi TP_22bt;

LEXICON 22_AAG-AE ! saag-sae
:ag An_SgN;
:e$ TP_22bn;
:agi TP_22bt;

LEXICON 22_EG-JA ! poeg-poja, aeg-aja
:eg An_SgN;
:ja$ TP_22bn;
:ega TP_22bt;


LEXICON 23_A	!hein
TP_23a;
:a$ TP_23bn;
:a TP_23bt;

LEXICON 23_I  !poiss
TP_23a;
:i$ TP_23bn;
:i TP_23bt;

LEXICON 24_A	!padi
:$ An_SgN;
:a$ TP_24bn;
:a TP_24bt;

LEXICON 24_E	!koger
:$ An_SgN;
:e$ TP_24bn;
:e TP_24bt;

LEXICON 24_U	!puder
:$ An_SgN;
:u$ TP_24bn;
:u TP_24bt;

LEXICON 24_I-VE	!tali
:i$ An_SgN;
:ve$ TP_24bn;
:ve TP_24bt;

LEXICON 25	!õnnelik
:$ An_SgN;
:u$ TP_25bn;
:u TP_25bt;
: An_SgG_der;

LEXICON 26	!idee
TP_26a0;
An_PlP_id;
:+i An_Pl_Vok;

LEXICON 26_ii	!galerii
TP_26a0;

! TP_  tüvevariantide paigutusviisid paradigmas

LEXICON TP_02b0  ! õpiku
An_SgG;
An_SgIll;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON TP_03bt	!vahtra, numbri
An_SgG;
An_SgIll;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON TP_04b0-05bt	!aseme (=04), liikme (=05)
An_SgG;
An_SgIll;
An_PlG_te;
An_PlP_id;

LEXICON TP_07an  ! hammas
An_SgN;
An_SgP_t;
An_PlG_te;

LEXICON TP_07bt  ! hamba (= 08)
An_SgG;
An_SgIll;
An_PlP_id;

LEXICON TP_08an  ! kannel
An_SgN;
An_SgP_t;
An_PlG_de;

LEXICON TP_09a0	! katus
An_SgN;
An_SgP_t;
An_PlG_te;

LEXICON TP_09b0	! katuse
An_SgG;
An_SgIll;
An_PlP_id;

LEXICON TP_12b0v  ! tavalisi
An_PlP_V;

LEXICON 12_PlV
An_Pl_Vok;

LEXICON TP_12c0  ! tavalis, tasumis
An_SgAdt_se;
TP_12c0';

LEXICON TP_12c0'
An_SgP_t;
An_PlG_te;

LEXICON TP_16a0	!kõne, ratsu
An_SgN;
An_SgG;
An_SgIll;
!:s An_SgG_der;
An_SgP_t;
An_PlG_de;
An_PlP_sid;

LEXICON TP_17a0	!saba, tuvi
An_SgN;
An_SgG;
An_SgIll_NG;
!:s An_SgG_der;
An_SgP_0;
An_PlG_de;
An_PlP_sid;
:+W An_PlP_V;	!Mitmuse tüvevokaali määratakse reeglitega.
:+W An_Pl_Vok;

LEXICON TP_18at	!sõda, tigu
An_SgN;
! siin olid kolmed?
An_SgP_0; ! TA muutetüvi
An_PlG_de;
An_PlP_sid;

LEXICON TP_18an	!sõja, teo
An_SgG;
An_SgIll_NG;
!An_SgG_der;

LEXICON TP_19a0	!seminar
An_SgN;

LEXICON TP_19b0	!seminari
An_SgG;
An_SgIll_NG;
!An_SgG_der;
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
:+W An_PlP_V;	!seminare
:+W An_Pl_Vok;

LEXICON TP_20a0	!nimi
An_SgN;

LEXICON TP_20b0	!nime
An_SgG;
An_SgIll_NG;
!An_SgG_der;
An_SgP_0;
:X2 An_SgAdt_0;
An_PlG_de;
An_PlP_sid;

LEXICON TP_21at ! jõgi
An_SgN;

LEXICON TP_21bn ! jõe
An_SgG;
An_SgIll_NG;
!An_SgG_der;

LEXICON TP_21bt ! jõge
An_SgP_0;
An_PlG_de;
An_PlP_sid;

LEXICON TP_22bn' !jala, tiku, sepa
An_SgG;
An_SgIll_NG;
!An_SgG_der;

LEXICON TP_22bn
TP_22bn';
:+W An_Pl_Vok;  !sõnal tikk vokaalmitmuse vormid naljakad, aga mõnel sõnal ehk pole?


LEXICON TP_22bt'  ! jal´ga, tikku, seppa
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
An_PlP_sid;

LEXICON TP_22bt
TP_22bt';
:+W An_PlP_V;

LEXICON TP_23a	!hein
An_SgN;
:$ An_PlG_te;

LEXICON TP_23bn	!heina
An_SgG;
!An_SgG_der;
:+W An_PlP_V;

LEXICON TP_23bt	!hei´na
An_SgP_0;
An_SgAdt_0;
:+W An_PlP_V;


LEXICON TP_24bn	!padja
An_SgG;
An_SgIll_NG;
!An_SgG_der;
:+W An_Pl_Vok;	!padjus

LEXICON TP_24bt	!patja
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
An_PlP_sid;
:+W An_PlP_V;	!patju

LEXICON TP_25bn	!õnneliku
An_SgG;
An_SgIll_NG;
!An_SgG_der;
:+W An_PlG_V;	!õnnelike, õnnelikes jne.

LEXICON TP_25bt	!õnnelikku
An_SgP_0;
An_SgAdt_0;
+Use/NG: An_PlG_de;
+Use/NG: An_PlP_sid;
:+W An_PlP_V;	!õnnelikke

LEXICON TP_26a0	!idee, galerii
An_SgN;
An_SgG;
An_SgIll;
!An_SgG_der;
An_SgP_d;
An_PlG_de;
An_PlP_sid;

! An_  põhivormide ja nende analoogiavormide moodustamine

LEXICON An_SgN  ! Sg Nom
 +Sg+Nom:0 GI;
 +Der+Der/ini+Adv:+ini GI; ! need võrdlusastmed, mis on otse sõnastikus (valjemini)
 +Sg+Nom+Der+Der/lik+A:+likK1 Der_25;
 LIIT;

LEXICON An_SgG  ! SgG analoogiarühm
 +Sg+Gen:0 GI;
 +Sg+Gen+Attr:0 Der_GenAttr;
 +Sg:0 KÄÄNE_1';
 +Pl+Nom:+d GI;
 +Comp:+m 02_A;
 +Der+Der/ini+Adv:+mini GI;
 +Sg+Gen+Der+Der/lik+A:+likK1 Der_25;
 +Der+Der/lt+Adv:+lt GI;
 +Dim/ke+N:ke 12_0-SE-S;
 +Dim/kene+N:ke 12_NE-SE-S;
 LIIT;
 !LIIT_DEV_G;

LEXICON An_SgG_der ! siia tuleb üleval pool ka sobiv tüvi
 +Der+Der/us+N:+us 11;
 +Der+Der/us+N:+us 09_E;


LEXICON An_SgP_0  ! SgP (formatiiv 0)
 +Sg+Par:0 GI;
 !LIIT_DEV_P;

LEXICON An_SgP_t  ! SgP (formatiiv t)
 +Sg+Par:+t GI;
 !:+t LIIT_DEV_P;

LEXICON An_SgP_tt  ! SgP (formatiiv tt)
 +Sg+Par:+tt GI;
 !:+tt LIIT_DEV_P;

LEXICON An_SgP_d  ! SgP (formatiiv d)
 +Sg+Par:+d GI;
 !:+d LIIT_DEV_P;


LEXICON An_SgAdt_0  ! SgAdt (formatiiv 0)
 +Sg+Ill:0 GI;

LEXICON An_SgAdt_de  ! SgAdt (formatiiv de)
 +Sg+Ill:+de GI;

LEXICON An_SgAdt_se  ! SgAdt (formatiiv se)
 +Sg+Ill:+se GI;

LEXICON An_SgAdt_se_NG  ! SgAdt (formatiiv se)
 +Sg+Ill+Use/NG:+se GI;

LEXICON An_SgAdt_tte ! SgAdt (formatiiv tte)
 +Sg+Ill:+tte GI;



LEXICON An_PlP_id  ! PlP (formatiiv id)
 +Pl+Par:+id GI;
 +Pl:+i KÄÄNE_2;
 :+i Spr;

LEXICON An_PlP_sid  ! PlP (formatiiv sid)
 +Pl+Par:+sid GI;



LEXICON An_PlP_V  ! PlP (vokaallõpp)
 +Pl+Par:0 GI;

LEXICON An_Pl_Vok  ! vokaalmitmuse paralleelvormid
 +Pl:0 KÄÄNE_2;
 Spr;

LEXICON An_PlG_te  ! de-mitmuse (formatiiv te) analoogiarühm
 +Pl+Gen:+te GI;
 +Pl:+te KÄÄNE_1;
 !:+te LIIT_DEV_G;

LEXICON An_PlG_de  ! de-mitmuse (formatiiv de) analoogiarühm
 +Pl+Gen:+de GI;
 +Pl:+de KÄÄNE_1;
 !:+de LIIT_DEV_G;
 :+de LIIT;

LEXICON An_PlG_V	!õnnelik-tüüp
 +Pl+Gen: GI;
 +Pl: KÄÄNE_1;
! LIIT_DEV_G;
 LIIT;

LEXICON An_Prefix
 +prefix: #;

!LEXICON An_Suffix
!  +suffix: #;

LEXICON Spr ! ülivõrre
 +Superl:%!+im 02_A;

LEXICON An_SgIll_NG
 +Sg+Ill+Use/NG:+sse GI;

LEXICON An_SgIll
 +Sg+Ill:+sse GI;

LEXICON KÄÄNE_1
 +Ill:+sse GI;
 KÄÄNE_1';

LEXICON KÄÄNE_1'  ! käändelõpud illatiivist komitatiivini
 +Ine:+s GI;
 +Ela:+st GI;
 +All:+le GI;
 +Ade:+l GI;
 +Abl:+lt GI;
 +Tra:+ks GI;
 +Trm:+ni GI;
 +Ess:+na GI;
 +Abe:+ta GI;
 +Com:+ga GI;

LEXICON KÄÄNE_2  ! käändelõpud illatiivist translatiivini (mitmus)
 +Ill:+sse GI;
 +Ine:+s GI;
 +Ela:+st GI;
 +All:+le GI;
 +Ade:+l GI;
 +Abl:+lt GI;
 +Tra:+ks GI;

LEXICON Der_GenAttr
@R.Cap.Opt@@C.Cap@ GI;

LEXICON Der_25
@U.Cap.Opt@@C.Cap@ 25;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! liitsõnade moodustamine

LEXICON LIIT

LIIT; ! tupikuks..
!& Nimisõna;
!& Omadussõna;
!& Tegusõna; ! vähemalt tuletiste jaoks on seda vaja.


!LEXICON LIIT_DEV_P
!+:& Deverbaal_P;

!LEXICON LIIT_DEV_G
!+:& Deriv_mine_ja;



