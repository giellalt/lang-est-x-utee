# Xerox xfst script file to optionally uppercase all lexical entries.
# Extend the alphabet as needed by your language.

# kopli -> KOPLI; Kopli -> KOPLI; kopli-äärne -> KOPLI-ÄÄRNE kopli-ÄÄRNE, KOPLI-äärne 

define WB [ .#. ] ;
define uppercase [
    A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|X|Y|Z|
    Á|À|Ã|Ä|Å|Æ|É|È|Ê|Ë|Í|Ì|Î|Ï|Ð|Ñ|Ó|Ò|Ô|Õ|Ö|Ø|Ú|Ù|Û|Ü|Ý|Þ|Š|Ž
    ];
define hyphen [ "-" ];
define doublehyphen[ %-%- ];
define endash [ – ];
define emdash [ — ];
define horizontalbar [ ― ];
define minus [ − ];           # from sme allcaps.xfscript

define someminus[ hyphen | doublehyphen | endash | emdash | horizontalbar | minus ];

# placeholders; they are defined elsewhere

define PlaceHolder ["^Unknowns" | "^Letters"] ;

define allcaps      a (->) A, b (->) B, c (->) C, d (->) D, e (->) E,
f (->) F, g (->) G, h (->) H, i (->) I, j (->) J, k (->) K, l (->) L,
m (->) M, n (->) N, o (->) O, p (->) P, q (->) Q, r (->) R, s (->) S,
t (->) T, u (->) U, v (->) V, w (->) W, x (->) X, y (->) Y, z (->) Z,
á (->) Á, à (->) À, â (->) Â, ã (->) Ã, ä (->) Ä, å (->) Å, æ (->) Æ, 
è (->) È, é (->) É, ê (->) Ê, ë (->) Ë, ì (->) Ì, í (->) Í, î (->) Î, 
ï (->) Ï, ð (->) Ð, ñ (->) Ñ, ò (->) Ò, ó (->) Ó, ô (->) Ô, õ (->) Õ, 
ö (->) Ö, ø (->) Ø, ù (->) Ù, ú (->) Ú, û (->) Û, ü (->) Ü, ý (->) Ý, 
þ (->) Þ, š (->) Š, ž (->) Ž
\/
WB [?* someminus]* [uppercase | someminus | PlaceHolder]* _ [uppercase | someminus]* [someminus ?*]* [someminus | WB] ;

push allcaps

# this context condition would result in:
# kopli -> KOPLI; Kopli -> KOPLI; kopli-äärne -> KOPLI-ÄÄRNE  
# WB [uppercase | someminus]* _ [uppercase | someminus]*  [someminus | WB] ;

