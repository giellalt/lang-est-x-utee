# placeholders are defined elsewhere
# root.lexc:
# 
# symbol_strings.lexc:
# ^Letters
# ...
# NB! the symbol classes are probably not complete!!!

define Digit ["0" | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ] ;

# alphabet taken from allcaps.xfscript

define Upper [ A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Á|À|Â|Ã|Ä|Å|Æ|È|É|Ê|Ë|Ì|Í|Î|Ï|Ð|Ñ|Ò|Ó|Ô|Õ|Ö|Ø|Ù|Ú|Û|Ü|Ý|Þ|Š|Ž ] ;
define Lower [ a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|á|à|â|ã|ä|å|æ|è|é|ê|ë|ì|í|î|ï|ð|ñ|ò|ó|ô|õ|ö|ø|ù|ú|û|ü|ý|þ|š|ž ] ;
define Letter [Upper | Lower] ;

define Bracket [ "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" ] ;
define Punct [ "." | "," | ";" | ":" | "!" | "?" | "…" ] ;
define Apostrophe ["'"] ;    # ’
define Minus ["-"] ;         # aka hyphen
define QuotationMark [%"] ;

# some common symbols in strings that might be inflected (???)
# from generated_files/punctuation.lexc and symbols.lexc
define CommonSymbol [= | ≈ | %& | %" | %* | %+ | ± | %/ | %\ | %~ | ° | § | %%  | © | ® | № | ‰ | %@ | ™]

# example containing Unknowns: 杨蔓-maaniale
define Unknown [ \[Letter | Digit | Punct] ] ; 

define Letters [Letter::1]+ ;   # longer string weighs more; this penalizes guessed words with short final parts

define SimplexName [Upper Lower+] ;         # Name 
define ComplicatedName [[Lower+ | Upper] ([Apostrophe | Minus]) SimplexName] ;  # iPhone deSantis al-Aksha al'Aksha N'Djamena, O'Hara, MacMahon; anti-Ameerika
define NonEuroName [Upper (Lower* Apostrophe) Lower+] ;       # Nuku'alofa

define ComplexName [[SimplexName | ComplicatedName | NonEuroName] [Minus [SimplexName | ComplicatedName | NonEuroName]*]] ;          # Name-Name 
define UnknownName [ComplexName] ;         #  
define UnknownWordLike [Letter::10][Lower::10][Lower::10]+ ;
define Punctuation [Bracket | Punct]+ ;

define UnknownString [[Letter::1 | Punct | Digit | Bracket | Minus | Apostrophe | QuotationMark | CommonSymbol | Unknown]+ & ~[UnknownName]] ;

substitute defined Letters for ^Letters
substitute defined UnknownWordLike for ^UnknownWordLike
substitute defined UnknownName for ^UnknownName
substitute defined Punctuation for ^Punctuation
substitute defined UnknownString for ^UnknownString


