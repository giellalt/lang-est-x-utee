
!! # Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes
! VVS type numbers = type numbers of Õigekeelsussõnastik 2013

LEXICON ELAMA    ! VVS 27

                      : IMPERS ;           ! elatud
                      : SUPINE_V ;         ! elama
                      : INF_IMP_NU ;       ! elada, elagu, elanud
                      :%>I7S7 IND_PAST ;   ! elas
                      : IND_COND_PRS ;     ! elan, elaksin
                      : US_DERIVATION_VERB ; ! õpetus
                      : IS_DERIVATION      ; ! arveldis

LEXICON JULGEMA   ! only julgema

                      : ELAMA ;    
           +Use/Rare:%{W%} IMPERS ;      ! impersonal forms  juleta...
           +Use/Rare:%{W%} NU ;          ! NU-affixes julenud...

LEXICON LEPPIMA   ! VVS 28

                      : LEPPIMA_STRONG ;
                      :%{W%} LEPPIMA_WEAK ;

LEXICON LEPPIMA_STRONG   ! strong grade sub-paradigm

                      : SUPINE_V ;        ! leppima
                      : INF_IMP_NU ;      ! leppida, leppigu, leppinud
                      :%>I7S7 IND_PAST ;  ! leppis

LEXICON LEPPIMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;          ! lepitud
                      : IND_COND_PRS ;    ! lepin, lepiksin

LEXICON LEKKIMA   ! VVS 28 & 27
                  ! like SÜTTIMA, but non-grading forms are less used

                      : LEPPIMA_STRONG ;         ! lekkima, lekib, lekitud, ...
                      :%{W%} LEPPIMA_WEAK ;      ! lekib, lekiks
           +Use/Rare: LEPPIMA_WEAK ;           ! lekkitud, lekkib, ...

LEXICON SÜTTIMA   ! VVS 28 & 27
                  ! like LEKKIMA, but weak grade forms are less used

                      : LEPPIMA_STRONG ;         ! süttima
                      : LEPPIMA_WEAK ;           ! süttib, süttiks
           +Use/Rare:%{W%} LEPPIMA_WEAK ;      ! sütitud, sütib, sütiks

LEXICON HAKKAMA    ! VVS 29 (hakkama), 30 (riidlema)

                      :%{W%} HAKKAMA_WEAK ;
                      : SUPINE_V ;            ! hakkama, riidlema
                      :%>I7S7 IND_PAST ;      ! hakkas, riidles
                      : IND_COND_PRS ;        ! hakkan, riidlen
                      : US_DERIVATION_VERB ;  ! only riidlema-type; others get blocked by block-derivations script

LEXICON HAKKAMA_WEAK    ! weak grade sub-paradigm

                      : IMPERS ;          ! hakatud, riieldud etc
                      : INF_IMP_NU ;      ! hakata, hakaku, hakanud; riielda, riielgu, riielnud

LEXICON ÜTLEMA    ! only ütlema, mõtlema; = HAKKAMA + Rare ELAMA
                  ! parallel forms: either t disappears in weak grade, or not

                      :le HAKKAMA ;             ! ütlema, ..., öeldud, öelda, öelgu, öelnud
             +Use/Rare:tle%{W%} HAKKAMA_WEAK ;  ! üteldud, ütelda, ütelgu, ütelnud

LEXICON TÜÜTAMA    ! only siirdama, tüütama, vastama, puistada, rüüstama, uitama, raadama, kiirgama ; = HAKKAMA + NotNorm ELAMA

                      : HAKKAMA ;             ! tüüdatud, tüüdata, tüüdaku, tüüdanud, ...
          +Use/NotNorm: HAKKAMA_WEAK ;        ! tüütatud, tüütada, tüütagu, tüütanud

LEXICON RABELEMA    ! VVS 31; = ELAMA + Rare HAKKAMA 
                    ! assuming that rabel- is less used than rabele- ...

                      : ELAMA ;                 ! rabelema; rabeletud, rabeleda, ...
           +Use/Rare:%{W%} HAKKAMA_WEAK ;     ! rabeldud, rabelda, ...

LEXICON LAASTAMA    ! only saastama, laastama; = ELAMA + Rare HAKKAMA 
                      : RABELEMA ;

LEXICON TAOTLEMA    ! only taotlema, töötama, taastama, kaotama, paitama, peatama, teatama ; = ELAMA + NotNorm HAKKAMA

                      : ELAMA ;
          +Use/NotNorm:%{W%} HAKKAMA_WEAK ;  ! taodeldud, taodelda, taodelgu, taodelnud
                                             ! töödatud, töödata, töödaku, töödanud


LEXICON KÄSKIMA     ! only käskima; like kiskuma, but with i; käsen...

                      :i LEPPIMA_STRONG ;       ! käskima etc
                      : IMPERS ;                ! kästud
          +Use/NotNorm:i%{W%} IMPERS ;          ! käsitud etc
                      :i%{W%} IND_COND_PRS ;    ! käsin etc
          +Use/NotNorm:e%{W%} IND_COND_PRS ;    ! käsen etc

LEXICON KISKUMA     ! only kiskuma 

                      :u LEPPIMA_STRONG ;         ! kiskuma etc
                      : IMPERS ;                  ! kistud
             +Use/Rare:u%{W%} IMPERS ;            ! kisutud etc
                      :u%{W%} IND_COND_PRS ;      ! kisun etc

LEXICON USKUMA      ! only uskuma 

                      :u LEPPIMA ;        ! uskuma, usutud etc
 @R.Part.One@@P.Part.Bad@+Use/Rare:@R.Part.One@@P.Part.Bad@ IMPERS_COMP ;  ! ustud etc; no derivation

LEXICON VAAGIMA    ! only vaagima
                   ! parallel forms: either g disappears in weak grade, or not

                      : LEPPIMA ;             ! vaagima, vaen, vaetud
             +Use/Rare: LEPPIMA_WEAK ;        ! vaagitud, vaagin

! ---------------
LEXICON NAERMA      ! VVS 32, 33  naerma, kuulma, seisma, tõusma etc (31 words)

                      : INF_IMP_NU ;         ! naerda, seista, naergu, seisku, naernud, seisnud
                      : IMPERS ;             ! naerdud, seistud
                      : SUPINE_V ;           ! naerma, seisma
                      :%>I7S7 IND_PAST ;     ! naeris, seisis
                      :%{W%} IND_COND_PRS ;  ! naerab, seisab, kuuleb, tõuseb

LEXICON PESEMA    ! only pesema

                      : INF_IMP_NU ;          ! pesta, pesku, pesnud
                      : IMPERS ;              ! pestud
                      :e SUPINE_V ;           ! pesema
                      :%> IND_PAST_I ;          ! pesi
                      :%{W%} IND_COND_PRS ;   ! peseb

LEXICON KUSEMA    ! only kusema; moving towards ELAMA

                      : PESEMA ;    
          +Use/NotNorm:e IMPERS ;             ! kusetud
          +Use/NotNorm:e INF_IMP_NU ;         ! kuseda, kusegu, kusenud
          +Use/NotNorm:e%>I7S7 IND_PAST ;     ! kuses

LEXICON KAITSMA    ! only  kaitsma, maitsma

                      : INF_IMP_NU ;          ! kaitsta, kaitsku, kaitsnud
             +Use/Rare:e INF_IMP_NU ;         ! kaitseda, kaitsegu, kaitsenud
                      : IMPERS ;              ! kaitstud
          +Use/NotNorm:e IMPERS ;             ! kaitseti, (on) kaitsetud
                      : SUPINE_V ;            ! kaitsma
             +Use/Rare:e SUPINE_V ;           ! kaitsema
                      :e%>I7S7 IND_PAST ;     ! kaitses
                      :e IND_COND_PRS ;       ! kaitseb

LEXICON SAATMA      ! VVS 34, 35   söötma, nutma; lõpma

                      : INF_IMP_NU ;          ! sööta, nutta, söötke, nutke, söötnud, nutnud
                      :%{W%} IMPERS ;         ! söödetud, nutetud
                      : SUPINE_V ;            ! söötma, nutma
                      :%>I7S7 IND_PAST ;      ! söötis, nuttis
                      :%{W%} IND_COND_PRS ;   ! söödab, nutab; lõpeb

LEXICON JOOKSMA     ! only jooksma

                      :%{W%} IMPERS ;         ! impersonal forms joostakse, ... 
             +Use/Rare: IMPERS ;              ! impersonal forms jookstakse,...
                      : SUPINE_V ;            ! jooksma
                      :%{W%} INFINITIVE ;     ! joosta, joostes 
             +Use/Rare: INFINITIVE ;          ! jooksta, jookstes
                      : IMPERATIVE ;          ! jooksku
             +Use/Rare:%{W%} IMPERATIVE ;     ! joosku
                      : NU ;                  ! jooksnud
                      :%>I7S7 IND_PAST ;      ! jooksis, jooksin
                      :e IND_COND_PRS ;       ! jookseb

LEXICON KATMA      ! VVS 35   katma kütma matma võtma jätma
                   ! almost like SAATMA, but IMPERS are even weaker grade

                      :t INF_IMP_NU ;        ! kütta, kütke, kütnud
                      :%{W%} IMPERS ;        ! köetud
          +Use/NotNorm:te%{W%} IMPERS ;      ! kütetud  (võiks olla ka ilma {W}-ta, s.t. :e IMPERS;)
                      :t SUPINE_V ;          ! kütma
                      :t%>I7S7 IND_PAST ;    ! küttis
                      :a IND_COND_PRS ;      ! kütab

LEXICON LASKMA    ! only laskma

                      : INF_IMP_NU ;          ! lasta, lasku, lasknud
                      : IMPERS ;              ! impersonal forms lastakse, ...
                      : SUPINE_V ;            ! laskma
             +Use/Rare:%>I7S7 IND_PAST ;      ! indicative past laskis, ...
                      :%{W%}%> IND_PAST_I ;     ! indicative past lasi, ...
                      :e%{W%} IND_COND_PRS ;  ! laseb

LEXICON NAASMA     ! only naasma, koolma; norm like NAERMA (tõusma), but usage moving towards ELAMA

                    :E7 NAERMA ;            ! naasma, naasis, naastud, ...
        +Use/NotNorm:e IMPERS ;             ! naasetud
        +Use/NotNorm:e SUPINE_V ;           ! naasema
        +Use/NotNorm:e INFINITIVE ;         ! naaseda
        +Use/NotNorm:e IMPERATIVE ;         ! naasegu
        +Use/NotNorm:e NU ;                 ! naasenud
        +Use/NotNorm:e%>I7S7 IND_PAST ;     ! naases

LEXICON VEENMA     ! only veenma, möönma; norm like NAERMA , but usage moving towards HAKKAMA

                    :A7 NAERMA ;            ! veenma, veenis, veendud, ...
        +Use/NotNorm:a IMPERS ;             ! veenatud
        +Use/NotNorm:a SUPINE_V ;           ! veenama
        +Use/NotNorm:a INFINITIVE ;         ! veenata
        +Use/NotNorm:a IMPERATIVE ;         ! veenaku
        +Use/NotNorm:a NU ;                 ! veenanud
        +Use/NotNorm:a%>I7S7 IND_PAST ;     ! veenas


!LEXICON C_COMMON  ! common to almost all words of types 32-35 (supine stem ends with C)
LEXICON INF_IMP_NU  ! common to almost all words of types 32-35 (supine stem ends with C)

                      : INFINITIVE ;        ! naerda, seista, sööta, nutta, kütta
                      : IMPERATIVE ;        ! naergu, seisku, söötku, nutku, kütku
                      : NU ;                ! kütnud


! ---------------
LEXICON TULEMA     ! only tulema, panema, surema

                      :%{dbl%}%> A_INFINITIVE ;       ! surra
                      :%{dbl%} IMPERS_A ;        ! surrakse, surdud
                      :e SUPINE_V ;          ! surema
                      :%{W%} IMPERATIVE ;    ! surgu
                      :%{W%} NU ;            ! surnud
                      :%> IND_PAST_I ;       ! suri, surin, ...
                      :e%{W%} IND_COND_PRS ; ! sureb

LEXICON OLEMA    ! only olema 

                      :ol%{dbl%}%> A_INFINITIVE ;       ! olla
                      :ol%{dbl%} IMPERS_A ;       ! ollakse, oldud
                      :ole SUPINE_V ;         ! olema
                      :ol%{W%} IMPERATIVE ;   ! olgu
                      :ol%{W%} NU ;           ! olnud
                      :ol%> IND_PAST_I ;      ! oli, olin, ...
             +Pers+Prs:ole%{W%}%> CONDITIONAL ;
   @R.Part.One@@P.Part.Bad@ OLEMA2 ;

LEXICON OLEMA2
   +Pers+Prs+Imprt+Sg2:ole%> GI ;
     +Pers+Prs+Ind+Neg:ole%> GI ;
         +Pers+Prs+Ind:ole SG1 ;
         +Pers+Prs+Ind:ole SG2 ;
 +Pers+Prs+Ind+Sg3+Aff:on GI ;
         +Pers+Prs+Ind:ole PL1 ;
         +Pers+Prs+Ind:ole PL2 ;
 +Pers+Prs+Ind+Pl3+Aff:on GI ;

! + negative forms...
     +Pers+Prs+Ind+Neg:pole%> GI ;        ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%>ksin GI ;
+Pers+Prs+Cond+Sg2+Neg:pole%>ksid GI ;
    +Pers+Prs+Cond+Neg:pole%>ks GI ;
+Pers+Prs+Cond+Pl1+Neg:pole%>ksime GI ;
+Pers+Prs+Cond+Pl2+Neg:pole%>ksite GI ;
+Pers+Prs+Cond+Pl3+Neg:pole%>ksid GI ;
    +Pers+Prs+Quot+Neg:pole%>vat GI ;
     +Pers+Prt+Ind+Neg:pol%>nud GI ;
+Pers+Prt+Cond+Sg1+Neg:pol%>nuksin GI ;
+Pers+Prt+Cond+Sg2+Neg:pol%>nuksid GI ;
    +Pers+Prt+Cond+Neg:pol%>nuks GI ;
+Pers+Prt+Cond+Pl1+Neg:pol%>nuksime GI ;
+Pers+Prt+Cond+Pl2+Neg:pol%>nuksite GI ;
+Pers+Prt+Cond+Pl3+Neg:pol%>nuksid GI ;
    +Pers+Prt+Quot+Neg:pol%>nuvat GI ;
   +Impers+Prs+Ind+Neg:pol%>da GI ;  
  +Impers+Prs+Cond+Neg:pol%>daks GI ;
  +Impers+Prs+Quot+Neg:pol%>davat GI ;
   +Impers+Prt+Ind+Neg:pol%>dud GI ;
  +Impers+Prt+Cond+Neg:pol%>duks GI ;     ! almost non-attested form (by Google search) ... 

LEXICON MINEMA    ! only minema 

                      :min%{dbl%}%> A_INFINITIVE ;       ! minna
                      :min%{dbl%} IMPERS_A ;     ! minnakse, mindud
                      :mine SUPINE_V ;           ! minema
                      :min%{W%} IMPERATIVE ;     ! mingu
                      :läi%{W%} NU ;             ! läinud
                      :läk%>s IND_PAST ;         ! läks, läksin etc
             +Pers+Prs:lähe%{W%} IND_PRS ;       ! lähen, läheb, ...
             +Pers+Prs:lähe%{W%}%> CONDITIONAL ;  ! läheks, ...
   @R.Part.One@@P.Part.Bad@ MINEMA2 ;

LEXICON MINEMA2
   +Pers+Prs+Imprt+Sg2:mine%{W%}%> GI ;          ! mine
+Pers+Prs+Ind+Pl1+Aff+Use/Rare:läh%{W%}%>me GI ;  ! lähme more colloquial...

! ---------------
! 17 exceptional words of types 37, 38, all ending in VV

LEXICON KEEMA   ! VVS 37   laama, keema, leema, naima, võima, näima

                      : VV_COMMON ;
                      : INFINITIVE ;         ! keeda
                      : IMPERS_DA ;          ! keedakse, keedud
                      :%>I7S7 IND_PAST ;     ! kees

LEXICON SAAMA !!= @CODE@ is a lexicon for saama-type words. ! VVS 37   saama, jääma

                      : VV_COMMON ;
                      : INFINITIVE ;         ! saada
                      : IMPERS_DA ;          ! saadakse, saadud
                      :%> IND_PAST_I ;       ! sai

LEXICON VIIMA     ! VVS 38   viima, käima, pooma, müüma

                      : VV_COMMON ;
                      :%> A_INFINITIVE ;     ! viia
                      : IMPERS_A ;           ! viiakse, viidud
                      :%>I7S7 IND_PAST ;     ! viis

LEXICON TOOMA     ! VVS 38   jooma, looma, tooma, lööma, sööma

                      : VV_COMMON ;
                      :%> A_INFINITIVE ;     ! surra, süüa
                      : IMPERS_A ;           ! juuakse, joodud
                      :%> IND_PAST_I ;       ! jõi
 
LEXICON VV_COMMON  ! common to all 17 words of types 37, 38 (supine stem ends with VV)

                      : SUPINE_V ;            ! keema, saama etc
                      :%{W%} IMPERATIVE ;     ! keegu, saagu etc
                      :%{W%} NU ;             ! keenud, saanud etc
                      :%{W%} IND_COND_PRS ;   ! keeb, saab etc

LEXICON IMPERS_A     ! if infinitive affix is -a, then ...
 @R.Part.One@@P.Part.Bad@ IMPERS_A_COMP ;
                      :»d TAV_DERIVATION ;       ! söödav, söödavalt, ... 
                      :»d TAMATU_DERIVATION ;    ! söödamatu 
                      :»d TU_DERIVATION ;        ! söödu, söödult, sööduim, ... 

LEXICON IMPERS_A_COMP    ! impersonal forms 
                      :%>d IMPERS_DUD ;        ! surdud, söödud
                      :%{W%}%> IMPERS_AKSE ;   ! surrakse, süüakse
        +Use/NotNorm:%>d IMPERS_AKSE ;         ! surdakse, söödakse

LEXICON IMPERS_DA     ! if infinitive affix is -da, then ...
 @R.Part.One@@P.Part.Bad@ IMPERS_DA_COMP ;
                      :»d TAV_DERIVATION ;       ! keedav, keedavalt, ... 
                      :»d TAMATU_DERIVATION ;    ! keedamatu 
                      :»d TU_DERIVATION ;        ! keedu, keedult, keeduim, ... 

LEXICON IMPERS_DA_COMP    ! impersonal forms 
                      :%>d IMPERS_DUD ;     ! keedud
                      :%>d IMPERS_AKSE ;    ! keedakse

! ---------------

LEXICON AJAMA      ! only ajama; almost like VEDAMA

                      :%{W%} IMPERS ;        ! aetud
                      :a SUPINE_V ;          ! ajama
                      :a INF_IMP_NU ;        ! ajada, ajagu, ajanud
                      :a%>I7S7 IND_PAST ;    ! ajas
                      :a IND_COND_PRS ;      ! ajab

LEXICON VEDAMA     ! VVS 28 vedama, pügama, pidama

                      :e%{W%} IMPERS ;       ! veetud
                      :a SUPINE_V ;          ! vedama
                      :a INF_IMP_NU ;        ! vedada, vedagu, vedanud
                      :a%>I7S7 IND_PAST ;    ! vedas
                      :a%{W%} IND_COND_PRS ; ! veab

LEXICON PIDAMA     ! only pidama-pidi

                      ! no impersonal forms
                      :a SUPINE_V ;          ! pidama
                      :a INF_IMP_NU ;        ! pidada, pidagu, pidanud
                      :%> IND_PAST_I ;       ! pidi
                      :a%{W%} IND_COND_PRS ; ! peab

LEXICON TEGEMA     ! only tegema, nägema

                      :h%> A_INFINITIVE ;    ! teha
                      :h IMPERS_A ;          ! tehakse, tehtud
                      :ge SUPINE_V ;         ! tegema
                      :h IMPERATIVE ;        ! tehku
                      :i NU ;                ! teinud
                      :g%> IND_PAST_I ;      ! tegi
                      :e%{W%} IND_COND_PRS ; ! teeb

! a verb cannot participate in compounding;
! it has to be turned into a declinable word first
! flag diacritics are set on paths that are NOT changing the verb word class
! the flag diacritic @R.Part.One@ requires the stem to be the first in this word;
! and then @P.Part.Bad@ ensures that no further parts are possible

LEXICON IND_COND_PRS    ! indicative, conditional present + imp sg2
 @R.Part.One@@P.Part.Bad@ IMPRT2_COMP ;
             +Pers+Prs:%> IND_PRS ;
             +Pers+Prs:%> CONDITIONAL ;
                      : NG_DERIVATION ;

LEXICON IMPRT2_COMP    ! 
   +Pers+Prs+Imprt+Sg2:%> GI ;

LEXICON IND_PRS     ! indicative present 
 @R.Part.One@@P.Part.Bad@ IND_PRS_COMP ;

LEXICON IND_PRS_COMP     ! indicative present 
              +Ind+Neg: GI ;
                  +Ind: SG1 ;
                  +Ind: SG2 ;
                  +Ind: SG3B ;
                  +Ind: PL1 ;
                  +Ind: PL2 ;
                  +Ind: PL3VAD ;
            
LEXICON IND_PAST_I    ! indicative past without -s (tegi, nägi, pidi, tõi, sõi, jõi, lõi, sai, jäi, pesi, kusi, oli, tuli, pani, suri)
 @R.Part.One@@P.Part.Bad@ IND_PAST_SG3I_COMP ;
         +Pers+Prt+Ind:i MARKED_PERSON ;

LEXICON IND_PAST_SG3I_COMP    ! indicative past sg3
         +Pers+Prt+Ind:i SG3 ;

LEXICON IND_PAST    ! indicative past
 @R.Part.One@@P.Part.Bad@ IND_PAST_SG3_COMP ;
         +Pers+Prt+Ind:i MARKED_PERSON ;

LEXICON IND_PAST_SG3_COMP    ! indicative past sg3
         +Pers+Prt+Ind: SG3 ;

LEXICON NU    !  NU-affixes
 @R.Part.One@@P.Part.Bad@ NU_COMP ;
             +Pers+Prt:%>nu CONDITIONAL ; ! conditional past
                      : NU_DERIVATION ;

LEXICON NU_COMP    !  NU-affixes
             !+Pers+Prt+Imprt:%>nud GI ;         ! omitted for now because it would create homonymy
            +Pers+Prt+Ind+Neg:%>nud GI ;
                +Pers+Prt+Prc:%>nud GI ;
               +Pers+Prt+Quot:%>nuvat GI ;
+Pers+Prt+Ind+Neg+Use/NotNorm:%>nd GI ;
    +Pers+Prt+Prc+Use/NotNorm:%>nd GI ;

LEXICON IMPERATIVE    ! imperative
 @R.Part.One@@P.Part.Bad@ IMPERATIVE_COMP ;

LEXICON IMPERATIVE_COMP    ! imperative
       +Pers+Prs+Imprt:%>G7u GI ;         ! elagu
   +Pers+Prs+Imprt+Pl1:%>G7em GI ;        ! elagem
   +Pers+Prs+Imprt+Pl2:%>G7e GI ;         ! elage

LEXICON SUPINE_V
 @R.Part.One@@P.Part.Bad@ SUPINE_V_COMP ;
             +Pers+Sup:%>ma SUPINE ;
                      : MINE_DERIVATION ;    ! elamine
                      : JA_DERIVATION ;      ! elaja, õpetaja, õpetajanna, õpetajake, õpetajalik, ...
                      : V_DERIVATION ;       ! õpetav, õpetavalt, õpetavus, õpetavam, ...
                      : MATU_DERIVATION ;    ! segamatu, segamatult, segamatus, segamatum, ...
                      : MATA_DERIVATION ;     ! segamata
                      : MUS_DERIVATION ;     ! küllastumus, promoveerumus etc

LEXICON SUPINE_V_COMP
         +Pers+Prs+Prc:%>v GI ;
        +Pers+Prs+Quot:%>vat GI ;

LEXICON SUPINE
 @R.Part.One@ SUPINE_COMP ;     ! Note that these forms can be 1st part of a compound

LEXICON SUPINE_COMP
                      : ILLATIVE ;          ! elama
                      :%>s INESSIVE ;       ! elamas
                      :%>st ELATIVE ;
                      :%>ks TRANSLATIVE ;
                      :%>ta ABESSIVE ;

LEXICON IMPERS    ! impersonal forms for most paradigms
 @R.Part.One@@P.Part.Bad@ IMPERS_COMP ;
                      :»T7 TAV_DERIVATION ;       ! elatav, elatavalt, elatavam, elatavaim, ... 
                      :»T7 TAMATU_DERIVATION ;       ! elatav, elatavalt, elatavam, elatavaim, ... 
                      :»T7 TU_DERIVATION ;       ! elatu, elatult, elatum, elatuim 

LEXICON IMPERS_COMP    ! impersonal forms for most paradigms
                      :%>T7 IMPERS_DUD ;
                      :%>T7 IMPERS_AKSE ;

LEXICON IMPERS_DUD    ! impersonal forms -[td]ud

      +Impers+Prs+Cond:aks GI ;
     +Impers+Prs+Imprt:agu GI ;
   +Impers+Prs+Ind+Neg:a GI ;
       +Impers+Prs+Prc:av GI ;
      +Impers+Prs+Quot:avat GI ;
      +Impers+Prt+Cond:uks GI ;
   +Impers+Prt+Ind+Aff:i GI ;
   +Impers+Prt+Ind+Neg:ud GI ;
       +Impers+Prt+Prc:ud GI ;
           +Impers+Sup:ama GI ;

LEXICON IMPERS_AKSE    ! impersonal form -[td0]akse

   +Impers+Prs+Ind+Aff:akse GI ;

LEXICON INFINITIVE    ! infinitive, gerund (des-form)

                      :%>D7 A_INFINITIVE ;

LEXICON A_INFINITIVE   ! common part of an infinitive, gerund (des-form);
                       ! also alone in a subparadigm of a few old words

 @R.Part.One@             INF_COMP ;     ! This can be 1st part of a compound
 @R.Part.One@@P.Part.Bad@ GER_COMP ;

LEXICON INF_COMP
                  +Inf:a GI ;
LEXICON GER_COMP
                  +Ger:es GI ;

LEXICON CONDITIONAL    ! conditional present and past  
 @R.Part.One@@P.Part.Bad@ CONDITIONAL_COMP ;
                 +Cond:ksi MARKED_PERSON ;

LEXICON CONDITIONAL_COMP
                 +Cond:ks GI ;

LEXICON MARKED_PERSON     ! sg3 ~ underspecified is missing from here 
 @R.Part.One@@P.Part.Bad@ MARKED_PERSON_COMP ;

LEXICON MARKED_PERSON_COMP     ! sg3 ~ underspecified is missing from here 
                      : SG1 ;
                      : SG2 ;
                      : PL1 ;
                      : PL2 ;
                      : PL3 ;

LEXICON SG1
              +Sg1+Aff:n GI ;

LEXICON SG2
              +Sg2+Aff:d GI ;

LEXICON SG3
              +Sg3+Aff: GI ;

LEXICON SG3B
              +Sg3+Aff:b GI ;

LEXICON PL1
              +Pl1+Aff:me GI ;

LEXICON PL2
              +Pl2+Aff:te GI ;

LEXICON PL3
              +Pl3+Aff:d GI ;

LEXICON PL3VAD
              +Pl3+Aff:vad GI ;

! paradigmatic derivation, i.e. very regular
! @R.Part.One@ means that this is not a latter part of a compound 
! @R.Case.Par@ and @R.Case.Gen@ mean that this is a latter part of a compound 
! and the previous word must be either in Par or Gen for this derivation to be allowable

LEXICON NU_DERIVATION
  @R.Part.One@@P.POS.N@  NU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.N@  NU_DERIVATION_SUF ;
  @R.Part.One@@P.POS.A@  NUD_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  NUD_DERIVATION_SUF ;

LEXICON NU_DERIVATION_SUF
             +Der/nu+N:»nu AASTA ;             ! derivation: elanu etc

LEXICON NUD_DERIVATION_SUF
             +Der/nud+A:»nu TUNTUD ;             ! derivation: elanud etc
             +Der/nud+A:»nud # ;             ! derivation: noninflecting A: elanud 

LEXICON TU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.A@  TU_DERIVATION_SUF ;

LEXICON TU_DERIVATION_SUF  !    NB! the morpheme border + d/t is assigned before this lexicon
             +Der/tu+A:u AASTA ;              ! derivation: elatu etc
             +Der/tud+A:ud # ;             ! derivation: noninflecting A: elatud 

LEXICON MATA_DERIVATION
  @R.Part.One@@P.POS.A@  MATA_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  MATA_DERIVATION_SUF ;

LEXICON MATA_DERIVATION_SUF
           +Der/mata+A:»mata GI ;            ! elamata 

LEXICON MATU_DERIVATION
  @R.Part.One@@P.POS.N@  MATU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MATU_DERIVATION_SUF ;

LEXICON MATU_DERIVATION_SUF
           +Der/matu+A:»matu AASTA ;             ! segamatu etc

LEXICON TAMATU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TAMATU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  TAMATU_DERIVATION_SUF ;

LEXICON TAMATU_DERIVATION_SUF  !    NB! the morpheme border + d/t is assigned before this lexicon
           +Der/tamatu+A:amatu AASTA ;             ! segatamatu etc

LEXICON V_DERIVATION
  @R.Part.One@@P.POS.A@  V_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  V_DERIVATION_SUF ;

LEXICON V_DERIVATION_SUF
              +Der/v+A:»v MAGUS ;              ! elav, elava etc

LEXICON TAV_DERIVATION   !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TAV_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  TAV_DERIVATION_SUF ;

LEXICON TAV_DERIVATION_SUF   !    NB! the morpheme border + d/t is assigned before this lexicon
            +Der/tav+A:av MAGUS ;              ! elatav, elatava etc

LEXICON MINE_DERIVATION
  @R.Part.One@@P.POS.N@  MINE_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MINE_DERIVATION_SUF ;

LEXICON MINE_DERIVATION_SUF
           +Der/mine+N:»mine OLULINE ;         ! elamine

LEXICON JA_DERIVATION
  @R.Part.One@@P.POS.N@  JA_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  JA_DERIVATION_SUF ;

LEXICON JA_DERIVATION_SUF
             +Der/ja+N:»ja AASTA ;             ! elaja, elajat etc
             +Der/ja+N:»jas KOND_DERIVATION ; ! õpetajaskond, õpetajaskondlik, etc

LEXICON IS_DERIVATION
  @R.Part.One@@P.POS.N@  IS_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  IS_DERIVATION_SUF ;

LEXICON IS_DERIVATION_SUF
             +Der/is+N:»is OLULINE ;         ! arveldis

LEXICON MUS_DERIVATION
  @R.Part.One@@P.POS.N@  MUS_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MUS_DERIVATION_SUF ;

LEXICON MUS_DERIVATION_SUF
             +Der/mus+N:»mus OLULINE ;         ! promoveerumus, elamus, ...

LEXICON US_DERIVATION_VERB         ! us also changes A -> N; directing to that continuation class 
  @R.Part.One@   US_DERIVATION ;   ! no previous part; no restrictions   
  @R.Case.Gen@   US_DERIVATION ;

LEXICON NG_DERIVATION
  @R.Part.One@@P.POS.N@  NG_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  NG_DERIVATION_SUF ;

LEXICON NG_DERIVATION_SUF
             +Der/ng+N:»ng VIRSIK ;         ! annoteering 



