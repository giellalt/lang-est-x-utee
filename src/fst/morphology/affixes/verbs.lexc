
!! # Verb inflection
! VVS stands for Ü. Viks "Väike vormisõnastik" for reference purposes
! VVS type numbers = type numbers of Õigekeelsussõnastik 2013

LEXICON ELAMA    ! VVS 27

                      : IND_COND_PRS ;       ! elan, elaksin
                      : INF_IMP_NU ;         ! elada, elagu, elanud
                      : IND_PAST_SUPINE ;    ! elas, elama
                      : IMPERS ;             ! elatud
                      : US_DERIVATION_VERB ; ! õpetus
                      : IS_DERIVATION      ; ! arveldis

LEXICON JULGEMA   ! only julgema

                      : ELAMA ;    
             +Usage/Rare:%{W%} IMPERS ;      ! impersonal forms  juleta...
             +Usage/Rare:%{W%} NU ;          ! NU-affixes julenud...

LEXICON LEPPIMA   ! VVS 28

                      :%{W%} LEPPIMA_WEAK ;
                      : LEPPIMA_STRONG ;

LEXICON LEPPIMA_WEAK    ! weak grade sub-paradigm

                      : IND_COND_PRS ;    ! lepin, lepiksin
                      : IMPERS ;          ! lepitud

LEXICON LEPPIMA_STRONG   ! strong grade sub-paradigm

                      : INF_IMP_NU ;      ! leppida, leppigu, leppinud
                      : IND_PAST_SUPINE ; ! leppis, leppima

LEXICON LEKKIMA   ! VVS 28 & 27
                  ! like SÜTTIMA, but non-grading forms are less used

                      :%{W%} LEPPIMA_WEAK ;      ! lekib, lekiks
             +Usage/Rare: LEPPIMA_WEAK ;           ! lekkitud, lekkib, ...
                      : LEPPIMA_STRONG ;         ! lekkima, lekib, lekitud, ...

LEXICON SÜTTIMA   ! VVS 28 & 27
                  ! like LEKKIMA, but weak grade forms are less used

                      : LEPPIMA_WEAK ;           ! süttib, süttiks
             +Usage/Rare:%{W%} LEPPIMA_WEAK ;      ! sütitud, sütib, sütiks
                      : LEPPIMA_STRONG ;         ! süttima

LEXICON HAKKAMA    ! VVS 29 (hakkama), 30 (riidlema)

                      : IND_COND_PRS ;        ! hakkan, riidlen
                      :%{W%} HAKKAMA_WEAK ;
                      : IND_PAST_SUPINE ;     ! hakkas, hakkama; riidles, riidlema
                      : US_DERIVATION_VERB ;  ! only riidlema-type; others get blocked by block-derivations script

LEXICON HAKKAMA_WEAK    ! weak grade sub-paradigm

                      : INF_IMP_NU ;      ! hakata, hakaku, hakanud; riielda, riielgu, riielnud
                      : IMPERS ;          ! hakatud, riieldud etc

LEXICON ÜTLEMA    ! only ütlema, mõtlema; = HAKKAMA + Rare ELAMA
                  ! parallel forms: either t disappears in weak grade, or not

                      :le HAKKAMA ;             ! ütlema, ..., öeldud, öelda, öelgu, öelnud
             +Usage/Rare:tle%{W%} HAKKAMA_WEAK ;  ! üteldud, ütelda, ütelgu, ütelnud

LEXICON TÜÜTAMA    ! only siirdama, tüütama, vastama, puistada, rüüstama, uitama, raadama, kiirgama ; = HAKKAMA + NotNorm ELAMA

                      : HAKKAMA ;             ! tüüdatud, tüüdata, tüüdaku, tüüdanud, ...
          +Usage/NotNorm: HAKKAMA_WEAK ;        ! tüütatud, tüütada, tüütagu, tüütanud

LEXICON RABELEMA    ! VVS 31; = ELAMA + Rare HAKKAMA 
                    ! assuming that rabel- is less used than rabele- ...

                      : ELAMA ;                 ! rabelema; rabeletud, rabeleda, ...
             +Usage/Rare:%{W%} HAKKAMA_WEAK ;     ! rabeldud, rabelda, ...
 
LEXICON LAASTAMA    ! only saastama, laastama; = ELAMA + Rare HAKKAMA 
                      : RABELEMA ;

LEXICON TAOTLEMA    ! only taotlema, töötama, taastama, kaotama, paitama, peatama, teatama ; = ELAMA + NotNorm HAKKAMA

                      : ELAMA ;
          +Usage/NotNorm:%{W%} HAKKAMA_WEAK ;  ! taodeldud, taodelda, taodelgu, taodelnud
                                             ! töödatud, töödata, töödaku, töödanud


LEXICON KÄSKIMA     ! only käskima; like kiskuma, but with i; käsen...

                      :i%{W%} IND_COND_PRS ;    ! käsin etc
          +Usage/NotNorm:e%{W%} IND_COND_PRS ;    ! käsen etc
                      :i LEPPIMA_STRONG ;       ! käskima etc
                      : IMPERS ;                ! kästud
          +Usage/NotNorm:i%{W%} IMPERS ;          ! käsitud etc

LEXICON KISKUMA     ! only kiskuma 

                      :u%{W%} IND_COND_PRS ;      ! kisun etc
                      :u LEPPIMA_STRONG ;         ! kiskuma etc
                      : IMPERS ;                  ! kistud
             +Usage/Rare:u%{W%} IMPERS ;            ! kisutud etc

LEXICON USKUMA      ! only uskuma 

                      :u LEPPIMA ;        ! uskuma, usutud etc
             +Usage/Rare:%>T7 IMPERS_DUD ;  ! ustud etc; no derivation
             +Usage/Rare:%>T7 IMPERS_AKSE ; ! ustakse etc; no derivation

LEXICON VAAGIMA    ! only vaagima
                   ! parallel forms: either g disappears in weak grade, or not

                      : LEPPIMA ;             ! vaagima, vaen, vaetud
             +Usage/Rare: LEPPIMA_WEAK ;        ! vaagitud, vaagin

! ---------------
! VVS types 32, 33, 34, 35
LEXICON NAERMA      ! VVS 32, 33  naerma, kuulma, seisma, tõusma etc (31 words)

                      :%{W%} IND_COND_PRS ;  ! naerab, seisab, kuuleb, tõuseb
                      : INF_IMP_NU ;         ! naerda, seista, naergu, seisku, naernud, seisnud
                      : IND_PAST_SUPINE ;    ! naeris, naerma; seisis, seisma
                      : IMPERS ;             ! naerdud, seistud

LEXICON SAATMA      ! VVS 34, 35   söötma, nutma etc; lõpma

                      :%{W%} IND_COND_PRS ;   ! söödab, nutab; lõpeb
                      : INF_IMP_NU ;          ! sööta, nutta, söötke, nutke, söötnud, nutnud
                      : IND_PAST_SUPINE ;     ! söötis, söötma; nuttis, nutma
                      :%{W%} IMPERS ;         ! söödetud, nutetud

LEXICON KATMA      ! VVS 35   katma kütma matma võtma jätma
                   ! almost like SAATMA, but IMPERS are even weaker grade

                      :a IND_COND_PRS ;      ! kütab
                      :t INF_IMP_NU ;        ! kütta, kütke, kütnud
                      :t IND_PAST_SUPINE ;   ! küttis, kütma
                      :%{W%} IMPERS ;        ! köetud
          +Usage/NotNorm:te%{W%} IMPERS ;      ! kütetud  (võiks olla ka ilma {W}-ta, s.t. :e IMPERS;)

LEXICON NAASMA     ! only naasma, koolma; norm like NAERMA (tõusma), but usage moving towards ELAMA

                    :E7 NAERMA ;            ! naasma, naasis, naastud, ...
        +Usage/NotNorm:e INF_IMP_NU ;         ! naaseda, naasegu, naasenud
        +Usage/NotNorm:e IND_PAST_SUPINE ;    ! naases, naasema
        +Usage/NotNorm:e IMPERS ;             ! naasetud

LEXICON VEENMA     ! only veenma, möönma; norm like NAERMA , but usage moving towards HAKKAMA

                    :A7 NAERMA ;            ! veenma, veenis, veendud, ...
        +Usage/NotNorm:a INF_IMP_NU ;         ! veenata, veenaku, veenanud
        +Usage/NotNorm:a IND_PAST_SUPINE ;    ! veenas, veenama
        +Usage/NotNorm:a IMPERS ;             ! veenatud

LEXICON KAITSMA    ! only  kaitsma, maitsma

                      :e IND_COND_PRS ;       ! kaitseb
                      : INF_IMP_NU ;          ! kaitsta, kaitsku, kaitsnud
             +Usage/Rare:e INF_IMP_NU ;         ! kaitseda, kaitsegu, kaitsenud
                      :e IND_PAST ;           ! kaitses
                      : SUPINE_V ;            ! kaitsma
             +Usage/Rare:e SUPINE_V ;           ! kaitsema
                      : IMPERS ;              ! kaitstud
          +Usage/NotNorm:e IMPERS ;             ! kaitseti, (on) kaitsetud

LEXICON JOOKSMA     ! only jooksma

                      :e IND_COND_PRS ;       ! jookseb
                      :%{W%} INFINITIVE ;     ! joosta, joostes 
             +Usage/Rare: INFINITIVE ;          ! jooksta, jookstes
                      : IMPERATIVE ;          ! jooksku
             +Usage/Rare:%{W%} IMPERATIVE ;     ! joosku
                      : NU ;                  ! jooksnud
                      : IND_PAST_SUPINE ;     ! jooksis, jooksin, jooksma
                      :%{W%} IMPERS ;         ! impersonal forms joostakse, ... 
             +Usage/Rare: IMPERS ;              ! impersonal forms jookstakse,...


! ---------------
! 17 exceptional words of types 37, 38, all ending in VV

LEXICON KEEMA   ! VVS 37   laama, keema, leema, naima, võima, näima

                      : VV_COMMON ;
                      : INFINITIVE ;         ! keeda
                      : IND_PAST ;           ! kees
                      :%>d IMPERS_AKSE ;    ! keedakse

LEXICON SAAMA !!= @CODE@ is a lexicon for saama-type words. ! VVS 37   saama, jääma

                      : VV_COMMON ;
                      : INFINITIVE ;         ! saada
                      : IND_PAST_I ;         ! sai
                      :%>d IMPERS_AKSE ;    ! saadakse

LEXICON VIIMA     ! VVS 38   viima, käima, pooma, müüma

                      : VV_COMMON ;
                      :%> A_INFINITIVE ;     ! viia
                      :%{W%}%> IMPERS_AKSE ;   ! viiakse
        +Usage/NotNorm:%>d IMPERS_AKSE ;         ! viidakse
                      : IND_PAST ;           ! viis

LEXICON TOOMA     ! VVS 38   jooma, looma, tooma, lööma, sööma

                      : VV_COMMON ;
                      :%> A_INFINITIVE ;     ! surra, süüa
                      : IND_PAST_I ;         ! jõi
                      :%{W%}%> IMPERS_AKSE ;   ! süüakse
        +Usage/NotNorm:%>d IMPERS_AKSE ;         ! söödakse
 
LEXICON VV_COMMON  ! common to all 17 words of types 37, 38 (stem ends with VV)

                      :%{W%} IND_COND_PRS ;   ! keeb, saab etc
                                              ! infinitive subparadigm -a/-da
                      :%{W%} IMPERATIVE ;     ! keegu, saagu etc
                      :%{W%} NU ;             ! keenud, saanud etc
                                              ! indicative past subparadigm -s/-i
                      : SUPINE_V ;            ! keema, saama etc
                      :%>d IMPERS_DUD ;       ! keedud
                      :»d IMPERS_DER ;        ! keedav, keedavalt, ... 
                                              ! -akse/-dakse

! ---------------

LEXICON AJAMA      ! only ajama; almost like VEDAMA

                      :a IND_COND_PRS ;      ! ajab
                      :a INF_IMP_NU ;        ! ajada, ajagu, ajanud
                      :a IND_PAST_SUPINE ;   ! ajas, ajama
                      :%{W%} IMPERS ;        ! aetud

LEXICON VEDAMA     ! VVS 28 vedama, pügama, pidama

                      :a%{W%} IND_COND_PRS ; ! veab
                      :a INF_IMP_NU ;        ! vedada, vedagu, vedanud
                      :a IND_PAST_SUPINE ;   ! vedas, vedama
                      :e%{W%} IMPERS ;       ! veetud

LEXICON PIDAMA     ! only pidama-pidi

                      :a%{W%} IND_COND_PRS ; ! peab
                      :a INF_IMP_NU ;        ! pidada, pidagu, pidanud
                      : IND_PAST_I ;         ! pidi
                      :a SUPINE_V ;          ! pidama
                      ! no impersonal forms

LEXICON TEGEMA     ! only tegema, nägema

                      :e%{W%} IND_COND_PRS ; ! teeb
                      :h%> A_INFINITIVE ;    ! teha
                      :h IMPERATIVE ;        ! tehku
                      :i NU ;                ! teinud
                      :g IND_PAST_I ;        ! tegi
                      :ge SUPINE_V ;         ! tegema
                      :h%>d IMPERS_DUD ;        ! tehtud
                      :h%{W%}%> IMPERS_AKSE ;   ! tehakse
        +Usage/NotNorm:%>hd IMPERS_AKSE ;         ! tehtakse
                      :h»d IMPERS_DER ;       ! tehtav, ... 

LEXICON PESEMA    ! only pesema

                      :%{W%} IND_COND_PRS ;   ! peseb
                      : INF_IMP_NU ;          ! pesta, pesku, pesnud
                      : IND_PAST_I ;          ! pesi
                      :e SUPINE_V ;           ! pesema
                      : IMPERS ;              ! pestud

LEXICON KUSEMA    ! only kusema; moving towards ELAMA

                      : PESEMA ;    
          +Usage/NotNorm:e INF_IMP_NU ;         ! kuseda, kusegu, kusenud
          +Usage/NotNorm:e IND_PAST ;           ! kuses
          +Usage/NotNorm:e IMPERS ;             ! kusetud

LEXICON LASKMA    ! only laskma

                      :e%{W%} IND_COND_PRS ;  ! laseb
                      : INF_IMP_NU ;          ! lasta, lasku, lasknud
             +Usage/Rare: IND_PAST ;            ! indicative past laskis, ...
                      :%{W%} IND_PAST_I ;     ! indicative past lasi, ...
                      : SUPINE_V ;            ! laskma
                      : IMPERS ;              ! impersonal forms lastakse, ...

! ---------------
LEXICON TULEMA     ! only tulema, panema, surema

                      :e%{W%} IND_COND_PRS ; ! sureb
                      :%{dbl%}%> A_INFINITIVE ;       ! surra
                      :%{W%} IMPERATIVE ;    ! surgu
                      :%{W%} NU ;            ! surnud
                      : IND_PAST_I ;         ! suri, surin, ...
                      :e SUPINE_V ;          ! surema
                      :%{dbl%}%>d IMPERS_DUD ;        ! tuldud
                      :%{dbl%}%{W%}%> IMPERS_AKSE ;   ! tullakse
          +Usage/NotNorm:%{dbl%}%>d IMPERS_AKSE ;         ! tuldakse
                      :%{dbl%}»d IMPERS_DER ;       ! tuldav, ... 

LEXICON OLEMA    ! only olema 

   +Pers+Prs+Imprt+Sg2:ole%> NO_COMPOUND ;
     +Pers+Prs+Ind+Neg:ole%> NO_COMPOUND ;
         +Pers+Prs+Ind:ole SG1 ;
         +Pers+Prs+Ind:ole SG2 ;
 +Pers+Prs+Ind+Sg3+Aff:on NO_COMPOUND ;
         +Pers+Prs+Ind:ole PL1 ;
         +Pers+Prs+Ind:ole PL2 ;
 +Pers+Prs+Ind+Pl3+Aff:on NO_COMPOUND ;

                      :ol%{dbl%}%> A_INFINITIVE ;       ! olla
                      :ol%{W%} IMPERATIVE ;   ! olgu
                      :ol%{W%} NU ;           ! olnud
                      :ol IND_PAST_I ;        ! oli, olin, ...
                      :ole SUPINE_V ;         ! olema
             +Pers+Prs:ole%{W%}%> CONDITIONAL ;
                      :ol%{dbl%}%>d IMPERS_DUD ;        ! oldud
                      :ol%{dbl%}%{W%}%> IMPERS_AKSE ;   ! ollakse
          +Usage/NotNorm:ol%{dbl%}%>d IMPERS_AKSE ;         ! oldakse
                      :ol%{dbl%}»d IMPERS_DER ;       ! oldav, ... 

! + negative forms...
     +Pers+Prs+Ind+Neg:pole%> NO_COMPOUND ;        ! NB! it has the same categories as ole ...
+Pers+Prs+Cond+Sg1+Neg:pole%>ksin NO_COMPOUND ;
+Pers+Prs+Cond+Sg2+Neg:pole%>ksid NO_COMPOUND ;
    +Pers+Prs+Cond+Neg:pole%>ks NO_COMPOUND ;
+Pers+Prs+Cond+Pl1+Neg:pole%>ksime NO_COMPOUND ;
+Pers+Prs+Cond+Pl2+Neg:pole%>ksite NO_COMPOUND ;
+Pers+Prs+Cond+Pl3+Neg:pole%>ksid NO_COMPOUND ;
    +Pers+Prs+Quot+Neg:pole%>vat NO_COMPOUND ;
     +Pers+Prt+Ind+Neg:pol%>nud NO_COMPOUND ;
+Pers+Prt+Cond+Sg1+Neg:pol%>nuksin NO_COMPOUND ;
+Pers+Prt+Cond+Sg2+Neg:pol%>nuksid NO_COMPOUND ;
    +Pers+Prt+Cond+Neg:pol%>nuks NO_COMPOUND ;
+Pers+Prt+Cond+Pl1+Neg:pol%>nuksime NO_COMPOUND ;
+Pers+Prt+Cond+Pl2+Neg:pol%>nuksite NO_COMPOUND ;
+Pers+Prt+Cond+Pl3+Neg:pol%>nuksid NO_COMPOUND ;
    +Pers+Prt+Quot+Neg:pol%>nuvat NO_COMPOUND ;
   +Impers+Prs+Ind+Neg:pol%>da NO_COMPOUND ;  
  +Impers+Prs+Cond+Neg:pol%>daks NO_COMPOUND ;
  +Impers+Prs+Quot+Neg:pol%>davat NO_COMPOUND ;
   +Impers+Prt+Ind+Neg:pol%>dud NO_COMPOUND ;
  +Impers+Prt+Cond+Neg:pol%>duks NO_COMPOUND ;     ! almost non-attested form (by Google search) ... 

LEXICON MINEMA    ! only minema 

             +Pers+Prs:lähe%{W%} IND_PRS ;       ! lähen, läheb, ...
             +Pers+Prs:lähe%{W%}%> CONDITIONAL ;  ! läheks, ...
   +Pers+Prs+Imprt+Sg2:mine%{W%}%> NO_COMPOUND ;          ! mine
+Pers+Prs+Ind+Pl1+Aff+Usage/Rare:läh%{W%}%>me NO_COMPOUND ;  ! lähme more colloquial...
                      :min%{dbl%}%> A_INFINITIVE ;       ! minna
                      :min%{W%} IMPERATIVE ;     ! mingu
                      :läi%{W%} NU ;             ! läinud
         +Pers+Prt+Ind:läk%>s SG3 ;              ! läks
         +Pers+Prt+Ind:läk%>s MARKED_PERSON ;    ! läksin, läksid etc
                      :mine SUPINE_V ;           ! minema
                      :min%{dbl%}%>d IMPERS_DUD ;        ! mindud
                      :min%{dbl%}%{W%}%> IMPERS_AKSE ;   ! minnakse
          +Usage/NotNorm:min%{dbl%}%>d IMPERS_AKSE ;         ! mindakse
                      :min%{dbl%}»d IMPERS_DER ;       ! mindav, ... 

!--------
LEXICON IND_COND_PRS    ! indicative, conditional present + imp sg2
   +Pers+Prs+Imprt+Sg2:%> NO_COMPOUND ;
             +Pers+Prs:%> IND_PRS ;
             +Pers+Prs:%> CONDITIONAL ;
                      : NG_DERIVATION ;

LEXICON IND_PRS         ! indicative present 
              +Ind+Neg:   NO_COMPOUND ;
                  +Ind:   SG1 ;
                  +Ind:   SG2 ;
                  +Ind:b  SG3 ;
                  +Ind:   PL1 ;
                  +Ind:   PL2 ;
                  +Ind:va PL3 ;

LEXICON IND_PAST_SUPINE    ! common to many inflectional types
                      : IND_PAST ;
                      : SUPINE_V ;
            
LEXICON IND_PAST_I    ! indicative past without -s (tegi, nägi, pidi, tõi, sõi, jõi, lõi, sai, jäi, pesi, kusi, oli, tuli, pani, suri)
         +Pers+Prt+Ind:%>i SG3 ;
         +Pers+Prt+Ind:%>  MARKED_PERSON ;

LEXICON IND_PAST    ! indicative past
         +Pers+Prt+Ind:%>I7S7 SG3 ;
         +Pers+Prt+Ind:%>I7S7 MARKED_PERSON ;

LEXICON INF_IMP_NU  ! common to almost all words 

                      : INFINITIVE ;        ! elada, naerda, seista, sööta, nutta, kütta
                      : IMPERATIVE ;        ! elagu, naergu, seisku, söötku, nutku, kütku
                      : NU ;                ! elanud, naernud, kütnud


LEXICON NU    !  NU-affixes
             !+Pers+Prt+Imprt:%>nud NO_COMPOUND ;         ! omitted for now because it would create homonymy
            +Pers+Prt+Ind+Neg:%>nud NO_COMPOUND ;
                +Pers+Prt+Prc:%>nud NO_COMPOUND ;
               +Pers+Prt+Quot:%>nuvat NO_COMPOUND ;
+Pers+Prt+Ind+Neg+Usage/NotNorm:%>nd NO_COMPOUND ;
    +Pers+Prt+Prc+Usage/NotNorm:%>nd NO_COMPOUND ;
                    +Pers+Prt:%>nu CONDITIONAL ; ! conditional past
                             : NU_DERIVATION ;

LEXICON IMPERATIVE    ! imperative
       +Pers+Prs+Imprt:%>G7u NO_COMPOUND ;         ! elagu
   +Pers+Prs+Imprt+Pl1:%>G7em NO_COMPOUND ;        ! elagem
   +Pers+Prs+Imprt+Pl2:%>G7e NO_COMPOUND ;         ! elage

LEXICON SUPINE_V
         +Pers+Prs+Prc:%>v NO_COMPOUND ;
        +Pers+Prs+Quot:%>vat NO_COMPOUND ;
 @R.Part.One@           SUPINE_MA_FORMS ;    ! These can be 1st part of a compound
                      : MINE_DERIVATION ;    ! elamine
                      : JA_DERIVATION ;      ! elaja, õpetaja, õpetajanna, õpetajake, õpetajalik, ...
                      : V_DERIVATION ;       ! õpetav, õpetavalt, õpetavus, õpetavam, ...
                      : MATU_DERIVATION ;    ! segamatu, segamatult, segamatus, segamatum, ...
                      : MATA_DERIVATION ;     ! segamata
                      : MUS_DERIVATION ;     ! küllastumus, promoveerumus etc

LEXICON SUPINE_MA_FORMS
             +Pers+Sup:%>ma SUPINE ;
             
LEXICON SUPINE
                      : ILLATIVE ;          ! elama
                      :%>s INESSIVE ;       ! elamas
                      :%>st ELATIVE ;
                      :%>ks TRANSLATIVE ;
                      :%>ta ABESSIVE ;

LEXICON IMPERS    ! impersonal forms for most paradigms
                      :%>T7 IMPERS_DUD ;
                      :%>T7 IMPERS_AKSE ;
                      :»T7 IMPERS_DER  ;         

LEXICON IMPERS_DER    ! impersonal subparadigm-related derivations
                      : TAV_DERIVATION ;       ! elatav, elatavalt, elatavam, elatavaim, ... 
                      : TAMATU_DERIVATION ;       ! elatamatu 
                      : TU_DERIVATION ;       ! elatu, elatult, elatum, elatuim 


LEXICON IMPERS_DUD    ! impersonal forms -[td]ud

      +Impers+Prs+Cond:aks NO_COMPOUND ;
     +Impers+Prs+Imprt:agu NO_COMPOUND ;
   +Impers+Prs+Ind+Neg:a NO_COMPOUND ;
       +Impers+Prs+Prc:av NO_COMPOUND ;
      +Impers+Prs+Quot:avat NO_COMPOUND ;
      +Impers+Prt+Cond:uks NO_COMPOUND ;
   +Impers+Prt+Ind+Aff:i NO_COMPOUND ;
   +Impers+Prt+Ind+Neg:ud NO_COMPOUND ;
       +Impers+Prt+Prc:ud NO_COMPOUND ;
           +Impers+Sup:ama NO_COMPOUND ;

LEXICON IMPERS_AKSE    ! impersonal form -[td0]akse

   +Impers+Prs+Ind+Aff:akse NO_COMPOUND ;

LEXICON INFINITIVE    ! infinitive, gerund (des-form)

                      :%>D7 A_INFINITIVE ;

LEXICON A_INFINITIVE   ! common part of an infinitive, gerund (des-form);
                       ! also alone in a subparadigm of a few old words

 @R.Part.One@             INF_FORMS ;     ! This can be 1st part of a compound
                  +Ger:es NO_COMPOUND ;

LEXICON INF_FORMS
                  +Inf:a GI ;

LEXICON CONDITIONAL    ! conditional present and past  
                 +Cond:ks NO_COMPOUND ;
                 +Cond:ks MARKED_PERSON ;

LEXICON MARKED_PERSON     ! sg3 ~ underspecified is missing from here 
                      :i SG1 ;
                      :i SG2 ;
                      :i PL1 ;
                      :i PL2 ;
                      :i PL3 ;

LEXICON SG1
              +Sg1+Aff:n NO_COMPOUND ;

LEXICON SG2
              +Sg2+Aff:d NO_COMPOUND ;

LEXICON SG3
              +Sg3+Aff: NO_COMPOUND ;

LEXICON PL1
              +Pl1+Aff:me NO_COMPOUND ;

LEXICON PL2
              +Pl2+Aff:te NO_COMPOUND ;

LEXICON PL3
              +Pl3+Aff:d NO_COMPOUND ;

! a verb cannot participate in compounding;
! it has to be turned into a declinable word first
! flag diacritics are set on paths that are NOT changing the verb word class
! the flag diacritic @R.Part.One@ requires the stem to be the first in this word;
! and then @P.Part.Bad@ ensures that no further parts are possible

LEXICON NO_COMPOUND           ! this form cannot participate in a compound word
  @R.Part.One@@P.Part.Bad@ GI ;             

! paradigmatic derivation, i.e. very regular
! @R.Part.One@ means that this is not a latter part of a compound 
! @R.Case.Par@ and @R.Case.Gen@ mean that this is a latter part of a compound 
! and the previous word must be either in Par or Gen for this derivation to be allowable

LEXICON NU_DERIVATION
  @R.Part.One@@P.POS.N@  NU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.N@  NU_DERIVATION_SUF ;
  @R.Part.One@@P.POS.A@  NUD_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  NUD_DERIVATION_SUF ;

LEXICON NU_DERIVATION_SUF
             +Der/nu+N:»nu AASTA ;             ! derivation: elanu etc

LEXICON NUD_DERIVATION_SUF
             +Der/nud+A:»nu TUNTUD ;             ! derivation: elanud etc
             +Der/nud+A:»nud # ;             ! derivation: noninflecting A: elanud 

LEXICON TU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.A@  TU_DERIVATION_SUF ;

LEXICON TU_DERIVATION_SUF  !    NB! the morpheme border + d/t is assigned before this lexicon
             +Der/tu+A:u AASTA ;              ! derivation: elatu etc
             +Der/tud+A:ud # ;             ! derivation: noninflecting A: elatud 

LEXICON MATA_DERIVATION
  @R.Part.One@@P.POS.A@  MATA_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  MATA_DERIVATION_SUF ;

LEXICON MATA_DERIVATION_SUF
           +Der/mata+A:»mata GI ;            ! elamata 

LEXICON MATU_DERIVATION
  @R.Part.One@@P.POS.N@  MATU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MATU_DERIVATION_SUF ;

LEXICON MATU_DERIVATION_SUF
           +Der/matu+A:»matu AASTA ;             ! segamatu etc

LEXICON TAMATU_DERIVATION  !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TAMATU_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  TAMATU_DERIVATION_SUF ;

LEXICON TAMATU_DERIVATION_SUF  !    NB! the morpheme border + d/t is assigned before this lexicon
           +Der/tamatu+A:amatu AASTA ;             ! segatamatu etc

LEXICON V_DERIVATION
  @R.Part.One@@P.POS.A@  V_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  V_DERIVATION_SUF ;

LEXICON V_DERIVATION_SUF
              +Der/v+A:»v MAGUS ;              ! elav, elava etc

LEXICON TAV_DERIVATION   !    NB! the morpheme border + d/t is assigned before this lexicon
  @R.Part.One@@P.POS.A@  TAV_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Par@@P.POS.A@  TAV_DERIVATION_SUF ;

LEXICON TAV_DERIVATION_SUF   !    NB! the morpheme border + d/t is assigned before this lexicon
            +Der/tav+A:av MAGUS ;              ! elatav, elatava etc

LEXICON MINE_DERIVATION
  @R.Part.One@@P.POS.N@  MINE_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MINE_DERIVATION_SUF ;

LEXICON MINE_DERIVATION_SUF
           +Der/mine+N:»mine OLULINE ;         ! elamine

LEXICON JA_DERIVATION
  @R.Part.One@@P.POS.N@  JA_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  JA_DERIVATION_SUF ;

LEXICON JA_DERIVATION_SUF
             +Der/ja+N:»ja AASTA ;             ! elaja, elajat etc
             +Der/ja+N:»jas KOND_DERIVATION ; ! õpetajaskond, õpetajaskondlik, etc

LEXICON IS_DERIVATION
  @R.Part.One@@P.POS.N@  IS_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  IS_DERIVATION_SUF ;

LEXICON IS_DERIVATION_SUF
             +Der/is+N:»is OLULINE ;         ! arveldis

LEXICON MUS_DERIVATION
  @R.Part.One@@P.POS.N@  MUS_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  MUS_DERIVATION_SUF ;

LEXICON MUS_DERIVATION_SUF
             +Der/mus+N:»mus OLULINE ;         ! promoveerumus, elamus, ...

LEXICON US_DERIVATION_VERB         ! us also changes A -> N; directing to that continuation class 
  @R.Part.One@   US_DERIVATION ;   ! no previous part; no restrictions   
  @R.Case.Gen@   US_DERIVATION ;

LEXICON NG_DERIVATION
  @R.Part.One@@P.POS.N@  NG_DERIVATION_SUF ;   ! no previous part; no restrictions   
  @R.Case.Gen@@P.POS.N@  NG_DERIVATION_SUF ;

LEXICON NG_DERIVATION_SUF
             +Der/ng+N:»ng VIRSIK ;         ! annoteering 



