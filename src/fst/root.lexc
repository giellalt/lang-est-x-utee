! -*- mode: lexc; coding: utf-8 -*-

!! !!!Morphology

!! !!Multicharacter symbols

Multichar_Symbols

!! !S√µnaliik / Part of speech

+N !!= * {{@CODE@}}: nimis√µna		/ substantive		
+A !!= * {{@CODE@}}: omaduss√µna		/ adjective		
+Num !!= * {{@CODE@}}: arvs√µna		/ numeral
+Pron !!= * {{@CODE@}}: ases√µna		/ pronoun		
+V !!= * {{@CODE@}}: tegus√µna 		/ verb			
+Adv !!= * {{@CODE@}}: m√§√§rs√µna		/ adverb		
+Interj !!= * {{@CODE@}}: h√º√ºds√µna		/ interjection		
+CC !!= * {{@CODE@}}: rinnastav sides√µna		/ co-ordinating conjunction
+CS !!= * {{@CODE@}}: alistav sides√µna		/ subordinating conjunction		
+Adp !!= * {{@CODE@}}: kaass√µna	/ adposition, i.e. pre/postposition	
+Pref !!= * {{@CODE@}}: prefiks         / prefix				

!! Genitiivatribuut pole eraldi kategooria / No special tag for genitive attribute : ''angoora+N+Sg+Gen''	

!! !Alamkategooriad / Sub-pos categories

+Prop !!= * {{@CODE@}}: p√§risnimi	/ proper name		
+Card !!= * {{@CODE@}}: p√µhiarvs√µna	/ cardinal numeral
+Ord !!= * {{@CODE@}}: j√§rgarvs√µna	/ ordinal numeral

!! !V√µrdlusastmed / Comparation

+Comp !!= * {{@CODE@}}: komparatiiv	/ comparative
+Superl !!= * {{@CODE@}}: superlatiiv	/ superlative

!!‚Ç¨gt-desc hfstol:
!!‚Ç¨ asi: asi+N+Sg+Nom # thing
!!‚Ç¨ Jaan: Jaan+N+Prop+Sg+Nom
!!‚Ç¨ asjalik: asjalik+A+Sg+Nom # serious
!!‚Ç¨ parem: parem+A+Comp+Sg+Nom # better
!!‚Ç¨ parim: parim+A+Superl+Sg+Nom # best
!!‚Ç¨ kaks: kaks+Num+Card+Sg+Nom # two
!!‚Ç¨ teine: teine+Num+Ord+Sg+Nom # second
!!‚Ç¨ see: see+Pron+Sg+Nom # that
!!‚Ç¨ asjastama: asjastama+V+Pers+Sup+Ill # objectify
!!‚Ç¨ asjatult: asjatult+Adv # in vain
!!‚Ç¨ hei: hei+Interj # hi
!!‚Ç¨ ja: ja+CC # and
!!‚Ç¨ et: et+CS # that
!!‚Ç¨ kaudu: kaudu+Adp # via 
!!‚Ç¨ eba-: eba+Pref # un-, non- 

!! !S√µnamuutus / Inflection

!! !K√§√§nds√µna             / Noun

!! !Arv 			/ number

+Sg !!= * {{@CODE@}}: ainsus	/ singular
+Pl !!= * {{@CODE@}}: mitmus	/ plural

!! !K√§√§ne	/ Case

+Nom !!= * {{@CODE@}}: nimetav			/ nominative	
!!‚Ç¨ part: part+N+Sg+Nom
!!‚Ç¨ maja: maja+N+Sg+Nom
!!‚Ç¨ pardid: part+N+Pl+Nom
!!‚Ç¨ majad: maja+N+Pl+Nom

+Gen !!= * {{@CODE@}}: omastav 			/ genitive	
!!‚Ç¨ pardi: part+N+Sg+Gen
!!‚Ç¨ maja: maja+N+Sg+Gen
!!‚Ç¨ partide: part+N+Pl+Gen
!!‚Ç¨ majade: maja+N+Pl+Gen

+Par !!= * {{@CODE@}}: osastav			/ partitive	
!!‚Ç¨ parti: part+N+Sg+Par
!!‚Ç¨ maja: maja+N+Sg+Par
!!‚Ç¨ parte: part+N+Pl+Par
!!‚Ç¨ partisid: part+N+Pl+Par+Use/Rare
!!‚Ç¨ maju: maja+N+Pl+Par
!!‚Ç¨ majasid: maja+N+Pl+Par+Use/Rare

+Ill !!= * {{@CODE@}}: sisse√ºtlev			/ illative	
!!‚Ç¨ parti: part+N+Sg+Ill
!!‚Ç¨ majja: maja+N+Sg+Ill
!!‚Ç¨ pardisse: part+N+Sg+Ill+Use/Hyp
!!‚Ç¨ majasse: maja+N+Sg+Ill+Use/Rare
!!‚Ç¨ partidesse: part+N+Pl+Ill
!!‚Ç¨ majadesse: maja+N+Pl+Ill
!! L√ºhike sisse√ºtlev e suunduv pole eraldi k√§√§ne (''parti'', ''majja'') / Short illative or additive is not a separate case 

+Ine !!= * {{@CODE@}}: sees√ºtlev			/ inessive	
!!‚Ç¨ pardis: part+N+Sg+Ine
!!‚Ç¨ majas: maja+N+Sg+Ine
!!‚Ç¨ partides: part+N+Pl+Ine
!!‚Ç¨ majades: maja+N+Pl+Ine

+Ela !!= * {{@CODE@}}: seest√ºtlev			/ elative	
!!‚Ç¨ pardist: part+N+Sg+Ela
!!‚Ç¨ majast: maja+N+Sg+Ela
!!‚Ç¨ partidest: part+N+Pl+Ela
!!‚Ç¨ majadest: maja+N+Pl+Ela

+All !!= * {{@CODE@}}: alale√ºtlev			/ allative	
!!‚Ç¨ pardile: part+N+Sg+All
!!‚Ç¨ majale: maja+N+Sg+All
!!‚Ç¨ partidele: part+N+Pl+All
!!‚Ç¨ majadele: maja+N+Pl+All

+Ade !!= * {{@CODE@}}: alal√ºtlev			/ adessive	
!!‚Ç¨ pardil: part+N+Sg+Ade
!!‚Ç¨ majal: maja+N+Sg+Ade
!!‚Ç¨ partidel: part+N+Pl+Ade
!!‚Ç¨ majadel: maja+N+Pl+Ade

+Abl !!= * {{@CODE@}}: alalt√ºtlev			/ ablative	
!!‚Ç¨ pardilt: part+N+Sg+Abl
!!‚Ç¨ majalt: maja+N+Sg+Abl
!!‚Ç¨ partidelt: part+N+Pl+Abl
!!‚Ç¨ majadelt: maja+N+Pl+Abl

+Tra !!= * {{@CODE@}}: saav				/ translative	
!!‚Ç¨ pardiks: part+N+Sg+Tra
!!‚Ç¨ majaks: maja+N+Sg+Tra
!!‚Ç¨ partideks: part+N+Pl+Tra
!!‚Ç¨ majadeks: maja+N+Pl+Tra

+Trm !!= * {{@CODE@}}: rajav				/ terminative	
!!‚Ç¨ pardini: part+N+Sg+Trm
!!‚Ç¨ majani: maja+N+Sg+Trm
!!‚Ç¨ partideni: part+N+Pl+Trm
!!‚Ç¨ majadeni: maja+N+Pl+Trm

+Ess !!= * {{@CODE@}}: olev				/ essive	
!!‚Ç¨ pardina: part+N+Sg+Ess
!!‚Ç¨ majana: maja+N+Sg+Ess
!!‚Ç¨ partidena: part+N+Pl+Ess
!!‚Ç¨ majadena: maja+N+Pl+Ess

+Abe !!= * {{@CODE@}}: ilma√ºtlev			/ abessive	
!!‚Ç¨ pardita: part+N+Sg+Abe
!!‚Ç¨ majata: maja+N+Sg+Abe
!!‚Ç¨ partideta: part+N+Pl+Abe
!!‚Ç¨ majadeta: maja+N+Pl+Abe

+Com !!= * {{@CODE@}}: kaasa√ºtlev			/ comitative	
!!‚Ç¨ pardiga: part+N+Sg+Com
!!‚Ç¨ majaga: maja+N+Sg+Com
!!‚Ç¨ partidega: part+N+Pl+Com
!!‚Ç¨ majadega: maja+N+Pl+Com

!! !Verb

!! Verbil on finiitsed (p√∂√∂rdelised) ja infiniitsed (nn k√§√§nduvad) vormid. 
!! P√∂√∂rdelistel vormidel on kategooriad: 
!! tegumood, aeg, k√µneviis, isik, arv, k√µneliik
!! (tegumood ja aeg on ka m√µnedel infiniitsetel vormidel).

!! Verbs have finite (conjugable) and infinite ("declinable") forms.
!! The finite forms have categories: 
!! voice, tense, mood, person, number, negation
!! (some infinite forms also have voice and tense).

!! !P√∂√∂rdelised vormid / Conjugable forms

!! !Tegumood		/ Voice

+Impers !!= * {{@CODE@}}: umbisikuline	/ impersonal
+Pers !!= * {{@CODE@}}: isikuline	/ personal

!! !Aeg			/ Tense

+Prs !!= * {{@CODE@}}: olevik	/ present
+Prt !!= * {{@CODE@}}: minevik	/ past (i.e. not present)

!! Lihtminevik = minevik / Past imperfect = past

!! T√§isminevik / Past perfect: ''olema'' (pres) + ''nud/tud/dud'' (''olen teinud'')

!! Enneminevik / Past pluperfect: ''olema'' (impf) + ''nud/tud/dud'' (''olin teinud'')

!! !K√µneviis		/ Mood

+Ind !!= * {{@CODE@}}: kindel	/ indicative
+Cond !!= * {{@CODE@}}: tingiv	/ conditional
+Imprt !!= * {{@CODE@}}: k√§skiv	/ imperative / jussive
+Quot !!= * {{@CODE@}}: kaudne	/ quotative

!! !P√∂√∂re ja arv	/ Person and number

+Sg1 !!= * {{@CODE@}}: ainsuse 1. p√∂√∂re	/ singular 1st person
+Sg2 !!= * {{@CODE@}}: ainsuse 2. p√∂√∂re	/ singular 2nd person
+Sg3 !!= * {{@CODE@}}: ainsuse 3. p√∂√∂re	/ singular 3rd person
+Pl1 !!= * {{@CODE@}}: mitmuse 1. p√∂√∂re	/ plural   1st person
+Pl2 !!= * {{@CODE@}}: mitmuse 2. p√∂√∂re	/ plural   2nd person
+Pl3 !!= * {{@CODE@}}: mitmuse 3. p√∂√∂re	/ plural   3rd person

!! !K√µneliik		/ Polarity

+Aff !!= * {{@CODE@}}: jaatav k√µne	/ affirmative
+Neg !!= * {{@CODE@}}: eitav k√µne	/ negative

!! !Infiniitsed (nn k√§√§nduvad) verbivormid / Infinite ("declinable") verb forms

+Sup !!= * {{@CODE@}}: ma-tegevusnimi / supine (ma-infinitive)

+Inf !!= * {{@CODE@}}: da-tegevusnimi / infinitive (da-infinitive)a

+Ger !!= * {{@CODE@}}: des-vorm	 / gerund (des-form)	

+Prc !!= * {{@CODE@}}: kesks√µna	 / participle


!! !Kategooriate v√µimalikud kombinatsioonid / Possible combinations of categories

!! allikas / source :
!! [Heiki-Jaan Kaalep. Eesti verbi vormistik. Keel ja kirjandus 1/2015 lk 1-15.|http://kjk.eki.ee/ee/issues/2015/1/582] 

!! The categories are given in the order in which the allomorphs (if they can be
!! distinguished) that represent them are attached to the word stem (note that the
!! treatment of allomorphs is sloppy here). The justification is that the
!! categories are not equal, but form an hierarchy: those closer to the word end 
!! tend to be more optional, more often non-specified.   

!! # __voice:__ personal vs. impersonal (0-morph vs. ''t/d(aiu)''), eg. ''elaks'' vs.
!!  ''elataks'', ''elav'' vs. ''elatav''
!! # __tense:__ present vs. past (0-morph vs. ''s/si/nu''), e.g. ''elan'' vs. ''elasin'',
!!  ''elaks'' vs. ''elanuks''
!! # __mood:__ indicative vs. conditional vs imperative vs quotative
!!  (0-morph vs. ''ks'' vs. ''k/g(ue)'' vs. ''vat'')
!! # __person+number:__ notice that in personal present and past conditional, imperative 
!!  and quotative moods one form is underspecified for person+number
!! # __negation:__ affirmative vs. negative. It manifests itself via
!!  lexical means: it is either present in an exceptional wordform (some forms of
!!  ''olema'') or gets adhered to a form, normally used in affirmative,
!!  from an immediately preceding word ''ei'', ''ega'' or ''√§ra'' (e.g. ''ei elaks'').
!!  The only case when negation has a dedicated form of its own is impersonal
!!  present indicative negative (e.g. ''ei elata'').  

!! The forms of negative words ''pole'' and ''√§ra'' are included in order to capture really all 
!!  the combinations; 
!! also note that for ''olema'', there are some constellations of categories that are the same for
!! a wordform beginning with ''ole-'' and ''pole-'' 

!! !---- Personal finite forms ----

!!‚Ç¨ elan: elama+V+Pers+Prs+Ind+Sg1+Aff
!!‚Ç¨ elad: elama+V+Pers+Prs+Ind+Sg2+Aff
!!‚Ç¨ elab: elama+V+Pers+Prs+Ind+Sg3+Aff
!!‚Ç¨ elame: elama+V+Pers+Prs+Ind+Pl1+Aff
!!‚Ç¨ elate: elama+V+Pers+Prs+Ind+Pl2+Aff
!!‚Ç¨ elavad: elama+V+Pers+Prs+Ind+Pl3+Aff

!! \\
!!‚Ç¨ ela: elama+V+Pers+Prs+Ind+Neg
!!‚Ç¨ pole: olema+V+Pers+Prs+Ind+Neg 
!!‚Ç¨ ole: olema+V+Pers+Prs+Ind+Neg 

!! NB! ''ei ole'' = ''pole''


!!‚Ç¨ elaksin: elama+V+Pers+Prs+Cond+Sg1+Aff
!!‚Ç¨ elaksid: elama+V+Pers+Prs+Cond+Sg2+Aff
!!‚Ç¨ elaks: elama+V+Pers+Prs+Cond
!!‚Ç¨ elaksime: elama+V+Pers+Prs+Cond+Pl1+Aff
!!‚Ç¨ elaksite: elama+V+Pers+Prs+Cond+Pl2+Aff
!!‚Ç¨ elaksid: elama+V+Pers+Prs+Cond+Pl3+Aff

!! \\
!!‚Ç¨ poleksin: olema+V+Pers+Prs+Cond+Sg1+Neg
!!‚Ç¨ poleksid: olema+V+Pers+Prs+Cond+Sg2+Neg
!!‚Ç¨ poleks: olema+V+Pers+Prs+Cond+Neg
!!‚Ç¨ poleksime: olema+V+Pers+Prs+Cond+Pl1+Neg
!!‚Ç¨ poleksite: olema+V+Pers+Prs+Cond+Pl2+Neg
!!‚Ç¨ poleksid: olema+V+Pers+Prs+Cond+Pl3+Neg

!! \\
!!‚Ç¨ ela: elama+V+Pers+Prs+Imprt+Sg2
!!‚Ç¨ elagu: elama+V+Pers+Prs+Imprt
!!‚Ç¨ elagem: elama+V+Pers+Prs+Imprt+Pl1
!!‚Ç¨ elage: elama+V+Pers+Prs+Imprt+Pl2

!! \\
!!‚Ç¨ √§ra: √§ra+V+Pers+Prs+Imprt+Sg2+Neg
!!‚Ç¨ √§rgu: √§ra+V+Pers+Prs+Imprt+Neg
!!‚Ç¨ √§rgem: √§ra+V+Pers+Prs+Imprt+Pl1+Neg
!!‚Ç¨ √§rme: √§ra+V+Pers+Prs+Imprt+Pl1+Neg+Use/Rare
!!‚Ç¨ √§rge: √§ra+V+Pers+Prs+Imprt+Pl2+Neg

!! \\
!!‚Ç¨ elavat: elama+V+Pers+Prs+Quot
!!‚Ç¨ polevat: olema+V+Pers+Prs+Quot+Neg

!! \\ 
!!‚Ç¨ elasin: elama+V+Pers+Prt+Ind+Sg1+Aff
!!‚Ç¨ elasid: elama+V+Pers+Prt+Ind+Sg2+Aff
!!‚Ç¨ elas: elama+V+Pers+Prt+Ind+Sg3+Aff
!!‚Ç¨ elasime: elama+V+Pers+Prt+Ind+Pl1+Aff
!!‚Ç¨ elasite: elama+V+Pers+Prt+Ind+Pl2+Aff
!!‚Ç¨ elasid: elama+V+Pers+Prt+Ind+Pl3+Aff

!! \\
!!‚Ç¨ elanud: elama+V+Pers+Prt+Ind+Neg
!!‚Ç¨ polnud: olema+V+Pers+Prt+Ind+Neg
!!‚Ç¨ olnud: olema+V+Pers+Prt+Ind+Neg

!! NB! ''ei olnud'' = ''polnud''


!!‚Ç¨ elanuksin: elama+V+Pers+Prt+Cond+Sg1+Aff
!!‚Ç¨ elanuksid: elama+V+Pers+Prt+Cond+Sg2+Aff
!!‚Ç¨ elanuks: elama+V+Pers+Prt+Cond
!!‚Ç¨ elanuksime: elama+V+Pers+Prt+Cond+Pl1+Aff
!!‚Ç¨ elanuksite: elama+V+Pers+Prt+Cond+Pl2+Aff
!!‚Ç¨ elanuksid: elama+V+Pers+Prt+Cond+Pl3+Aff

!! \\
!!‚Ç¨ polnuksin: olema+V+Pers+Prt+Cond+Sg1+Neg
!!‚Ç¨ polnuksid: olema+V+Pers+Prt+Cond+Sg2+Neg
!!‚Ç¨ polnuks: olema+V+Pers+Prt+Cond+Neg
!!‚Ç¨ polnuksime: olema+V+Pers+Prt+Cond+Pl1+Neg
!!‚Ç¨ polnuksite: olema+V+Pers+Prt+Cond+Pl2+Neg
!!‚Ç¨ polnuksid: olema+V+Pers+Prt+Cond+Pl3+Neg

!! \\
!! elanud: elama+V+Pers+Prt+Imprt: 1.11.2016 ei genereerita ega tunta √§ra / not recognized nor generated as of Nov 1, 2016
!!‚Ç¨ √§rnud: √§ra+V+Pers+Prt+Imprt+Neg

!! \\
!!‚Ç¨ elanuvat: elama+V+Pers+Prt+Quot
!!‚Ç¨ polnuvat: olema+V+Pers+Prt+Quot+Neg

!! !---- Personal infinite forms ----

!!‚Ç¨ elav: elama+V+Pers+Prs+Prc
!!‚Ç¨ elanud: elama+V+Pers+Prt+Prc

!!          (on, oli, ...) + V+Pers+Prt+Prc = some analytical personal form 

!!‚Ç¨ elama: elama+V+Pers+Sup+Ill
!!‚Ç¨ elamas: elama+V+Pers+Sup+Ine
!!‚Ç¨ elamast: elama+V+Pers+Sup+Ela
!!‚Ç¨ elamaks: elama+V+Pers+Sup+Tra
!!‚Ç¨ elamata: elama+V+Pers+Sup+Abe

!! !---- Impersonal finite forms ----

!!‚Ç¨ elatakse: elama+V+Impers+Prs+Ind+Aff
!!‚Ç¨ elata: elama+V+Impers+Prs+Ind+Neg 
                
!!‚Ç¨ polda: olema+V+Impers+Prs+Ind+Neg 
!!‚Ç¨ olda: olema+V+Impers+Prs+Ind+Neg 

!! NB! ei olda = polda

!!‚Ç¨ elataks: elama+V+Impers+Prs+Cond
!!‚Ç¨ poldaks: olema+V+Impers+Prs+Cond+Neg

!! \\
!!‚Ç¨ elatagu: elama+V+Impers+Prs+Imprt
!!‚Ç¨ √§rdagu: √§ra+V+Impers+Prs+Imprt+Neg

!! \\
!!‚Ç¨ elatavat: elama+V+Impers+Prs+Quot
!!‚Ç¨ poldavat: olema+V+Impers+Prs+Quot+Neg

!! \\
!!‚Ç¨ elati: elama+V+Impers+Prt+Ind+Aff
!!‚Ç¨ elatud: elama+V+Impers+Prt+Ind+Neg 

!!‚Ç¨ poldud: olema+V+Impers+Prt+Ind+Neg
!!‚Ç¨ oldud: olema+V+Impers+Prt+Ind+Neg

!! NB! ei oldud = poldud

!!‚Ç¨ elatuks: elama+V+Impers+Prt+Cond
!!‚Ç¨ polduks: olema+V+Impers+Prt+Cond+Neg

!! !---- Impersonal infinite forms ----

!!‚Ç¨ elatav: elama+V+Impers+Prs+Prc
!!‚Ç¨ elatud: elama+V+Impers+Prt+Prc

!! (''on'', ''oli'', ...) + V+Impers+Prt+Prc = some analytical personal form 


!!‚Ç¨ elatama: elama+V+Impers+Sup

!! !---- Infinite forms with no voice category ----

!!‚Ç¨ elada: elama+V+Inf
!!‚Ç¨ elades: elama+V+Ger

!! !Exceptional cases:

!! personal present (Prs not implemented????), 3 words:  ''kuulukse'', ''tunnukse'', ''n√§ikse''

!!‚Ç¨ kuulukse: kuulukse+V

!! !Negation verb 

!!‚Ç¨ ei: ei+V+Neg

!! Analytical forms (''olen elanud'', ''olin elanud'', ''oleksin elanud'', ''ei olnud elanud'',
!! ''ei olnuks elanud'' etc) are not treated here...

!! !Partikkel -gi/-ki / Particle -gi/-ki

+Foc/gi !!= * {{@CODE@}}
!!‚Ç¨ pulkki: pulk+N+Sg+Nom+Foc/gi

!! ! Kasutust (sagedust jm) iseloomustavad m√§rgendid / Usage-related tags

+Emph !!= * {{@CODE@}}: long inflectional forms of personal pronouns ''mina'', ''sina'', ''tema'', ''meie'', ''teie'', ''nemad''
!!‚Ç¨ temale: tema+Pron+Sg+All+Emph

!! !Paralleelvormide erinevat kasutussagedust iseloomustavad / Usage info for parallel forms (either correct according to the norm, or incorrect)

+Use/Rare !!= * {{@CODE@}}: norm, but rare 
!!‚Ç¨ puusid: puu+N+Pl+Par+Use/Rare

+Use/Hyp !!= * {{@CODE@}}:  norm, but so rare that norm is probaly wrong 
!!‚Ç¨ tiivasse: tiib+N+Sg+Ill+Use/Hyp

+Use/NotNorm !!= * {{@CODE@}}:  not norm, but sometimes used 
!!‚Ç¨ p√∂idlates: p√∂ial+N+Pl+Ine+Use/NotNorm

+Use/CommonNotNorm !!= * {{@CODE@}}:  not norm, and used more than norm 
!!‚Ç¨ k√∂√∂ment: k√∂√∂men+N+Sg+Par+Use/CommonNotNorm

!! Oletatav anal√º√ºs / Guessed analysis
+Guess !!= * {{@CODE@}}: guessed analysis

!! !Tuletus / Derivation

!! !Eesliited on harvad, aga ikkagi ... / Prefixes are seldom used, but still...

+Pref !!= * {{@CODE@}}
!!‚Ç¨ taasriigistama: taas+Pref#riigistama+V+Pers+Sup+Ill
!!‚Ç¨ reinvesteerima: re+Pref#investeerima+V+Pers+Sup+Ill

!! !J√§relliited / Suffixes

!! V --> N

+Der/mine !!= * {{@CODE@}}
!!‚Ç¨ jahumine: jahuma+V+Der/mine+N+Sg+Nom

+Der/ja !!= * {{@CODE@}}
!!‚Ç¨ jahuja: jahuma+V+Der/ja+N+Sg+Nom

+Der/nu !!= * {{@CODE@}}
!!‚Ç¨ elanu: elama+V+Der/nu+N+Sg+Nom

+Der/mus !!= * {{@CODE@}}
!!‚Ç¨ k√ºllastumus: k√ºllastuma+V+Der/mus+N+Sg+Nom 

+Der/ng !!= * {{@CODE@}}
!!‚Ç¨ devalveering: devalveerima+V+Der/ng+N+Sg+Nom 

+Der/is !!= * {{@CODE@}}
!!‚Ç¨ arveldis: arveldama+V+Der/is+N+Sg+Nom

!! V --> A

+Der/v !!= * {{@CODE@}}
!!‚Ç¨ jahuv: jahuma+V+Der/v+A+Sg+Nom

+Der/tav !!= * {{@CODE@}}
!!‚Ç¨ √∂eldav: √ºtlema+V+Der/tav+A+Sg+Nom

+Der/nud !!= * {{@CODE@}}
!!‚Ç¨ elanud: elama+V+Der/nud+A+Sg+Nom

+Der/mata !!= * {{@CODE@}}
!!‚Ç¨ elamata: elama+V+Der/mata+A

+Der/matu !!= * {{@CODE@}}
!!‚Ç¨ segamatu: segama+V+Der/matu+A+Sg+Nom

+Der/tamatu !!= * {{@CODE@}}
!!‚Ç¨ m√µeldamatu: m√µtlema+V+Der/tamatu+A+Sg+Nom

+Der/tu !!= * {{@CODE@}}
!!‚Ç¨ elatu: elama+V+Der/tu+A+Sg+Nom

+Der/tud !!= * {{@CODE@}}
!!‚Ç¨ elatud: elama+V+Der/tud+A

!! N --> A

+Der/lik !!= * {{@CODE@}}
!!‚Ç¨ kotkalik: kotkas+N+Der/lik+A+Sg+Nom

+Der/line !!= * {{@CODE@}}
!!‚Ç¨ p√µ√µsaline: p√µ√µsas+N+Der/line+A+Sg+Nom
!!‚Ç¨ onomastiline: onomastika+N+Der/line+A+Sg+Nom
!!‚Ç¨ apooriline: apooria+N+Der/line+A+Sg+Nom

+Der/ne !!= * {{@CODE@}}

!! A --> Adv

+Der/lt !!= * {{@CODE@}}
!!‚Ç¨ roosalt: roosa+A+Der/lt+Adv

+Der/sti !!= * {{@CODE@}}
!!‚Ç¨ valusasti: valus+A+Der/sti+Adv

+Der/ini !!= * {{@CODE@}}
!!‚Ç¨ paremini: parem+A+Comp+Der/ini+Adv

!! A --> A

+Der/m !!= * {{@CODE@}}
!!‚Ç¨ valusam: valus+A+Der/m+A+Comp+Sg+Nom

+Der/im !!= * {{@CODE@}}
!!‚Ç¨ valusaim: valus+A+Der/im+A+Superl+Sg+Nom

!! N --> N

+Der/nna !!= * {{@CODE@}}
!!‚Ç¨ √µmblejanna: √µmbleja+N+Der/nna+N+Sg+Nom

+Der/kond !!= * {{@CODE@}}
!!‚Ç¨ vaatajaskond: vaatama+V+Der/ja+N+Der/kond+N+Sg+Nom

+Der/ist !!= * {{@CODE@}}
!!‚Ç¨ kapitalist: kapitalism+N+Der/ist+N+Sg+Nom

!! A --> N, V --> N

+Der/us !!= * {{@CODE@}}
!!‚Ç¨ porisus: porine+A+Der/us+N+Sg+Nom
!!‚Ç¨ √µpetus: √µpetama+V+Der/us+N+Sg+Nom

!! N --> Adv

+Der/ti !!= * {{@CODE@}}
!!‚Ç¨ lauati: laud+N+Der/ti+Adv

+Der/lane !!= * {{@CODE@}}:  helveetslane:Helveetsia+N+Prop+Der/lane+N+Sg+Nom
!!‚Ç¨ firenzelane: Firenze+N+Prop+Der/lane+N+Sg+Nom
!!‚Ç¨ helveetslane: Helveetsia+N+Prop+Der/lane+N+Sg+Nom

!! N --> N, A --> A

+Dim/ke !!= * {{@CODE@}}:  diminutive 
!!‚Ç¨ p√µ√µsake: p√µ√µsas+N+Dim/ke+Sg+Nom

+Der/minus !!= * {{@CODE@}}:  shortening stem 
!!‚Ç¨ vaatamis-: vaatamine+N+Der/minus
!!‚Ç¨ astraal-: astraalne+A+Der/minus

!! Num --> N

+Der/kas !!= * {{@CODE@}}
!!‚Ç¨ kolmekas: kolm+Num+Card+Der/kas+N+Sg+Nom


!! !Muud / Other tags
!! Copied from Sami root.lexc

! +Der  !!‚âà * @CODE@: Keelest s√µltumatutes filtrites? / In language-independent filters ?
!! !Symbols that need to be escaped on the lower side (towards twolc):

 ¬ª7     !! * ¬ª
 ¬´7     !! * ¬´
 %[%>%] !! * > (escaped with square brackets, to avoid collision with > as morpheme boundary)
 %[%<%] !! * < (escaped with square brackets, to avoid collision with < as morpheme boundary)


'  !!‚âà * @CODE@  7 is the real apostrophe ?? 
! "üò±"  !! {{@CODE@}} is used in some languages to prefix # in dynamic compounds

+Use/Circ !!= * {{@CODE@}}: for arabic and roman numerals; 
+Use/PMatch !!= * {{@CODE@}}: for tokenisation with pmatch
+Use/-PMatch !!= * {{@CODE@}}: for tokenisation with pmatch

+ABBR !!= * {{@CODE@}}: L√ºhend / Abbreviation
+Symbol !!‚âà * @CODE@ = independent symbols in the text stream, like ¬£, ‚Ç¨, ¬©
+ACR !!= * {{@CODE@}}: Suurt√§htl√ºhend / Acronym
+CLB !!= * {{@CODE@}}: Osalause piir (punkt, koma) / Clause border (full stop, comma..)
+PUNCT !!= * {{@CODE@}}: Kirjavahem√§rk / Punctuation
+LEFT !!= * {{@CODE@}}: Kirjavahem√§rgi asetus / Punctuation orientation
+RIGHT !!= * {{@CODE@}}: Kirjavahem√§rgi asetus / Punctuation orientation
+Err/Orth !!= * {{@CODE@}}: Ortograafiaviga (genereeritud failide s√ºmbol)/ orthography error

!! !!Flag diacritics
!! Use the following flag diacritics to control downcasing of derived proper
!! nouns (e.g. Finnish Pariisi -> pariisilainen). See e.g. North S√°mi for how to use
!! these flags. There exists a ready-made regex that will do the actual down-casing
!! given the proper use of these flags.
 @U.Cap.Obl@        !!‚âà | @CODE@ | Allowing downcasing of derived names: deatnulasj.
 @U.Cap.Opt@        !!‚âà | @CODE@ | Allowing downcasing of derived names: deatnulasj.
 @C.Cap@

!! These are used for limiting the number of components in a compound word
!! (the compound transducer is cyclic, but in reality there is a limit to the length of words)
 @D.Part@        !!‚âà | @CODE@ | No part of a compound should have been encountered yet
 @P.Part.One@        !!‚âà | @CODE@ | Indicate that this could be the first part of a compound
 @R.Part.One@        !!‚âà | @CODE@ | Require that the first part has been encountered; if a lemma has it, it means that the lemma cannot be part2 or part3 of a compound
 @D.Part.One@        !!‚âà | @CODE@ | Require that the first part has NOT been encountered 
 @P.Part.Two@        !!‚âà | @CODE@ | Indicate that this could be the second part of a compound
 @R.Part.Two@        !!‚âà | @CODE@ | Require that the second part has been encountered
 @P.Part.Three@      !!‚âà | @CODE@ | Indicate that this could be the third part of a compound
 @P.Part.Bad@        !!‚âà | @CODE@ | Indicate that this cannot be a part of a compound
 @C.Part@

!! POS is used for filtering derivations and compounds
 @R.POS.A@
 @P.POS.A@
 @R.POS.AComp@
 @P.POS.AComp@
! @R.POS.ASuperl@ ! Superl = Comp in compounding
! @P.POS.ASuperl@ ! Superl = Comp in compounding
 @R.POS.N@
 @P.POS.N@
 @R.POS.NProp@
 @P.POS.NProp@
 @D.POS.NProp@
 @R.POS.GA@
 @P.POS.GA@
 @R.POS.IntNum@
 @P.POS.IntNum@
 @R.POS.NonIntNum@
 @P.POS.NonIntNum@
 @R.POS.NumCardCompound@   !!‚âà | @CODE@ | compound numeral (viis+sada - five hundred)
 @P.POS.NumCardCompound@
 @R.POS.NumOrd@
 @P.POS.NumOrd@
 @R.POS.Num@       ! number
 @P.POS.Num@
 @R.POS.V@
 @P.POS.V@
 @R.POS.Interj@
 @P.POS.Interj@
 @R.POS.Adv@
 @P.POS.Adv@
 @R.POS.Pron@
 @P.POS.Pron@
 @R.POS.Pref@
 @P.POS.Pref@
 @R.POS.ACR@
 @P.POS.ACR@
 @D.POS.ACR@
 @R.POS.ACRMinus@   ! acronym ending with a hyphen; only allowed in a compound word
 @P.POS.ACRMinus@
 @D.POS.ACRMinus@
 @C.POS@
 @R.POS.Guess@
 @P.POS.Guess@

!! Case is used for filtering derivations and compounds
 @R.Case.Nom@
 @P.Case.Nom@
 @D.Case.Nom@   ! previous word should not be SgNom; if a lemma has it, then this lemma is restricted in compounding productivity
 @R.Case.Gen@
 @P.Case.Gen@
 @R.Case.Par@
 @P.Case.Par@
 @R.Case.Ine@  ! inessive case; flag only used for noninflecting adjectives (punnis)
 @P.Case.Ine@
 @R.Case.Sem@  ! semantic (aka oblique) case, incl inessive
 @P.Case.Sem@
 @D.Case.PlNom@  ! in compounding, +Pl is relevant for +Nom only
 @P.Case.PlNom@
 @R.Case.Short@  ! no case because stem is shortened (astraal-, nais-)
 @P.Case.Short@
 @D.Case@
 @C.Case@  ! initialise case flag

!! Remember there has been some derivation from A or N; for filtering compounds
!! derivation from V is called paradigmatic and does not result in Der; just a new POS... 
 @P.Der.kas@    ! here not really derivation, but adjective type
 @D.Der.kas@
 @R.Der.us@     ! Der.us means that POS of the derivation base is irrelevant
 @P.Der.us@
 @D.Der.us@
 @P.Der.ist@    ! Der/ist does not really affect the compounding
 @P.Der.m@      ! Der/m (A+Comp) does not really affect the compounding
 @P.Der.im@     ! Der/im (A+Superl) does not really affect the compounding
 @P.Der.N@
 @D.Der.N@
 @P.Der.A@
 @D.Der.A@
 @P.Der.Adv@
 @D.Der.Adv@
 @D.Der@
 @C.Der@

!! Remember the stem type; for filtering compounds 
 @P.Stem.Single@   ! does not participate in compounding (used for words that would normally compound)   
 @R.Stem.Single@
 @D.Stem.Single@   ! more restrictive than @R.Part.One@
 @P.Stem.Final@   ! may be only the last part: alam+N, √ºlem+N, vana+N 
 @R.Stem.Final@
 @D.Stem.Final@
 @P.Stem.Nom@   ! Even if this word is Sg Nom, smth may follow it 
 @R.Stem.Nom@   ! Previous word should have been in Sg Nom
 @D.Stem.Nom@   ! Previous word should not have been in Sg Nom
 @P.Stem.Verb@   ! to remember that A or N was derived from verb 
 @R.Stem.Verb@
 @D.Stem.Verb@

 @P.Stem.ise@   ! enese + anything
 @R.Stem.ise@
 @D.Stem.ise@
 @P.Stem.√ºle@   ! √ºle + geoname + line (√ºle+siberi+line)
 @R.Stem.√ºle@
 @D.Stem.√ºle@
 @P.Stem.v√§he@   ! v√§he, puht + adjective
 @R.Stem.v√§he@
 @D.Stem.v√§he@
 @P.Stem.topelt@   ! Adv topelt et al;  Adv + anything
 @R.Stem.topelt@
 @D.Stem.topelt@
 @P.Stem.Guessed@   ! stem is guessed, it is not from the lexicon
 @R.Stem.Guessed@
 @D.Stem.Guessed@   ! if a lemma has it, then this lemma is restricted in its ability to be the last part of a compound 
 @C.Stem@

!! A special condition that is used for filtering derivations and compounds
 @P.NeedAdj.On@         ! +A or +Der/A
 @R.NeedAdj@
 @D.NeedAdj@
 @C.NeedAdj@
 @P.NeedNoun.On@         ! +N or +Der/N
 @R.NeedNoun@
 @D.NeedNoun@
 @C.NeedNoun@
 @P.NeedPartThree.On@   ! Pref + Adv needs + x
 @R.NeedPartThree@
 @D.NeedPartThree@
 @C.NeedPartThree@
 @P.NeedDerNe.On@         ! +Der/ne 5-autone
 @R.NeedDerNe@
 @D.NeedDerNe@
 @C.NeedDerNe@

 @P.Pmatch.Backtrack@  ! for tokenizer pmhfst-file; it gets converted to pmatch variables in pmhfst compilation process
 @P.Pmatch.Loc@  ! for tokenizer pmhfst-file; it gets converted to pmatch variables in pmhfst compilation process

!! Tokeniser 
! ... may backtrack to give alternative readings to dot-ending stuff
! 6.   may be: 6. Num Ord or 6. Num Ord . CLB   or 6 Num Card .   CLB
! 6... may be:               6. Num Ord ... CLB or 6 Num Card ... CLB
! jne.   may be: jne ABBR or jne ABBR .   CLB
! jne... may be:             jne ABBR ... CLB
! so when there is more than one dot, the analysis without CLB is not possible
! that means that we must remember that a dot has been processed, and block the path if more dots follow
 @P.Dot.One@   ! there was a dot
 @D.Dot.One@   ! there should not have been a dot

!! Guesser
! Pay attention to the stress
 @P.Stress.On@    ! mark this as a stressed syllable 
 @R.Stress.On@    ! need the previous syllable to be a stressed one 
 @D.Stress.On@    ! not used? 
 @P.Stress.Off@   ! mark this as a syllable following a stressed one
 @R.Stress.Off@   ! need the previous syllable to be an UNstressed one 
 @D.Stress.Off@   ! not used? 
 @D.Stress@       ! no stress on a previous syllable, i.e. there is no previous syllable
 @C.Stress@       ! not used ?

! Pay attention to 'grade' (I, II or III), expressed via 'syllable' length
! It co-occurs with only stressed syllables
 @P.Len.I@
 @R.Len.I@
 @D.Len.I@
 @P.Len.II@
 @R.Len.II@
 @D.Len.II@
 @P.Len.III@
 @R.Len.III@
 @D.Len.III@
 @C.Len@

!! 

! Morphophonology

! Triggers

 %{W%} ! The weak grade trigger never appears on the surface side.
 %{F%} ! The foreign words flag never appears on the surface side.
 %{S%}  ! Strong stem trigger (for short illatives only) never appears on the surface side.
 %{E%}  ! Schwa trigger (for -el/er words only) never appears on the surface side.
 %{id%}   ! lexicon flag for pl part possible ending (esseesid/esseid)
 %{stemill%} ! lexicon flag for short sg ill, formed by stem change (j√µgi-j√µkke)
 %{dbl%}     ! double the previous letter (kabinet - kabinetti)
 %{no.apo%}  ! trigger for deleting apostrofy (used for foreign word inflections) from sg nom

! usage tag; never on the surface side

 %{rare%}       ! rare or less used form (norm) ; e.g. kingasid

 %>     ! Inflectional suffix boundary
 ¬ª      ! Derivational suffix boundary
 #      ! Word boundary for both lexicalised and dynamic compounds
 -      ! Final hyphen for shortened stems

 %{apo%}   ! Apostrophe is needed separating inflectional affix from foreign word lemma

!Morphophonemes

! short stops
 G1 ! 0 g k   lagi lae lakke
 B1 ! 0 b p   tuba toa tuppa
 D1 ! 0 d t l n r  rida rea ritta talla kanna korra

! orthographic convention: after voiceless (e.g. s or h), gbd is written as kpt
! e.g. √µhk-√µhu, vask-vase

 K1 ! k 0
 P1 ! p 0 ?? EKK: never happens ??
 T1 ! t 0

 B2 ! b v p  t√µbi t√µve t√µppe

 K2 ! k g
 P2 ! p b
 T2 ! t d
 
 G3 ! 0 g  Riia Riiga
 D3 ! 0 d  Rannu Randu
 G4 ! g k  T√ºrgi T√ºrki
 D4 ! d t  Leedu Leetu
 B4 ! b p  Abja Apja

 S1 ! 0 s

! Unstressed syllable vowels disappear...
A2   ! a 0  valvas valvsa
E2   ! e 0  laulev laulva
I2   ! i 0  √µilis √µilsa
U2   ! u 0  hirmus hirmsa

! Stem vowels ... ad hoc ...
E1    ! e 0  soolase, raudset  soolas t
I1    ! i    katkisil
A1    ! a    pingsa

!  Few words...

E4   ! e j   aeg aja 
J1   ! i j ahi ahju

 H3 ! h 0  soo sohu
 Y3 ! vowel vowel  soo sohu

A6   ! hea pea
E6   ! hea pea
√ñ6   ! √º√º √∂√∂ √µ  s√∂√∂ma - s√º√ºa - s√µi
O6   ! uu oo √µ  tooma - tuua - t√µi

M1   ! m n  lumi lund
N1   ! 0 n l√§√§s l√§√§ne, kolmas kolmanda
K3   ! k 0  vennas vennakse
  
! Stem vowels for verbs of some inflectional types

A7   ! a 0  naerab naerma
E7   ! e 0  jookseb jooksma

! verb affixes have k-g and t-d-0 alternations
 G7 ! g k
 D7 ! d t 0
 T7 ! d t 0

! To form past indicative forms and make them pronouncable
 I7 ! i 0
 S7 ! s 0

! Stem vowels for singular and plural
! For plural partitive, the form is generated either with sg vowel + sid or plural vowel + 0

 %{sg.a%}  ! a 0  Stem vowel tag in lexicon  t√ºnga
 %{sg.e%}  ! e 0  Stem vowel tag in lexicon   neeme
 
! default sg stem vowel for most types
 %{sg.i%}  ! i 0  Stem vowel tag in lexicon
 
 %{sg.u%}  ! u 0  Stem vowel tag in lexicon   koonu

 %{pl.i%}  ! i 0  Stem vowel tag in lexicon   kingi
 %{pl.e%}  ! e 0  Stem vowel tag in lexicon   t√µuge
 %{pl.u%}  ! u 0  Stem vowel tag in lexicon   laudu

! Inflectional affixes sid/id/vowel sse/0 having the same grammatical meaning
! defined un-naturally letter by letter

 %{i%}  ! i 0
 %{d%}  ! d 0
 
 %{s%}  ! s 0
 %{e%}  ! s 0

!! Legitimate strings that are not words: numbers, acronyms, ...

Definitions

! Capital letters are used for making acronyms
! An acronym is inflected according to its pronunciation
! An acronym can be read as word (e.g. NATO), or letter-by-letter (e.g. ABC)
! In the latter case, the reading of the last letter determines the inflectional type
CapVow = A|E|I|O|U|√ï|√ú|√ñ|√Ñ|Y ;
CapConsv  = B|C|D|G|H|K|P|Q|≈†|≈Ω|T|V|W ;  ! bee, tsee, ...
CapConsc  = F|J|L|M|N|R|S|Z|X ;          ! eff, jott, ell, ...
CapCons = CapConsv | CapConsc ;
CapLetter = CapVow | CapCons ;
! word end pronouncable pairs where last C is CapConsv
! so the word belongs to FIAT type (instead of ARMEE)
CapPairC = [{HH}|{KK}|{PP}|{≈†≈†}|{TT}|
           {HK}|{HP}|{HT}|{HV}|{VT}|{FT}|
           {LP}|{LK}|{LT}|{LB}|{LG}|{LD}|
           {RP}|{RK}|{RT}|{RB}|{RG}|{RD}|
           {MP}|{NK}|{NT}|{MB}|{NG}|{ND}|
           {SP}|{SK}|{ST}|
           {LC}|{NC}|{RC}|{CK}|{SH}|{WK}|{TH}
] ; 
CapNonPairC = [[CapCons CapConsv] - CapPairC] ;

!Cap = A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|≈†|Z|≈Ω|T|U|V|W|√ú|√ñ|√Ñ|X|Y ;

!! !!Lexicons  

LEXICON Root !!= * {{@CODE@}} is the starting point of everything

!! This is actually a loop, formed after applying Kleene star 
!! concatenation on the simple word transducer with itself.
!! When looping, it remembers which paths it has taken in every loop. 
!! For remembering it sets up flags on the path:
!! 1. flag the first 3 loops (thus forbidding any more loops)
!! 2. in every loop, flag the POS and inflection, to be used in the following loop
!!
!! Different paths may result in the same output string, 
!! e.g. mootori√µlilik = (mootori+√µli)+lik and (mootori)+(√µli+lik)

@D.Part@@P.Part.Bad@ Single ;      !! strictly simplex word; cannot be a part of a compound
@D.Part@@P.Part.One@ First ;       !! a simplex word, or the first part of a compound

@R.Part.One@@P.Part.Two@ Nonfirst "weight: 10" ;                      ! second loop; 
@R.Part.Two@@P.Part.Three@@C.NeedPartThree@ Nonfirst "weight: 10" ;   ! third loop; 

! only two components allowed, thus must bypass LEXICON First:
! number starting a word is a special case...
@D.Part@@P.Part.Two@@P.NeedPartThree.On@@P.POS.Num@ CardinalNumbersInCompBeg ;   !!= * {{@CODE@}} 5-autone etc

LEXICON Nonfirst

! D.Der means that the previous word should not be a derived one;
! it may be too restrictive here...
! previous word should not be allowed only single, nor one that can be only final
  @D.Stem.Single@@D.Stem.Final@@C.Cap@@D.Case.PlNom@@D.Der.N@@D.Der.A@@D.Der.Adv@ Latter ;   ! a latter part

LEXICON Single     !! strictly simplex words; cannot be a part of a compound

  Abbreviations ;             !!= * {{@CODE@}}
  Adpositions ;             !!= * {{@CODE@}}
  Conjunctions ;            !!= * {{@CODE@}}
  NoninflectingVerbs ;      !!= * {{@CODE@}}
  CardinalNumbersSingle ;      !!= * {{@CODE@}}
  OrdinalNumbers ;          !!= * {{@CODE@}}
  Punctuation ;          !!= * {{@CODE@}}
  Symbols     ;          !!= * {{@CODE@}}

LEXICON First     !!= * {{@CODE@}} a simplex word or the first part of a compound 

  @P.POS.A@ Adjectives ;              !!= * {{@CODE@}}
  @P.POS.A@ NoninflectingAdjectives ; !!= * {{@CODE@}}
  @P.POS.AComp@ ComparativeAdjectives ;   !!= * {{@CODE@}}
  @P.POS.AComp@ SuperlativeAdjectives ;   !!= * {{@CODE@}}  ! Superl = Comp in compounding
  @P.POS.Adv@ Adverbs ;                 !!= * {{@CODE@}}
  @P.POS.N@ Nouns ;                   !!= * {{@CODE@}}
  @P.POS.NProp@@U.Cap.Obl@ ProperNouns ; !!= * {{@CODE@}}  ! always uppercase
  @P.POS.NProp@@U.Cap.Opt@ ProperNouns ; !!= * {{@CODE@}}  ! downcased for derivations
  @P.POS.IntNum@ IntegerNumerals ;                !!= * {{@CODE@}}
  @P.POS.NonIntNum@ NonIntegerNumerals ;                !!= * {{@CODE@}}
  @P.POS.NumOrd@ OrdinalNumerals ;                !!= * {{@CODE@}}
  @P.POS.Pron@ Pronouns ;                !!= * {{@CODE@}}
  @P.POS.V@ Verbs ;                   !!= * {{@CODE@}}
  @P.POS.Interj@ Interjections ;           !!= * {{@CODE@}}
  @P.POS.GA@@P.Case.Gen@ GenitiveAttributes ;      !!= * {{@CODE@}}
  @P.POS.Pref@ Prefixes ;                !!= * {{@CODE@}}
  @P.POS.ACR@ Acronyms ;          !!= * {{@CODE@}}
  @P.POS.ACRMinus@ Acrominus ;          !!= * {{@CODE@}}

! the nature of the previous word is reflected by flag diacritics that were set back then
! (possibly when passing through a continuation lexicon);
! use the diacritics to select possible continuation paths

LEXICON Latter !!= * {{@CODE@}} the latter part of a compound

! guesser: blahblah + word:
  @R.POS.Guess@ Nouns ;
  @R.POS.Guess@@P.POS.A@ Adjectives ;
  @R.POS.Guess@ AnyLatterVerb ;

! x + prefix
  @R.POS.ACRMinus@ Prefixes ;      
  @R.POS.Pref@@R.Part.Two@ Prefixes ;     ! agrobio- agrobiotoode;
  @R.POS.N@@R.Case.Gen@@R.Part.Two@ Prefixes ;     ! jalabio- jalabiotoode;
  @R.Case.Short@@R.Part.Two@ Prefixes ;     ! vaatamisbio- astraalbiotoode;

! x + adjective
  @R.POS.ACRMinus@ LatterAdjective ;      
  @R.POS.Pref@ LatterAdjective ;  ! ebapunane, √∂kokorralik
  @R.POS.Pref@ LatterAdjectiveComp ;  ! inimt√ºhjem
  @R.Case.Short@ LatterAdjective ;  ! vaatamispunane, astraalpunane

  @R.POS.N@@R.Case.Nom@@R.Stem.Nom@ LatterAdjective "weight: 5" ; ! laud√µiekas
  @R.POS.N@@R.Case.Nom@@D.Stem.Nom@ LatterAdjective_ne "weight: 5" ; ! aerjalgne
  @R.POS.N@@R.Case.Gen@ LatterAdjective_ne ; ! lauapunane, laudadepunane
  @R.POS.N@@R.Case.Gen@ PlainLatterAdjective ; ! lauaahastuslik
  @R.POS.N@@R.Case.Par@ LatterAdjective_v ; ! m√§gejuhtiv
  @R.POS.N@@R.Case.Sem@ LatterAdjective_v ; ! m√§estjuhtiv

  @R.POS.GA@ LatterAdjective_ne ; ! himaalajajalgne

  @R.POS.IntNum@@R.Case.Nom@@D.Der.kas@@R.Stem.Nom@ LatterAdjective ; ! poolraske
  @R.POS.IntNum@@R.Case.Gen@ LatterAdjective_ne ;          ! viiemeetrine
  @R.POS.IntNum@@R.Case.Gen@@P.NeedDerNe.On@ Nouns ;        ! viieautone
  @R.POS.NumCardCompound@@R.Case.Gen@ LatterAdjective_ne ; ! kahek√ºmnemeetrine
  @R.POS.NumCardCompound@@R.Case.Gen@@P.NeedDerNe.On@ Nouns ;    ! kahek√ºmneautone
  @R.POS.NumOrd@@R.Case.Gen@ LatterAdjective_ne ;        ! viiendaj√§rguline
  @R.POS.Num@ LatterAdjective_ne ;                       ! 5punane, 3j√§rguline
  @R.POS.Num@@P.NeedDerNe.On@ Nouns ;                    ! 5-autone
 
  @R.POS.A@@R.Case.Nom@@R.Stem.Nom@ LatterAdjective ;  ! r√§mepunane
  @R.POS.A@@R.Case.Gen@ LatterAdjective_ne ;  ! suureruuduline
  @R.POS.A@@R.Case.Ine@ LatterAdjective_ne ; ! punnispunane
  @R.Der.us@@R.Case.Nom@ LatterAdjective_ne ;  ! funktsionaalsusjalgne

  @R.POS.AComp@@R.Case.Nom@@R.Stem.Nom@ LatterAdjective ;  ! enampunane, enimpunane
  @R.POS.AComp@@R.Case.Gen@ LatterAdjective_ne ;  ! enamaruuduline, enimaruuduline

! NB only some pronouns !
  @R.POS.Pron@@D.Stem.ise@@R.Case.Gen@ LatterAdjective_ne ; ! minupunane, meiep√µ√µsaline
  @R.POS.Pron@@R.Stem.ise@@R.Case.Gen@ LatterAdjective ; ! enesepunane, eneseuhke

  @R.POS.Adv@@R.Stem.topelt@ LatterAdjective ;        ! topelthallikas etc 
  @R.POS.Adv@@R.Stem.v√§he@ LatterAdjective ;        ! v√§hehallikas etc 
  @R.POS.Adv@@R.Stem.√ºle@ LatterAdjective ;        ! √ºlehallikas etc 
  @R.POS.Adv@@D.Stem.topelt@@D.Stem.v√§he@@D.Stem.√ºle@ LatterAdjective_v ;        ! harvan√§htavaid etc 

! x + noun
  @R.POS.ACRMinus@ LatterNoun ;      
  @R.POS.ACRMinus@ LatterProperNoun ;      
!  @R.POS.Pref@@P.Part.One@ LatterNoun ;    ! ebalaud; allow pref + 3-part-compounds
  @R.POS.Pref@ LatterNoun ;    ! ebalaud; allow pref + 3-part-compounds
  @R.POS.Pref@ LatterProperNoun ;          ! ehttartulik
  @R.POS.Pref@ LatterGenitiveAttribute ;   ! √ºrgkreeka
  @R.Case.Short@ LatterNoun ;              ! astraallaud, ekstrariiul
  @R.Case.Short@ LatterGenitiveAttribute ;   ! astraalkreeka

  @R.POS.N@@R.Case.Nom@@R.Stem.Nom@ LatterNoun "weight: 5" ;
  @R.POS.N@@R.Case.Gen@ LatterNoun ;             ! lauariiul
  @R.POS.N@@R.Case.Gen@ LatterGenitiveAttribute ;             ! lauakreeka
  @R.POS.IntNum@@R.Case.Nom@@D.Der.kas@ LatterNoun ;       ! neliriiul
  @R.POS.NonIntNum@@R.Case.Nom@@D.Der.kas@ LatterNoun ;       ! viiendikriiul
  @R.POS.IntNum@@R.Case.Gen@ LatterNoun ;       ! neljariiul, neljakariiul

! NB only short adjectives are good here !!!
  @R.POS.A@@R.Case.Nom@@R.Stem.Nom@@D.Der.kas@ LatterNoun ;   ! lapikriiul, NOT pruunjasriiul (?)
  @R.POS.AComp@@R.Case.Nom@@R.Stem.Nom@ LatterNoun ;   ! enamriiul, enimriiul

  @R.Der.us@@R.Case.Nom@@D.Stem.Nom@ LatterNoun ;  ! funktsionaalsusriiul

! NB only enese, enda !
  @R.POS.Pron@@R.Case.Gen@@R.Stem.ise@ LatterNoun ;        ! eneseabi, enesekriitika etc 

  @R.POS.Adv@@R.Stem.topelt@ LatterNoun ;        ! topeltriiul etc 
  @R.POS.Adv@@R.Stem.v√§he@@P.NeedAdj.On@@D.NeedDerNe@ LatterNoun ;  ! v√§he + kalor +Der/line; not v√§heautone
  @R.POS.Adv@@R.Stem.√ºle@ LatterNoun ;        ! √ºletoodang etc 
  @R.POS.Adv@@R.Stem.√ºle@ LatterProperNounGeo ;          ! √ºlesiberiline

! x + deverbal noun (in addition to x + noun)
 
  @R.POS.N@@R.Case.Sem@ LatterDeverbal ;  ! m√§gedesttulek
  @R.POS.V@@R.Case.Sem@ LatterDeverbal ;  ! -ma, -mas etc lugematulek
  @R.POS.A@@R.Case.Sem@ LatterDeverbal ;  ! ilmsekstulek
  @R.POS.AComp@@R.Case.Sem@ LatterDeverbal ;  ! ilmsemakstulek
  @R.POS.Adv@ LatterDeverbal ;            ! huupitulek
  @R.POS.GA@ LatterDeverbal ;             ! himaalajatulek

! x + numeral
  @R.POS.Pref@@P.POS.IntNum@ IntegerNumerals ;  ! esiviisteist, esikolmekas
  @R.POS.N@@R.Case.Gen@@P.POS.IntNum@@C.Der@@C.Stem@@P.NeedNoun.On@ IntegerNumerals ;   ! arvuti + kolmekas
  @R.POS.NumCardCompound@@R.Case.Gen@@P.POS.IntNum@@C.Der@@C.Stem@@P.NeedNoun.On@ IntegerNumerals ;   ! kahek√ºmne + kolmekas

! x + number
! number should be the final component
  @R.POS.Pref@@P.POS.Num@@P.Part.Three@ CardinalNumbersInCompFin ;  ! esi-10
  @R.POS.N@@R.Case.Nom@@P.POS.Num@@P.Part.Three@ CardinalNumbersInCompFinMinus ;  ! uraan-235
  @R.POS.ACR@@R.Case.Nom@@P.POS.Num@@P.Part.Three@ CardinalNumbersInCompFinMinus ;  ! A-10
  @R.POS.ACR@@R.Case.Nom@@P.POS.Num@@P.Part.Three@ CardinalNumbersInCompFin ;       ! E10

! x + adverb
  @R.POS.Pref@@P.POS.Adv@@P.NeedPartThree.On@ CompoundingAdverbs ;  ! era+ratsa+baas, era+j√§rel+valvama+Der/ja 
  @R.POS.N@@R.Case.Nom@@R.Stem.Nom@@P.POS.Adv@@C.Case@@P.NeedPartThree.On@ CompoundingAdverbs ;  ! ehitus+j√§rel+valvama+Der/ja 
  @R.POS.N@@R.Case.Gen@@P.POS.Adv@@C.Case@@P.NeedPartThree.On@ CompoundingAdverbs ;  ! ehitus+j√§rel+valvama+Der/ja 

! x + verb derivation
! verb derivations expect previous word to be either in Gen or Par

  @R.POS.ACRMinus@ AnyLatterVerb ;      
  @R.POS.Pref@ AnyLatterVerb ;  ! ebalugemine

  @R.POS.N@@R.Case.Gen@ LatterVerb ;   ! lauajooksja
  @R.POS.N@@R.Case.Par@ LatterVerb ;   ! laudajooksnud

  @R.POS.N@@R.Case.Nom@@R.Stem.Nom@ AnyLatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.N@@R.Case.Sem@ AnyLatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.N@@R.Case.Short@ AnyLatterVerb ;  ! investeerimisn√µustamine

  @R.POS.A@@R.Case.Short@ AnyLatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.A@@R.Case.Sem@ AnyLatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.AComp@@R.Case.Sem@ AnyLatterVerb ;  ! map orig. case flag for derivation needs

  @R.POS.GA@@P.Case.Gen@ LatterVerb ;   ! eestivihkaja

! NB only short adjectives are good here !!!
  @R.POS.A@@R.Case.Nom@@R.Stem.Nom@@D.Der.kas@ AnyLatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.AComp@@R.Case.Nom@@R.Stem.Nom@ AnyLatterVerb ;  ! map orig. case flag for derivation needs

  @R.Der.us@@R.Case.Nom@@D.Stem.Nom@@P.Case.Gen@ LatterVerb ;  ! funktsionaalsusj√µramine, kindlustusj√µramine


! NB only short and good adverbs here !!!
  @R.POS.Adv@@D.Case@ AnyLatterVerb ;  ! map orig. case flag for derivation needs

  @R.POS.IntNum@@R.Case.Nom@@D.Der.kas@@P.Case.Gen@ LatterVerb ;  ! map orig. case flag for derivation needs
  @R.POS.IntNum@@R.Case.Gen@ LatterVerb  ;
! NB only some pronouns !
  @R.POS.Pron@@R.Case.Gen@ LatterVerb ;  
  @R.POS.Pron@@R.Case.Par@ LatterVerb ;
  @R.POS.Pron@@R.Case.Sem@ AnyLatterVerb ;

  @R.POS.V@@D.Case@ AnyLatterVerb ;              ! infinitive
  @R.POS.V@@R.Case.Sem@ AnyLatterVerb ;          ! -ma, -mas etc

! x + final component
! final components are words that attach to more words than their POS would suggest

  @R.POS.IntNum@@R.Case.Nom@@D.Der.kas@ AdjAfterNumeralNom ;          ! kolmkeelne
  @R.POS.IntNum@@R.Case.Gen@@P.POS.Adv@ AdvAfterNumeral ;  ! seitsmekaupa

  @R.POS.N@@R.Case.Nom@@P.POS.Adv@@C.Der@@C.Stem@ AdvAfterNounNom ;  ! riiulhaaval
  @R.POS.N@@R.Case.Gen@@P.POS.Adv@@C.Der@@C.Stem@ AdvAfterNounGen ;  ! riiulihaaval
  @R.POS.N@@R.Case.Par@@P.POS.Adv@@C.Der@@C.Stem@ AdvAfterNounPar ;  ! riiulitpidi

  @R.POS.GA@@P.POS.A@ AdjAfterGenAttrib ;                     ! himaalajapoolne
  @R.POS.GA@@P.POS.N@ NounFinalComp ;                         ! himaalajamees

  @R.POS.A@@R.Case.Gen@@P.POS.A@@C.Der@@C.Stem@ AdjAfterAdjGen ;      ! hapukav√µitu
  @R.POS.AComp@@R.Case.Gen@@P.POS.A@@C.Der@@C.Stem@ AdjAfterAdjGen ;  ! hapukamav√µitu
!  @R.POS.A@@R.Case.Gen@@P.POS.N@@C.Der@@C.Stem@ NounFinalComp ;       ! hapukavahend

  @R.POS.N@@R.Case.Short@@P.POS.A@@C.Der@@C.Stem@ V√§√§rtFinal ;    ! kiitmisv√§√§rt
! --------------------------------

LEXICON LatterAdjective

  @P.POS.A@@C.Der@@C.Stem@ Adjectives ;              !!= * {{@CODE@}}

LEXICON LatterAdjective_ne

  @P.POS.A@@C.Der@@C.Stem@ Adjectives_ne ;              !!= * {{@CODE@}}
  @P.NeedAdj.On@@D.NeedDerNe@ LatterNoun ;                 !! j√§rguline	j√§rk+N+Der/line+A  redellik redel+N+Der/lik+A     NOT -autone

LEXICON LatterAdjective_v

  @P.POS.A@@C.Der@@C.Stem@ Adjectives_v ;              !!= * {{@CODE@}}

LEXICON PlainLatterAdjective

  @P.POS.A@@C.Der@@C.Stem@ PlainAdjectives ;              !!= * {{@CODE@}}

LEXICON LatterAdjectiveComp

  @P.POS.AComp@@C.Der@@C.Stem@ ComparativeAdjectives ;              !!= * {{@CODE@}}
  @P.POS.AComp@@C.Der@@C.Stem@ SuperlativeAdjectives ;              !!= * {{@CODE@}}

LEXICON LatterNoun
  @P.POS.N@@C.Der@@C.Stem@ Nouns ;

LEXICON LatterProperNoun
  @P.POS.NProp@@C.Der@@C.Stem@@P.NeedAdj.On@@U.Cap.Opt@  ProperNouns ;    ! derived into an adjective

LEXICON LatterProperNounGeo
  @P.POS.NProp@@C.Der@@P.NeedAdj.On@@U.Cap.Opt@  ProperNounsGeo ;    ! √ºle + Siberi +Der/line

LEXICON LatterDeverbal
  @P.POS.N@@C.Der@@C.Stem@ DeverbalNouns ;    

LEXICON LatterGenitiveAttribute
  @P.POS.GA@@P.Case.Gen@@C.Stem@ GenitiveAttributes ;   ! -kreeka

LEXICON AnyLatterVerb     ! any verb derivation is allowed; set the case accordingly
              
  @P.Case.Gen@ LatterVerb ;                   !!= * {{@CODE@}}
  @P.Case.Par@ LatterVerb ;

LEXICON LatterVerb       ! a verb derivation
              
  @P.POS.V@@C.Der@@P.Stem.Verb@ Verbs ;                   !!= * {{@CODE@}}

! An acronym that may be first part of a compound:
! a sequence of capital letters; max length 5 and a trailing hyphen
LEXICON Acrominus

  < CapLetter^{1,5} "+ACR":"-" > # ;


