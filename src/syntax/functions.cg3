DELIMITERS = "<.>" "<?>" "<!>" "<..>" "<...>" "<¶>" "</p>" "<p>"  QDL "^vastas" ;

PARENTHESES = ("<(>" "<)>") ("<[>" "<]>") ("<{>" "<}>") (CLO CLC)  ; # ("<“>" "<”>") ; 

BEFORE-SECTIONS

LIST >>> = >>>  ;
LIST <<< = <<<  ;
LIST Koolonid = ":" ";"  ;
LIST Koolon = ":" ;
LIST Kvm = "," ":" ";" Punct "<{>" "<}>" ;
LIST LLZ = "." "?" Esc ;
LIST Sid|Kvm = (CC) ("kui" CC CS) "," ":" ";" Punct "<{>" "<}>" ;
LIST Jsub = CS  ;
LIST Fst = "." ;
LIST Koma = "," ;
LIST OPar = OPar ;
LIST CPar = CPar ;
LIST CLB = CLB CLBC ;
LIST CLBC = CLB ;
LIST Opr = "(" ;
LIST Cpr = ")" ;
LIST Dsh = "-" ;
LIST Oqu = "«" ;
LIST Cqu = "»" ;
LIST Quo = """ "»" "«" ;
LIST Cap = Prop ;
LIST Sides = "et" "nagu" "kui" "kuni" "kas" "kes" "mis" "kus" "kuid" "aga" "siis" "kuigi" "kuidas" "otse#kui" "sest" "ja" "ning" "ega" ("või" CC) "ehk" "kui#palju"  ;

LIST Tav = Der/tav Der#tav ;
LIST Tud = (Prt Prc Impers) Der/tu Der#tu ;
LIST Nud = (Prt Prc Pers) Der/nu Der#nu ;
LIST NudTud = (Prt Prc) Der/nu Der#nu Der/tu Der#tu ;
LIST NudTudDes = (Prt Prc) Ger Der/nu Der#nu Der/tu Der#tu ;

LIST Ja = "ja" "ning" "ega" ;
LIST JaVoi = "ja" "ning" ("või" CC) "ega" ;
LIST SbPrNom = (N Nom) (Pron Nom) (Num Card Nom) ;
LIST QuotPresPs = (Quot Prs Pers) ;
LIST QuotPs = (Quot Pers) ;
LIST SupInfGer = Sup Inf Ger ;
LIST InfSupiin = Inf Sup ;
LIST Supiin = Sup ;
LIST Supill = (Sup Ill) ;
LIST Inf = Inf ;
LIST Imper = Imprt ;
LIST Mod = Mod ;
LIST Aux = Aux ;
LIST Ger = Ger ;

LIST FinV = Ind Imprt Cond Quot ;
LIST InfP = *InfP ;

LIST Smine = Der/mine Der#mine ;

LIST S = N ;
LIST Scom = N ;
LIST Sprop = Prop ;
LIST SP = N Pron ;
LIST P = Pron ;
LIST Ppers = Pers ;
LIST Pdet = Det ;
LIST Pdem = Dem ;
LIST Pindef = Indef ;
LIST Ppos = Pos ;
LIST Prel = Rel ;
LIST Pps1 = Ps1 ;               
LIST Pps2 = Ps2 ;
LIST Pps3 = Ps3 ;


LIST Verb = V ;
LIST N = Num ;
LIST Ncard = Card ;
LIST Nord = Ord ;
LIST Adv = Adv ;
LIST Adj = A Attr  ;
LIST A = A ;
LIST I = Interj ;
LIST J = CC CS ;
LIST Y = ABBR ACR ;
LIST G = Attr ;
LIST X = X ;
LIST Z = Punct ;
LIST Ynominal = ABBR ACR ;
LIST K = Adp Po Pr ;
LIST Kpost = (Po) ;
LIST Kpre = (Pr) ;
LIST SPAN = N Pron A Num ;
LIST SPN = N Pron (Num Card) ;

LIST Partic = Prc  ;
LIST EiAra = "ei" "ära" ;
LIST Ei = "ei" ;
LIST Inf = Inf ;
LIST Ise = "ise" ;
LIST Neg = Neg ;
LIST Nii = "nii" ;
LIST Ka = "ka" ;
LIST Kui = "kui" ;

LIST Intr = *Intr ;

LIST KompKvAdv = "enam" "enim" "rohkem" "vähem" ;
LIST KvPron ="terve" ("kogu" Pron) "paljud" "mõlemad" "muist" "mitu" "mi=tu" ;
LIST NimKv = "liiter" "meeter" "kilo#meeter" "tonn" ("tund" N) "aasta" "tükk" "natuke" "ivake" "veidike" "korv" "viil" "kott" "ämber" "pudel" "klaas" "pott" "poti#täis" "pann" "panni#täis" "kilo" "kübe" "komplekt" "killu~ke" "m2" "m3" "km2" "km3" 
	"hulk" "rühm" "paar" "kimp" "kari" "virn" "polk" "trobi#kond" "osa" "enamik" "enamus" "raasuke" "kamp" "punt" "kamakas" "rida" "kuhi" "pakk" "kuu" "nädal" "ohtralt" ("jagu" N) "oma#jagu" "kiht" "osa" ;
LIST Täis = "täis" "tulvil" ;
LIST KvAdv = ("palju" Adv) "vähe" "pisut" "veidi" "küllalt" "rohkesti" "täis" "tulvil" "peo#täis" "rohkem" "vähem" "enamik" "enamus" "ohtralt" "arvukalt" ("jagu" N) "oma#jagu" "kiht" "patakas" ;
LIST ArvKv = (Num Card) "mitu" "mi=tu" "mitu#setu" "paar" "tosin" "mitu#kümmend" "mitu#sada" "kümmekond" "sadakond" "pool#teist" "tuh" "milj" ;
LIST ArvKvErand = "null" "veerand" "kolm#veerand" ;

LIST Subjekt = @SUBJ  ;
LIST Objekt = @OBJ ;
LIST Määrus = @ADVL ;
LIST Poolik = *- ;
LIST Digit = digit ;


LIST KNom = *nom ;
LIST KGen = *gen ;
LIST KPar = *part ;
LIST KEl = *el ;
LIST KAbl = *abl ;
LIST KAll = *all ;
LIST KAd = *ad ;
LIST KTerm = *term ;
LIST KAbes = *abes ;
LIST KKom = *kom ;



LIST IFV = @IMV @ICV ;
LIST JaVoiK = "ja" "ning" "ega" ("või" CC) "," ;
LIST Vaja = "vaja" "tarvis" ;


LIST Oeldis = @FMV @FCV @IMV @ICV ;
LIST Eestäiend = @NN> @AN> @KN> @DN> @INFN> ;
LIST Järeltäiend = @<NN @<AN @<KN @<DN @<INFN ;
LIST Head = @SUBJ @OBJ (N @PRD) (Pron @PRD) (N @ADVL) (Pron @ADVL) ;

LIST NotEestäiend = @<NN @SUBJ @OBJ @PRD (N @ADVL) (Num Card @ADVL) (Pron @ADVL) @<P ;

LIST Raha = "sendiline" "kopikaline" "rublane" "kroonine" ;
LIST Adjühik = "aastane" "meetrine" "liitrine" "tonnine" "kuune" "päevane" "tunnine" "kilogrammine" "grammine" "sentimeetrine" "detsimeetrine" "millimeetrine" "voldine" ;
LIST Võrdlus = "nagu" "kui" "otse#kui" "justkui" ;
LIST Aeg = "aeg" "aasta" "kuu" "nädal" "päev" ("tund" N) "minut" "hetk" "sekund" "moment" "kevad" "talv" "suvi" "kell"
 "sügis" "kuupäev" "kuu#päev" ("nädala#päev" Pl) ("aasta#päev" Pl) "õhtu#poolik" "öö" "õhtu" "hommik" "jaanuar" "veebruar" "märts" 
 "märtsikuu" "märtsi#kuu" "aprill" "aprillikuu" "aprilli#kuu" "mai" "maikuu" "mai#kuu" 
 "juuni" "juunikuu" "juuni#kuu" "juuli" "juulikuu" "juuli#kuu" "august" "september" "oktoober" "november"  "detsember" 
 "esmaspäev" "teisipäev" "kolmapäev" "neljapäev" "reede" "laupäev" "pühapäev" "aastakümme"
 "esmas#päev" "teisi#päev" "kolma#päev" "nelja#päev" "lau#päev" "püha#päev" "aasta#kümme"
 "sajand" "aasta#sajand" "kord" "samm" "aste" ("ulatus" N) "algus" "lõpp" "elu" "igavik" "aja#vahemik" "nüüd" ; # ajamääratlus

LIST NPpiir = *FinV Sup Inf Ger Cond Quot >>> <<< "." ";" "!" "?" CLB ;
LIST Imps = Impers ;
LIST Ps = Ps ;

LIST Viisimäärus = ("viis" N Par) ("suur" Par) ; 
LIST KedaMida = ("kes" Pron Sg Par) ("mis" Pron Sg Par) ;


LIST Vps12 = (V Pl1) (V Sg1) (V Pl2) (V Sg2) ;
LIST Vps1 = (V Pl1) (V Sg1) ;
LIST Vps1sg = (V Sg1) ;
LIST Vps1pl = (V Pl1) ;
LIST Vps2 = (V Pl2) (V Sg2) ;
LIST Vps2sg = (V Sg2) ;
LIST Vps2pl = (V Pl2) ;
LIST Vps3sg = (V Sg3) ;
LIST Vps3pl = (V Pl3) ;

LIST SgVerb = (V Sg1) (V Sg2) (V Sg3) ;
LIST PlVerb = (V Pl1) (V Pl2) (V Pl3) ;

LIST NotParFV = Gen (Sg Ill) (Pl Ill) Ine Ela All Ade Abl Tra Trm Ess Abe Com Inf Ger Comp 
          (Pr) "-" @FMV @FCV ;
LIST case = Nom Gen Par (Sg Ill) Ine (Pl Ill) Ela All Ade Abl Tra Trm Ess Abe Com ;

LIST SPNomPar = (N Nom) (N par) (Pron Nom) (Pron par) ;
LIST NomPar = Nom par ;
LIST NGP = Nom Gen Par ;

LIST NimOm = Nom Gen ;
LIST Nom = Nom  ;
LIST Gen = Gen  ;
LIST Par = Par ;
LIST Tr = Tra ;
LIST In = Ine ;
LIST El = Ela ;
LIST Ad = Ade ;
LIST All = All ;
LIST Abl = Abl ;
LIST Es = Ess ;
LIST Abes = Abe ;
LIST Kom = Com ;
LIST Term = Trm ;
LIST Sise = Ine Ade Ela Abl (Pl Ill) (Sg Ill) All ;
LIST IllAdt = (Pl Ill) (Sg Ill) ;
LIST Adit = (Sg Ill) ;
LIST NiNaTaGa = Trm Ess Abe Com ;


LIST Pl = Pl ;  # vt verbi kasutust: pole pl ja sg!
LIST Sg = Sg ;  # on ainult: sg1 pl1 sg2 pl2 sg3 pl3

LIST SPNom = (N Nom) (Pron Nom) (Num Nom) ;
LIST PlNom = (Pl Nom)  ; 
LIST SgNom = (Sg Nom)  ;
LIST PlGen = (Pl Gen)  ;  
LIST SgGen = (Sg Gen)  ;
LIST PlPar = (Pl Par)  ;  
LIST SgPar = (Sg Par)  ;

LIST PlIn = (Pl Ine)  ; 
LIST SgIn = (Sg Ine)  ;
LIST PlEl = (Pl Ela)  ;  
LIST SgEl = (Sg Ela)  ;
LIST PlIllAdt = (Pl Ill)  ;  
LIST SgIllAdt = (Sg Ill)  ;

LIST PlAd = (Pl Ade)  ; 
LIST SgAd = (Sg Ade)  ;
LIST PlAll = (Pl All)  ;  
LIST SgAll = (Sg All)  ;
LIST PlAbl = (Pl Abl)  ;  
LIST SgAbl = (Sg Abl)  ;

LIST PlEs = (Pl Ess)  ; 
LIST SgEs = (Sg Ess)  ;
LIST PlKom = (Pl Com)  ;  
LIST SgKom = (Sg Com)  ;
LIST PlTr = (Pl Tra)  ;  
LIST SgTr = (Sg Tra)  ;


LIST Mis = "mis" ;
LIST Koik = "kõik" ;
LIST Kysi = "?"  ;

LIST PartObjVerb = *Part *Part-P ;
LIST Ära = "ära" ;
LIST AtrAdv = @NN> @<NN @ADVL ;
LIST JärelKv = @<Q ;
LIST ParticPast = (Prt Prc) ;
LIST SbPrN = N Pron Num  ;
LIST Side = CC CS ;


LIST Pron = Pron ;

LIST Nimi = (N Prop) ;
LIST Poeg = ("p" ABBR ACR) ("t" ABBR ACR) ;
LIST Võrre = Comp Superl ;

LIST Parameeter = "liik" "tõug" "klass" "tüüp" "värv" "laad" "sort" "järk"
    "sugu" "mast" "kasv" ;
LIST SiseKaas = ("ees" Adp Po Pr) ;
LIST Deverbaal = Der/mine Der#mine "minek" "tulek" "jooks" "hüpe" ;
#LIST NotEestäiend = @<NN @SUBJ @OBJ @PRD (N @ADVL) (Num Card @ADVL) (Pron @ADVL) @<P ;

LIST HeadPPQ = @SUBJ @OBJ (@PRD N) (@PRD Pron)  (@ADVL N) (@ADVL Pron) @<P @P> @<Q ;
LIST NimEeslaiend = @NN> @P> ;
LIST PersPron = (Pron Pers) ;

LIST KohamäärIlAl = "siia" "sinna" "ülesse" "alla" "siiapoole" "sinnapoole"
              "ülespoole" "allapoole" "kuhugi" "kusagile" ;
LIST KohamäärInAd = "siin" "seal" "üleval" "all" "siinpool" "sealpool"
              "ülevalpool" "allpool" "kuskil" "kusagil" ;
LIST KohamäärElAb = "siit" "sealt" "ülevalt" "alt" "siitpoolt" "sealtpoolt"
              "ülevaltpoolt" "altpoolt" "kuskilt" "kusagilt" ;
LIST Täna = "homme" "eile" "täna" ;
LIST Omamoodi = "oma#moodi" "teist#moodi" "kõiksugu" ;

LIST YlesAlla = "üles" "alla" "ette" ("välja" Adv) "sisse" "mööda" ;
LIST Kaasa = "kaasa" "koos" "ühes" ;

LIST Arvsõna = (Num Card) "mitu" "mi=tu" "paar" ;
LIST Lauselaiend = "tõsi" "võimalik" "hea" "tore" "kena" "huvitav" ;
LIST Küll = "küll" ;
LIST Hiljem = "hiljem" "varem" "kauem" ;
LIST Iga = ("iga" Pron) ; 
LIST JärgArv = (Num Ord) ;
LIST Terve = "terve" "kogu" ; #Ajamääruse tundmiseks
LIST Kogu = "kogu" "terve" "mõni"  "mitu" "mi=tu" "järgmine" "eelmine" "viimane" ;
LIST Tervitus = "õhtune" "hommikune" "päevane" ;
LIST SpetsAdj = "suurune" "jämedune" "laiune" "kitsune" "pikkune" "kõrgune" 
          "sügavune" "paksune" "aastane" "kuune" "päevane" "tunnine" 
          "minutiline" "sekundiline" "hommikune" "lõunane" "õhtune" "öine" 
          "vanune" "sarnane" "laadne" "taoline" "vaheline" ("alune" A) "pealne" 
          "esine" "tagune" "poolne" "eelne" "järgne" "alane" "vastane"
          "nime=line" ;

LIST ParObjVerb = *Part *Part-P ;
LIST Trans = *NGP-P ;

LIST Tr2Intr = "aitama" "andma" "haarama" "hakkama" "hammustama" "hauduma" "heitma" "hoidma" "jooma" "jõllitama" "kiiluma" "kiitma" "kirjutama" "laskma" "litsuma" "lööma" "maksma" "murdma" "mõtlema" "mängima" "nägema" "pakkuma" "paluma" "panema" "passima" "pidama" "pingutama" "pistma" "pressima" "raiuma" "rebima" "rõõmustama" "rääkima" "saama" "tundma" "ulatama" "vaatama" "vahtima" "vedama" "viskama" "ütlema"  ;





LIST Olema = "olema" ;
LIST Saama = "saama" ;
LIST Pidama = "pidama" ;

LIST OleDaInf = "teadma" "nägema" "märkama" ("kuulma" V) ("tundma" V) ("kartma" V) "arvama" "lootma" "ootama" ;
LIST Yhepöördeline = ("tulema" Sg3) ("maksma" Sg3) ("tasuma" Sg3) ("tarvitsema" Sg3)("pruukima" Sg3) ("tulema" Cond) ("maksma" Cond) (tasuma" Cond) ("tarvitsema" Cond) ("pruukima" Cond) ;
LIST Suutelisusv = "õnnestuma" ;
LIST Emotsioonv = "meeldima" "vihastama" "tüütama" "kurvastama" "huvitama" "üllatama" ;
LIST Sobivusv = "sobima" "kõlbama" "sündima" "passima" ;
LIST Vajadusv = "tasuma" "kuluma" "tarvitsema" "maksma" "pruukima" "tulema" ;
LIST TeisedDaInfv = "olema" "minema" "tegema" "valmistama" "tekitama" "sünnitama" "pakkuma" ("tooma" V) ("jääma" V) "kujunema" "muutuma" "saama" "tekkima" ;
LIST MaVerb = "hakkama" ("jääma" V) "puhkema" "asuma" ;
LIST AhelVat = ("näima" Mod) ("paistma" Mod) ("tunduma" Mod) "näiksema" ;


#LIST PrVerb =("näima" V) ("paistma" V) ("tunduma" V) ("olema" V)  ; oli 2 listi
LIST Prverb = ("näima" V) ("paistma" V) ("tunduma" V) ("hakkama" V) ("püsima" V) ("juhtuma" V) ("kasvama" V) ("maitsma" V) ("nägema" V) ("olema" V) ; 


LIST SupIllAdvVerb = "käsutama" "hõikama" "hüüdma" ("kutsuma" V) "kamandama" "nõudma" 
               "paluma" "keelitama" "meelitama" "ahvatlema" "ehmatama" 
               "kohutama" "lask"&#V# "luba"&#V# "usaldama" "kauplema" 
               "tellima" "palkama" "värbama" "sundima" "kohustama" "provotseeri"
               "volitama" "õhutama" "häälestama" "inspireerima" "innustama" 
               "veetlema" "harjuma" "õppima" "sundi"
               "minema" "lonkima" "jalutama" "kõndima" "juhtuma" "trehvama" "tulema" "sõit"
               "ilmuma" "lendama" "jooksma" "tegema" "saatma" "viima" ;

LIST VatAdvVerb = "leidma" "lootma" "ütlema" "aimama" "arvama" "avastama" "eelda"
                 "kaeba""kartma" "kujutlema" "kurtma" "kuulma" "mõtlema" "möönma" "mäleta"
                 "märkama" "nentima" "nägema" "oletama" "ootama" "rääkima" "taipama" "taju"
                 "teadma" "tundma" "tunnistama" "väitma" "uskuma" ;

# ---------------------------------------------------------------------------

SET Main = Verb - Mod - Aux ;
SET IYV = Tr2Intr + (<PhVerb>) ;
SET Intr&YV = Intr OR IYV ;

#koolonid
MAP CLBC TARGET Z (0 Koolonid)(-1 Verb)(*1C Verb) ;
MAP CLBC TARGET Z (0 Koolonid)(*1C FinV BARRIER Kvm) ;
MAP CLBC TARGET Z (0 Koolonid)(1 Verb) ;

#komad
MAP CLBC TARGET Z (1 Jsub)(*1C Verb) ;
MAP CLBC TARGET Z (1 FinV) (*-1 FinV BARRIER Kvm) ;
MAP CLBC TARGET Z (1 FinV) (*-1 Inf BARRIER Kvm) ;
MAP CLBC TARGET Z (1 FinV) (*-1 Partic BARRIER Kvm) ;

MAP CLBC TARGET Z (NEGATE 1 Quo)
                   (*-1 FinV BARRIER Sid|Kvm) 
                   (*1 FinV BARRIER Sid|Kvm) ;
MAP CLBC TARGET Z (1 Jsub) (*2 Verb BARRIER Sid|Kvm) ;

MAP (CLBC) TARGET Z (1 Jsub) ;
MAP (CLBC) TARGET Koma (1 Verb)(*-1 Verb)  ;

#sidesõnad
ADD CLBC TARGET ("ja") OR ("ning") OR ("ega") OR ("või") + J OR ("ehk") 
                   (*1 FinV BARRIER Sid|Kvm)
                   (*-1C FinV BARRIER Sid|Kvm) ;
ADD CLBC TARGET ("ja") OR ("ning") OR ("ega") OR ("või") + J OR ("ehk") (1C FinV)(*-1C FinV) ;
ADD CLBC TARGET Nii (-1 Kvm) (*1C FinV)(*-1C FinV) ;
MAP CLBC TARGET Z (1 I) ;
ADD (CLBC) TARGET ("et") (1 Imper) ;


#mõttekriips
MAP CLBC TARGET Dsh (NEGATE *1 Dsh) (*1C Verb BARRIER Kvm) ;
MAP CLBC TARGET Dsh (1 Verb) ;
MAP CLBC TARGET Dsh (1 Kvm)(2 Verb) ;

#MAP CLBC TARGET SP OR N OR Adj OR J OR K (-1 Fst)(-2 Digit)(-3 Y)(-4 >>>) ;
#MAP CLBC TARGET J OR Adv OR P (-1 Dash)(NEGATE 0 Võrdlus)(NEGATE -2 Poolik) ;
#MAP CLBC TARGET J OR Adv OR P (-1 Dash)(0 Võrdlus)(NEGATE -2 Poolik)(NEGATE *1 Verb BARRIER Kvm) ;
#MAP CLBC TARGET SP OR N OR Adj OR J OR K (-1 Dsh) (1C FinV) ;
#MAP CLBC TARGET FinV (-1 Dsh) ;
#MAP CLBC TARGET Adj (-1 Dsh)(1 S)(2 FinV) ;

#MAP CLBC TARGET Verb (-1 Oquo)(-2 Kvm) ;



#MAP CLBC TARGET SP OR N OR Adj OR J OR K (-1 OPar) (NEGATE 0 Kvm) (*1 CPar BARRIER FinV) ;
#MAP CLBC TARGET SP OR N OR Adj OR J OR K (1 CPar) (NEGATE 0 Kvm) (*-1 OPar BARRIER FinV) ;




#MAP (CLBC) TARGET ("ja") OR ("ning") OR ("ega") OR ("või") OR ("ehk") (0 J) (1 SPNom)
# (*1 Vps3sg LINK -1 Koma) ;

#MAP (CLBC) TARGET ("välja") OR ("kaasa") (-1 Koma) (1 Tud) ;



#MAP (CLBC) TARGET S (0 Nom) (-1 Koma) (-2 Adj) ;
#MAP (CLBC) TARGET S (0 Nom) (-1 Koma) (-2 Supiin) ;
#MAP (CLBC) TARGET S (0 Nom) (-1 Ja) (-2 Adj) ;
#MAP (CLBC) TARGET S (0 Nom) (-1 Ja) (-2 Supiin) ;

BEFORE-SECTIONS

ADD (CLBC) TARGET ("ja") OR ("ning") OR ("ega") OR ("või") OR ("ehk")
           (0 J) (NEGATE -1 CLB) (*-1 FinV BARRIER CLB) (*1 FinV BARRIER CLB) ;
ADD (CLBC) TARGET SP OR N OR Adj OR J OR K OR Adv OR Verb 
           (-1 Kvm) (NEGATE -1 CLB)(*-2 FinV BARRIER CLB)(*1 FinV BARRIER CLB) ;
ADD (CLBC) TARGET SP OR N OR Adj OR J OR K (-1 Kvm)
           (NEGATE -1 CLB) (*-2 FinV BARRIER CLB) (*1 InfSupiin BARRIER CLB) ;


BEFORE-SECTIONS


# tea tekstide sulgude erand
#MAP (@NONE) TARGET (*) (*-1 Opr BARRIER Cpr)(*1 Cpr BARRIER Opr) ;

MAP (@NEG) TARGET ("ei") + Verb  ;
ADD (@FMV) TARGET Main + Neg  ; # TP, muidu jäi üldse ilma @!
MAP (@ICV) TARGET Aux + Partic  ;
MAP (@ICV) TARGET Aux + Inf  ;
MAP (@ICV @ADVL) TARGET Aux + Supill  ; 
MAP (@FCV @ICV) TARGET Aux + QuotPs  ;
MAP (@FCV) TARGET Aux  ;

MAP (@ICV) TARGET Mod + Partic + Neg  ;
MAP (@ICV @ADVL) TARGET Mod + Partic  ;
MAP (@ICV) TARGET Mod + Inf  ;
MAP (@ICV) TARGET Mod + Supill  ; 
MAP (@ICV) TARGET Mod + QuotPs  ;
MAP (@FCV) TARGET Mod  ;

MAP (@IMV @ADVL @INFN> @<INFN) TARGET Supill  ;
MAP (@PRD @ADVL @INFN> @<INFN) TARGET Supiin + Abes  ;
MAP (@ADVL @INFN> @<INFN) TARGET Supiin + El  ;
MAP (@ADVL @INFN> @<INFN) TARGET Supiin + In  ;
MAP (@ADVL @INFN> @<INFN) TARGET Supiin + Tr  ;
MAP (@IMV @ADVL @INFN> @<INFN) TARGET Supiin + Imps  ;
MAP (@ADVL) TARGET Ger  ;
MAP (@FMV @IMV @OBJ @ADVL) TARGET QuotPs  ; ###vat
MAP (@IMV) TARGET ParticPast + Neg  ;
MAP (@IMV @ADVL) TARGET ParticPast  ;
MAP (@IMV @SUBJ @OBJ @PRD @INFN> @<INFN @ADVL) TARGET Inf  ;
ADD (@FMV) TARGET Verb  ;

#kaassõnad
ADD (@P>) TARGET S + Sg (1C Kpost + KNom)  ; # TP MAP -> ADD 36475
ADD (@P>) TARGET SPN + Gen (*1 Kpost BARRIER FinV) ; #TP
ADD (@<P) TARGET SPN + Gen (*-1 Kpre BARRIER FinV) ; #TP
ADD (@P>) TARGET SPN + Gen (*1 KGen BARRIER FinV) ; 
ADD (@<P) TARGET SPN + Gen (*-1 KGen BARRIER FinV) ; 
ADD (@P>) TARGET SPN + Par (*1 KPar BARRIER FinV) ; 
ADD (@<P) TARGET SPN + Par (*-1 KPar BARRIER FinV) ;
ADD (@P>) TARGET SPN + El (*1 KEl BARRIER FinV) ; 
ADD (@<P) TARGET SPN + El (*-1 KEl BARRIER FinV) ;
ADD (@P>) TARGET SPN + All (*1 KAll BARRIER FinV) ; 
ADD (@<P) TARGET SPN + All (*-1 KAll BARRIER FinV) ;
ADD (@<P) TARGET SPN + Ad (*-1 KAd BARRIER FinV) ;
ADD (@P>) TARGET SPN + Ad (1 ("pool") + K) ; # TP 2012 kahel pool
ADD (@P>) TARGET SPN + Term (*1 KTerm BARRIER FinV) ;
ADD (@P>) TARGET SPN + Abes (*1 KAbes BARRIER FinV) ;
ADD (@P>) TARGET SPN + Kom (*1 KKom BARRIER FinV) ; 
ADD (@<P) TARGET SPN + Kom (*-1 KKom BARRIER FinV) ;

#täiendid
ADD (@NN>) TARGET SP OR Ncard OR Ynominal (*1 SP OR Y OR Ncard BARRIER FinV) ; # TP +Y + Ncard
ADD (@<NN) TARGET SP OR Ncard OR Ynominal (*-1 SP OR Y BARRIER FinV)(NEGATE -1 ("üks")) ; # TP +Y
ADD (@AN>) TARGET Adj OR Nord OR G (NEGATE 1 N)(*1 SP OR Y BARRIER FinV) ; # TP +Y
ADD (@<AN) TARGET Adj OR Nord OR G (*-1 SP OR Y BARRIER FinV) ;
ADD (@DN>) TARGET Adv (*1 SP BARRIER FinV) ;
ADD (@<DN) TARGET Adv (*-1 SP BARRIER FinV) ;
ADD (@KN>) TARGET K (*1 SP BARRIER FinV) ;
ADD (@<KN) TARGET K (*-1 SP BARRIER FinV) ;

#kvantorid
ADD (@<Q) TARGET SP OR Ncard OR Ynominal (NEGATE 0 SPNom)
	(*-1 KompKvAdv OR KvPron OR NimKv OR Täis OR KvAdv OR ArvKv OR ArvKvErand BARRIER FinV OR Nom OR Jsub LINK NOT 0 ("üks") OR Pl) ; # TP +Y(a., km...) +nom Kvantor asendatud + LINK NOT 0 ("üks") or pl + (NEGATE 0 SPNom)

#öeldistäited
ADD (@PRD) TARGET SPAN + Nom OR SPAN + Par OR G (*-1 Prverb) ;
ADD (@PRD) TARGET SPAN + Nom OR SPAN + Par OR G (*1 Prverb) ;

#sihitis
ADD (@OBJ) TARGET SPN + Nom OR SPN + SgGen OR SPN + Par OR Ynominal ;
# sl: Tulemusena anti lastekodule rohkem kui 3000 krooni, hulgaliselt D OBJ mänguasju ja raamatuid.

#alus
ADD (@SUBJ) TARGET SPNom OR SP + Par OR Ncard OR Ynominal ;
# TP ADD (@SUBJ) TARGET SPN + Nom OR SPN + Par OR Ynominal ;
ADD:TP (@SUBJ) TARGET S + Cap (1 ("et"))(2 ("al"))  ;
ADD:TP (@SUBJ) TARGET S + Poolik (1 Kvm OR J) ;

MAP (@ADVL) TARGET SP OR N OR Adv OR K OR Adj OR Y OR X ;
MAP (@J) TARGET J ;
MAP (@ADVL) TARGET I ;
# TP MAP (@B) TARGET I ;

SECTION



#0 ERANDID (TP)

REMOVE Poolik + (@SUBJ) (1 Kvm OR J)(NEGATE 2 Nom OR Par) ; # TP
REMOVE Poolik + (@OBJ) (1 Kvm OR J)(NEGATE 2 NGP) ; # TP
REMOVE Poolik + (@NN>) (1 Kvm OR J)(NEGATE 2 Gen) ; # TP

SELECT ("ise" @ADVL) (NEGATE 0 Par)(1 ("ei")) ; #(1 Verb)(NEGATE 1 Prverb) ; # aga: ühiskond ise muutub
SELECT ("ise" @OBJ) + Par (NEGATE -1 Par) ;
SELECT ("ise" @<NN) (0 $$case)(-1 $$case) ;
SELECT ("ise" @<NN) (0 NGP)(-1 Cap + NGP) ;
SELECT ("ise" @P>) + Gen (1 Kpost) ;
SELECT ("ise" @NN>) + Gen (1 SP) ;
SELECT ("ise" @ADVL)  ;
SELECT ("kell" @NN>) (1 Ncard) ;
SELECT Ncard + (@ADVL) (-1 ("kell")) ;
REMOVE ("see" @SUBJ) (*1 Inf BARRIER CLBC) ;

#I OELDISEGRAMMATIKA

#1) Nud-, tud-vormid
# a) nud-, tud-vorm on öeldiseks, kui eespool osalause piires leidub ühene
# abiverb ja osalauses pole rohkem nud-, tud-vorme või nad on
# koordinatsioonis

SELECT NudTud + (@IMV) OR NudTud + (@ICV) (*-1C Aux BARRIER NudTud OR CLB) ;
SELECT NudTud + (@IMV) OR NudTud + (@ICV) (*-1C Aux BARRIER CLBC LINK *1C IFV LINK 0 NudTud LINK *1 JaVoiK)  ;

# b) Lauselühend
REMOVE Nud + (@IMV) OR Nud + (@ICV) (*1 Koma LINK 1 Ps + FinV) ;
REMOVE Tud + (@IMV) OR Tud + (@ICV) (*1 Koma LINK 1 Imps + FinV) ;
REMOVE Tud + (@IMV) OR Tud + (@ICV) (1 Sid|Kvm)(*-1 Sid|Kvm BARRIER CLB OR Verb LINK 1 SPNom) ; # ,lõug vastu rinda surutud,

# c) sai+tud 
SELECT (@IMV) + Tud OR Tud + (@ICV) (*-1 Saama + Vps3sg BARRIER Verb OR CLB) ; 

# d)välja Natud
REMOVE ("Na" @IMV) + Tud (-1 ("välja")) (NEGATE *-1 Aux BARRIER CLBC) ;


# e) kaudne kõneviis
SELECT Tud + (@IMV) (NEGATE *-1 FinV)(NEGATE *1 FinV) ;

#2) Da-infinitiiv       

# da-infinitiiv on infiniitne öeldis või ahelverbi infiniitne komponent,
# kui osalauses leidub da-infinitiivi nõudev modaalverb suvalises vormis

SELECT Inf + (@IMV) OR Inf + (@ICV) (*-1C InfP BARRIER FinV OR Inf OR CLB) ;
SELECT Inf + (@IMV) OR Inf + (@ICV) (*1C InfP BARRIER FinV OR Inf OR CLB) ; # oli (dainf)

# da-infinitiiv on öeldis, kui osalause algab sõnaga "et"
SELECT Inf + (@IMV) OR Inf + (@ICV) (-1 ("et")) ;


# da-infinitiiv on öeldis, kui osaluses ei leidu verbi finiitseid vorme
SELECT Inf + (@IMV) (NEGATE *-1 FinV BARRIER CLBC)
                        (NEGATE *1 FinV BARRIER CLBC)
                        (NEGATE *-1 Inf BARRIER CLBC)  ;

# viga tekib kiilu korral: ülempreester ei tahtnud loomulikult sellist
# rahakat ning -mis veel tähtsam! - väga mõjuvõimast külalist minema _lasta_

# Predikaadi koosseisu kuulub olema ja da-infinitiivsete verbide
# teadma, nägema, märkama jne ühend.
SELECT Inf + (@IMV) (0 OleDaInf) (*-1C Olema + Main + FinV BARRIER CLB)
              (NEGATE *-1 Nom BARRIER CLB) (NEGATE *-1 Vaja BARRIER CLB) ;
# Deskriptiivverbid
SELECT Inf + (@IMV) (-1 FinV)(NEGATE -1 Olema)(NEGATE -1 InfP)
                        (NEGATE -1 Yhepöördeline)
                        (NEGATE -1 Emotsioonv)
                        (NEGATE -1 Sobivusv)
                        (NEGATE -1 Vajadusv)
                        (NEGATE -1 Suutelisusv)
                        (NEGATE -1 TeisedDaInfv) ;

# Koordinatsioon
SELECT Inf + (@IMV) (*-1 Inf BARRIER FinV LINK 0C IFV LINK 1 JaVoiK) ;
REMOVE Inf + (@IMV) (*-1 Inf BARRIER FinV LINK NOT 0 IFV LINK 1 JaVoiK) ;

#3) Supiin
# ma-infinitiiv on infiniitne öeldis või ahelverbi infiniitne komponent,
# kui osalauses leidub ma-infinitiivi nõudev modaalverb suvalises vormis

SELECT Supill + (@IMV) OR Supill + (@ICV) (*-1 Pidama + Mod BARRIER CLBC) ;
SELECT Supill + (@IMV) OR Supill + (@ICV) (1 Pidama + Mod) ;

# ma-infinitiiv on infiniitne öeldis või ahelverbi infiniitne komponent, kui
# osalauses leidub ma-infinitiivi nõudev verb ("hakkama","jääma") suv. vormis
SELECT Supill + (@IMV) OR Supill + (@ICV) (*-1 MaVerb + Oeldis BARRIER CLBC) ;
SELECT Supill + (@IMV) OR Supill + (@ICV) (1 MaVerb)(1C Oeldis) ;

REMOVE Supill + (@IMV) OR Supill + (@ICV) (*-1 MaVerb ^ Oeldis BARRIER CLB) ;
REMOVE Supill + (@IMV) OR Supill + (@ICV) (1 MaVerb ^ Oeldis) ;

# ma-infinitiiv pole öeldis, kui osalauses leidub predikatiiv
REMOVE Supill + (@IMV) OR Supill + (@ICV) (NEGATE 0 Prverb)(*-1C (@PRD) BARRIER CLBC) ;
# Nt Ma olin hakkaja Namust avaldama

# Koordinatsioon
SELECT Supill + (@IMV) OR Supill + (@ICV) (*-1 Supill BARRIER FinV LINK 0C IFV LINK *1 JaVoiK) ;
REMOVE Supill + (@IMV) OR Supill + (@ICV) (*-1 Supill BARRIER FinV LINK NOT 0 IFV LINK *1 JaVoiK) ;
# oleksid pidanud olema rohkem tähelepanelikumad ja Nestama seda, millise

#4)Vat-infinitiiv 
# vat-infinitiiv on infiniitne öeldis või ahelverbi infiniitne komponent,
# kui osalauses leidub vat-infinitiivi nõudev modaalverb suvalises vormis

SELECT QuotPresPs + (@IMV) OR QuotPresPs + (@ICV) (*-1 AhelVat BARRIER CLBC) ;
SELECT QuotPresPs + (@IMV) OR QuotPresPs + (@ICV) (*1 AhelVat BARRIER CLBC) ;

REMOVE QuotPresPs + (@FMV) (*-1C (@FMV) BARRIER CLBC) ;
REMOVE QuotPresPs + (@FMV) (*1C (@FMV) BARRIER CLBC) ;
SELECT QuotPresPs + (@FMV) (NEGATE *-1 FinV) ;

#KONKREETSED REEGLID

# Ase- või Arvsõna üks
SELECT S + PlPar + (@<NN) (*-1 ("üks") BARRIER Head OR Oeldis OR CLBC) ;
# oli olnud üks Partei juhtivaid tegelasi
REMOVE ("üks" @NN>) (*1 S + PlPar BARRIER Head OR CLBC) ;
# saatis ühe oma vibuküttidest ...
REMOVE ("üks" @NN>) (*1 S + PlEl BARRIER Head OR CLBC) ;
REMOVE (@<NN) (0 SP)(-1 ("üks")) ; # TP
SELECT (@NN>) (0 SP)(NEGATE 0 Nom)(1 Smine) ; # TP

#SELECT ("kõik" @<NN) (-1 P) ;

#indefiniitpronoomeni ühildumine
SELECT Pindef + (@NN>) + Nom (NEGATE 0 KvPron)(*1 S + Nom BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + Gen (NEGATE 0 KvPron)(*1 S + Gen BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord)(NEGATE 1 Kpost + KGen) ;
SELECT Pindef + (@NN>) + Par (NEGATE 0 KvPron)
	      (*1 S + Par BARRIER Oeldis OR CLB OR Võrdlus)
	      (NEGATE 1 Pindef)(NEGATE 1 Nord) ;  
SELECT Pindef + (@NN>) (0 IllAdt)(NEGATE 0 KvPron)
	      (*1 S + IllAdt BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + In (NEGATE 0 KvPron)(*1 S + In BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + El (NEGATE 0 KvPron)(*1 S + El BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + All (NEGATE 0 KvPron)(*1 S + All BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + Ad (NEGATE 0 KvPron)(*1 S + Ad BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + Abl (NEGATE 0 KvPron)(*1 S + Abl BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
SELECT Pindef + (@NN>) + Tr (NEGATE 0 KvPron)(*1 S + Tr BARRIER Oeldis OR CLB)
              (NEGATE 1 Pindef)(NEGATE 1 Nord) ;
#mingi teine
SELECT Pindef + (@NN>) + Nom (1 Pindef + Nom) ;
SELECT Pindef + (@NN>) + Gen (1 Pindef + Gen) ;
SELECT Pindef + (@NN>) + Par (1 Pindef + Par)(NEGATE 0 ("miski") + SgPar) ;
SELECT Pindef + (@NN>) (0 IllAdt)(1 Pindef)(1 IllAdt) ;
SELECT Pindef + (@NN>) + In (1 Pindef + In) ;
SELECT Pindef + (@NN>) + El (1 Pindef + El) ;
SELECT Pindef + (@NN>) + Ad (1 Pindef + Ad) ;
SELECT Pindef + (@NN>) + All (1 Pindef + All) ;
SELECT Pindef + (@NN>) + Abl (1 Pindef + Abl) ;
SELECT Pindef + (@NN>) + Tr (1 Pindef + Tr) ;
SELECT Pindef + (@NN>) + Gen (1 Pindef)(1 NiNaTaGa) ;

#keegi niisugune
SELECT Pindef + (@NN>) + Nom (1 Pdem + Nom) ;
SELECT Pindef + (@NN>) + Gen (1 Pdem + Gen) ;
SELECT Pindef + (@NN>) + Par (1 Pdem + Par)(NEGATE 0 ("miski") + SgPar) ;
SELECT Pindef + (@NN>) (0 IllAdt)(1 Pdem)(1 IllAdt) ;
SELECT Pindef + (@NN>) + In (1 Pdem + In) ;
SELECT Pindef + (@NN>) + El (1 Pdem + El) ;
SELECT Pindef + (@NN>) + Ad (1 Pdem + Ad) ;
SELECT Pindef + (@NN>) + All (1 Pdem + All) ;
SELECT Pindef + (@NN>) + Abl (1 Pdem + Abl) ;
SELECT Pindef + (@NN>) + Tr (1 Pdem + Tr) ;
SELECT Pindef + (@NN>) + Gen (1 Pdem)(1 NiNaTaGa) ;


REMOVE (@NN>) (0 Pindef)(NEGATE 1 Pindef OR Pdem OR Nord OR A)
	      (NEGATE *1 S BARRIER CLB) ; # TP + A
REMOVE (@NN>) (0 Pindef)(NEGATE 1 Pindef OR Pdem OR Nord)
              (*1 NPpiir BARRIER SP OR CLB) ; # TP +P
REMOVE (@NN>) (0 Pindef)(0 NiNaTaGa)(NEGATE 1 Smine) ;

REMOVE (@NN>) (0 Pindef + Nom)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + Nom BARRIER CLB) ;

REMOVE (@NN>) (0 Pindef + Par)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + Par BARRIER CLB) ;

#REMOVE (@NN>) (0 Pindef + Par)
#              (*1 A + Par BARRIER CLB OR Par) ;   
#TP ainuke kord rakendus ja siis kustutas õige: mitmeid keemilisi protsesse

REMOVE (@NN>) (0 Pindef)(0 IllAdt)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + IllAdt BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + In)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + In BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + El)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + El BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + All)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + All BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + Ad)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + Ad BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + Abl)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 S + Abl BARRIER CLB) ;
REMOVE (@NN>) (0 Pindef + Tr)(NEGATE 1 Pindef OR Pdem OR Nord)
              (NEGATE *1 Tr BARRIER CLB) ; # TP -S
REMOVE (@NN>) (0 Pindef + Nom)(1 Pindef)(NEGATE 1 Nom) ;
REMOVE (@NN>) (0 Pindef + Gen)(1 Pindef)(NEGATE 1 Gen)(NEGATE 1 NiNaTaGa) ;
REMOVE (@NN>) (0 Pindef + Par)(1 Pindef)(NEGATE 1 Par) ;
REMOVE (@NN>) (0 Pindef + IllAdt)(1 Pindef)(NEGATE 1 IllAdt) ;
REMOVE (@NN>) (0 Pindef + In)(1 Pindef)(NEGATE 1 In) ;
REMOVE (@NN>) (0 Pindef + El)(1 Pindef)(NEGATE 1 El) ;
REMOVE (@NN>) (0 Pindef + Ad)(1 Pindef)(NEGATE 1 Ad) ;
REMOVE (@NN>) (0 Pindef + All)(1 Pindef)(NEGATE 1 All) ;
REMOVE (@NN>) (0 Pindef + Abl)(1 Pindef)(NEGATE 1 Abl) ;
REMOVE (@NN>) (0 Pindef + Tr)(1 Pindef)(NEGATE 1 Tr) ;
REMOVE (@NN>) OR (@<NN) (0 ("miski") + P + SgPar) ;

#Demonstratiivpronoomenid
SELECT Pdem + (@NN>) + Nom (NEGATE -1 ("kõik"))
              (*1 S + Nom ^ (@PRD) BARRIER CLB OR Oeldis OR Võrdlus OR (@PRD)) ;
SELECT Pdem + (@NN>) + Nom (NEGATE 1 ("kõik"))
              (*1 S + Nom BARRIER CLB OR Oeldis OR Võrdlus)
              (*-1C Subjekt BARRIER CLB LINK 1 FinV) ;
SELECT Pdem + (@NN>) + Gen (*1 S + Gen BARRIER CLB OR Võrdlus OR Oeldis)
              (NEGATE 1 Kpost + KGen) ; 
SELECT Pdem + (@NN>) + Par (*1 S + Par BARRIER CLB OR Oeldis) ;

SELECT Pdem + (@NN>) (0 IllAdt)(*1 S + IllAdt BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + In (*1 S + In BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + El (*1 S + El BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + All (*1 S + All BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + Ad (*1 S + Ad BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + Abl (*1 S + Abl BARRIER CLB OR Oeldis) ;
SELECT Pdem + (@NN>) + Tr (*1 S + Tr BARRIER CLB OR Oeldis) ;

SELECT Pdem + (@NN>) + SgGen (1 Sg + NiNaTaGa ^ ("kõik")) ;
SELECT Pdem + (@NN>) + PlNom (1 PlNom ^ ("kõik"))(NEGATE -1 Oeldis) ;
SELECT Pdem + (@NN>) + PlGen (1 Pl + NiNaTaGa ^ ("kõik")) ;

REMOVE Pdem + (@NN>) (0 NiNaTaGa)(NEGATE 1 Smine) ;

REMOVE Pdem + (@NN>) + Nom (NEGATE 1 Nom)(NEGATE *1 S + Nom BARRIER CLB) ;
REMOVE Pdem + (@NN>) + Par (NEGATE 1 Par)(NEGATE *1 S + Par BARRIER CLB) ;
REMOVE (@NN>) (0C Pdem)(0 IllAdt)(NEGATE 1 IllAdt)
              (NEGATE *1 S + IllAdt BARRIER CLB) ;
REMOVE Pdem + (@NN>) + In (NEGATE 1 In)(NEGATE *1 S + In BARRIER CLB) ;
REMOVE Pdem + (@NN>) + El (NEGATE 1 El)(NEGATE *1 El BARRIER CLB) ;
REMOVE Pdem + (@NN>) + All (NEGATE 1 All)(NEGATE *1 S + All BARRIER CLB) ;
REMOVE Pdem + (@NN>) + Ad (NEGATE 1 Ad)(NEGATE *1 S + Ad BARRIER CLB) ;
REMOVE Pdem + (@NN>) + Abl (NEGATE 1 Abl)(NEGATE *1 S + Abl BARRIER CLB) ;
REMOVE Pdem + (@NN>) + Tr (NEGATE 1 Tr)(NEGATE *1 S + Tr BARRIER CLB) ;
REMOVE Pdem + (@NN>) (1C ("kõik")) ;

#Determinatiivpronoomenid
SELECT Pdet + (@NN>) + Nom (NEGATE -1 P)
              (*1 S + Nom ^ (@PRD) BARRIER CLB OR Oeldis OR Võrdlus) ;
SELECT Pdet + (@NN>) + Nom (NEGATE 0 Ise)(NEGATE -1 P)(NEGATE 1 ("kõik"))
              (*1 S + Nom BARRIER CLB OR Oeldis OR Võrdlus)
              (*-1C Subjekt BARRIER CLB) ;
SELECT Pdet + (@NN>) + Gen (*1 S + Gen BARRIER CLB OR Võrdlus)
              (NEGATE 1 Kpost + KGen)(NEGATE 1 Tav OR Tud) ; 
SELECT Pdet + (@NN>) + Par (NEGATE 0 Ise)(NEGATE 0 ("kõik"))
              (*1 S + Par BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) (0 IllAdt)
              (*1 S + IllAdt BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + In (*1 S + In BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + El (*1 S + El BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + All (*1 S + All BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + Ad (*1 S + Ad BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + Abl (*1 S + Abl BARRIER CLB OR Oeldis) ;
SELECT Pdet + (@NN>) + Tr (*1 S + Tr BARRIER CLB OR Oeldis) ;

REMOVE Pdet + (@NN>) (0 NiNaTaGa)(NEGATE 1 Smine) ;
REMOVE Pdet + (@NN>) + Nom (NEGATE 0 ("kõik") OR  Ise)
              (NEGATE *1 S + Nom BARRIER CLB) ;
REMOVE Pdet + (@NN>) + Par (NEGATE 0 ("kõik"))(NEGATE *1 S + Par BARRIER CLB) ;
REMOVE Pdet + (@NN>) (0 IllAdt ^ ("kõik"))(NEGATE *1 S + IllAdt BARRIER CLB) ;
REMOVE Pdet + (@NN>) + In (NEGATE 0 ("kõik"))(NEGATE *1 S + In BARRIER CLB) ;
REMOVE Pdet + (@NN>) + El (NEGATE 0 ("kõik"))(NEGATE *1 S + El BARRIER CLB) ;
REMOVE Pdet + (@NN>) + All (NEGATE 0 ("kõik"))(NEGATE *1 S + All BARRIER CLB) ;
REMOVE Pdet + (@NN>) + Ad (NEGATE 0 ("kõik"))(NEGATE *1 S + Ad BARRIER CLB) ;
REMOVE Pdet + (@NN>) + Abl (NEGATE 0 ("kõik"))(NEGATE *1 S + Abl BARRIER CLB) ;
REMOVE Pdet + (@NN>) + Tr (NEGATE 0 ("kõik"))(NEGATE *1 S + Tr BARRIER CLB) ;

REMOVE ("kõik" @NN>) + Nom (NEGATE 1 P)(NEGATE *1 S + Nom OR N + Nom BARRIER CLB) ;
REMOVE ("kõik" @NN>) + Par (NEGATE 1 P)(NEGATE *1 S + Par OR N + Par BARRIER CLB) ;
REMOVE ("kõik" @NN>) (0 IllAdt)(NEGATE 1 P)
              (NEGATE *1 S + IllAdt OR N + IllAdt BARRIER CLB ) ;
REMOVE ("kõik" @NN>) + In (NEGATE 1 P)(NEGATE *1 S + In OR N + In BARRIER CLB) ;
REMOVE ("kõik" @NN>) + El (NEGATE 1 P)(NEGATE *1 S + El OR N + El BARRIER CLB) ;
REMOVE ("kõik" @NN>) + All (NEGATE 1 P)(NEGATE *1 S + All OR N + All BARRIER CLB) ;
REMOVE ("kõik" @NN>) + Ad (NEGATE 1 P)(NEGATE *1 S + Ad OR N + Ad BARRIER CLB) ;
REMOVE ("kõik" @NN>) + Abl (NEGATE 1 P)(NEGATE *1 S + Abl OR N + Abl BARRIER CLB) ;
REMOVE ("kõik" @NN>) + Tr (NEGATE 1 P)(NEGATE *1 S + Tr OR N + Tr BARRIER CLB) ;

SELECT ("kõik" @NN>) + Nom (1 P + Nom)(NEGATE 1 Prel) ;
SELECT ("kõik" @NN>) + Gen (1 P + Gen) ;
SELECT ("kõik" @NN>) + Par (1 P + Par) ;
SELECT ("kõik" @NN>) (0 IllAdt)(1 P + IllAdt) ;
SELECT ("kõik" @NN>) + In (1 P + In) ;
SELECT ("kõik" @NN>) + El (1 P + El) ;
SELECT ("kõik" @NN>) + All (1 P + All) ;
SELECT ("kõik" @NN>) + Ad (1 P + Ad) ;
SELECT ("kõik" @NN>) + Abl (1 P + Abl) ;
SELECT ("kõik" @NN>) + Tr (1 P + Tr) ;
SELECT ("kõik" @NN>) + Gen (1 P + NiNaTaGa) ;

SELECT ("kõik" @NN>) + PlNom (1 S + PlNom) ;
SELECT ("kõik" @NN>) + PlPar (1 S + PlPar) ;

SELECT ("kõik" @NN>) + PlNom (*1 S + PlNom BARRIER CLB OR NotEestäiend)
              (NEGATE 1 Koma) ;
SELECT ("kõik" @NN>) + PlPar (*1 S + PlPar BARRIER CLB OR NotEestäiend) ;

#Possessiivpronoomenid
SELECT Ppos + (@NN>) + Nom (*1 S + Nom BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + Par (*1 S + Par BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) (0 IllAdt)
              (*1 S + IllAdt BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + In (*1 S + In BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + El (*1 S + El BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + All (*1 S + All BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + Ad (*1 S + Ad BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + Abl (*1 S + Abl BARRIER Oeldis OR Võrdlus OR CLB) ;
SELECT Ppos + (@NN>) + Tr (*1 S + Tr BARRIER Oeldis OR Võrdlus OR CLB) ;

REMOVE:XTP Ppers + (@ADVL) (NEGATE *-1 Verb - Intr BARRIER CLB)(NEGATE *1 Verb - Intr BARRIER CLB); # TP: ei saa objekt olla
REMOVE:XTP Ppers + (@ADVL) (0 NGP)(NEGATE 1 LLZ OR <<<); # TP: ei tule pähe, et saaks olla
REMOVE Ppers + (@NN>) + Nom  ; # TP: tingimus (0 Ppers + Nom) läks sihti, muidu kustutas ka gen!!! Ka kõik teised reeglid !!!
REMOVE Ppers + (@<NN) + Nom (NEGATE *-1 Koolon BARRIER Koolon OR CLB) ; # TP: sama

# Viited jm (TP)

SELECT:XTPN Nord + (@NN>) (1 Aeg) ; # kuupäeva _1._ oktoober 1937
SELECT:XTPN Ncard + (@<NN) (-1 Aeg)(NEGATE -2 Nord)(NEGATE 1 Aeg) ; # aasta _1937_
SELECT:XTPN Ncard + (@<NN) (-1 Aeg)(-2 Nord)(-3 Aeg)(NEGATE 1 Aeg) ; # kuupäeva 1. oktoober _1937_
SELECT:XTPN Ncard + (@<NN) (-1 Koolon)(-2 Ncard)(-3 S + Scom)(NEGATE 1 Aeg) ; # seisuga 1 : 2
SELECT:XTP Y + (@<NN) OR S + (@<NN) (-1 Opr)(-2 Nimi) ; # 
SELECT:XTP Y + (@<NN) OR S + (@<NN) (-1 Opr)(-2 SPAN)(1 Cpr) ; # lühend või nimetus

SELECT:XTP (@NN>) (0 Cap)(-1 Opr)(-2 SPAN)(1 Cap) ;
SELECT:XTP (@SUBJ) (0 Cap)(-1 (@NN>))(-2 Opr)(-3 SPAN) ;
SELECT:XTP (@SUBJ) (0 Cap)(-1 Opr)(-2 SPAN) ;
# viited sulgudes, aga need on väga ebajärjepidevalt märgendatud
# sõltub tegelikult viidete vormistusest, nt P, PE või EP ; nom tingimust praegu ei pane Y pärast + nt Hargis

SELECT:XTP ("jt" @SUBJ) (-1 Nimi + (@SUBJ) OR Cap + (@SUBJ)) ;
SELECT:XTP ("al" @SUBJ) (-1 ("et"))(-2 Nimi + (@SUBJ) OR Cap + (@SUBJ)) ;
SELECT:XTP ("al" @SUBJ) (-1 ("et"))(-3 Nimi + (@SUBJ) OR Cap + (@SUBJ)) ;
# viited sulgudes, autorite taga et al.

SELECT:XTP (@ADVL) (0 Ncard)(-1 Scom)(-2 (@SUBJ))(1 Cpr) ;
# aastaN viidete taga sulgudes (pole küll subjektigrammatika, aga olgu siin koos)

REMOVE:XTP S + (@SUBJ) + Nom OR S + (@OBJ) (0 NimKv)(NEGATE 1 Par) ; # aasta tegija
REMOVE:XTP S + Gen + (@<Q) (-1 NimKv + (@NN>)) ; # aasta tegija
REMOVE:XTP S + Gen + (@<Q) (0 (@NN>) + Cap)(1 SPAN) ; 
SELECT:XTP S + Gen + (@<Q) (0 NimKv)(-1 Ncard)(NEGATE 1 Par) ; # üle saja meetri ellipsoidi pinnast
SELECT:XTP S + (@NN>) + Gen (-1 Ncard)(1 El) ;
SELECT:XTP S + (@NN>) + Gen (-1 Ncard)(1 S + Gen OR A + El)(2 El) ;
# 50% Eesti elanikest

REMOVE:XTP S + Gen + (@OBJ) (1 Gen + (@OBJ))(2 FinV OR CLB) ; 

# V KVANTORIGRAMMATIKA  
# 1) Nimisõnaline kvantor
# eelneb nimisonaline kvantor millega käänded sobivad.
SELECT Par + (@<Q) (-1 NimKv)(-1 Nom OR Par)(0 Par) ;
# pool tundi _aega_
SELECT Par + (@<Q) (-1 NimKv)(-1C SgGen + Objekt) ;
SELECT Gen + (@<Q) (-1 NimKv)(-1C Gen)(NEGATE -1 Objekt)(NEGATE 1 Gen) ;
SELECT Gen + (@<Q) (-1 NimKv)(-1C Gen)(NEGATE -1 Objekt OR S + (@NN>)) ;
SELECT (@<Q) (-1 NimKv + IllAdt)(0 IllAdt) ;
SELECT In + (@<Q) (-1 NimKv + In) ;
SELECT El + (@<Q) (-1 NimKv + El) ;
SELECT All + (@<Q) (-1 NimKv + All) ;
SELECT Ad + (@<Q) (-1 NimKv + Ad) ;
SELECT Abl + (@<Q) (-1 NimKv + Abl) ;
SELECT Tr + (@<Q) (-1 NimKv + Tr) ;
SELECT (@<Q) (-1 NimKv)(-1C Gen)(0 NiNaTaGa) ;

REMOVE (@<Q) (NEGATE 0 Par)(-1 Scom OR J)(-2 Par + (@<Q)) ;

# 2) Määrsõnaline kvantor
# a) järgneb kvantoradverb "täis".

SELECT Par + (@Q>) (1 Täis) ;
SELECT Par + (@Q>) (1 J)(2 (@Q>))(3 Täis) ;
#kvantoradverbi "täis" ei järgne
REMOVE (@Q>) (NEGATE 1 Täis) ;

# b) järellaiend
SELECT Par + (@<Q) (*-1 KvAdv BARRIER CLB OR NotParFV) ;
# Kui adverbi ja sona vahel ei ole teisi käändeid ega verbi

# 3) Arvsõnaline kvantor
SELECT SgPar + (@<Q) (-1 ArvKv)(NEGATE -1 ("üks"))(NEGATE 1 SgPar) ; 
# Kaks meisterkonspiraatorit tegid ettevalmistusi
SELECT SgGen + (@<Q) (-1 ArvKv)(NEGATE -1 ("üks"))(1 Kpost + KGen) ; # TP 
# 3.5 kraadi piires

# Eespool leidub Nkvantor, millega käänded sobivad:

# Adjühik - kvantori ja sona vahel ei tohi olla aastane, meetrine jne
SELECT SgGen + (@<Q) (*-1 ArvKv + SgGen ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q) OR Adjühik )(NEGATE 0 ArvKv)(NEGATE 0 Raha)
(NEGATE *1 SgGen BARRIER CLB OR NPpiir OR Kpost + KGen) ;

SELECT SgGen + (@<Q) (*-1 ArvKv + SgGen ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q) OR Adjühik )(NEGATE 0 ArvKv)(NEGATE 0 Raha)
(1 SP)(NEGATE 1 Gen) ;

SELECT SgPar + (@<Q) (*-1 ArvKv + SgPar ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;

SELECT Sg + (@<Q) (0 IllAdt)(*-1 ArvKv + Sg + IllAdt ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;

SELECT SgIn + (@<Q) (*-1 ArvKv + SgIn ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgEl + (@<Q) (*-1 ArvKv + SgEl ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgAll + (@<Q) (*-1 ArvKv + SgAll ^ ("üks") BARRIER CLBC OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgAd + (@<Q) (*-1 ArvKv + SgAd ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgAbl + (@<Q) (*-1 ArvKv + SgAbl ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgTr + (@<Q) (*-1 ArvKv + SgTr ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT Sg + (@<Q) (0 NiNaTaGa) (*-1 ArvKv + SgGen ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q) OR S + (@ADVL)) ;

SELECT SgGen + (@<Q) (*-1 ArvKv + NiNaTaGa + Sg ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT SgNom + (@<Q) (*-1 ArvKv + SgPar ^ ("üks") BARRIER CLB OR Oeldis OR Subjekt OR (@<Q)) ;
SELECT Sg + (@<Q) (-1 Nom) (-1 ArvKvErand) ;
SELECT SgPar + (@<Q) (*-1 ArvKv + SgNom ^ ("üks") BARRIER (@<Q) OR Verb OR CLB) ;

# Ees on N numbrina:
SELECT Par + (@<Q) (-1 Digit ^ Nord) ;
SELECT (@<Q) (0 ArvKv) (-1 Digit ^ Nord) ;
SELECT (@<Q) (0 NimKv)(-1 Digit ^ Nord) ;
SELECT (@<Q) (-1 Digit)(NEGATE -1 Nord OR (@<NN))(NEGATE 1 S OR Adj) ;

#4) Koordinatsioon:
SELECT Sg + (@<Q) (-2C Sg + (@<Q))(-1 JaVoi) ;
SELECT Pl + (@<Q) (-2C Pl + (@<Q))(-1 JaVoi) ;
SELECT (@<Q) (-3C (@<Q))(-2 JaVoi)(-1 Adv) ;
SELECT SgPar + (@<Q) (-1 Koma)(NEGATE -1 CLBC)(-2 SgPar)(-2C (@<Q)) ;
SELECT PlPar + (@<Q) (-1 Koma)(NEGATE -1 CLBC)(-2 PlPar)(-2C (@<Q)) ;

# 5) Märgendi eemaldamise reeglid
# Kui sona pole osastavas käändes ja ükski eelnev reegel teda @<Q-ks ei määranud, siis ta seda ka pole
REMOVE (@<Q) (NEGATE 0 Par OR Gen)(NEGATE *-1 Digit BARRIER CLB) ; # TP gen vabanemisel
REMOVE (@<Q) (NEGATE 0 Par)(*-1 Nord + Digit BARRIER CLB OR Ncard) ;

# Kui eespool ei ole kvantoreid ;
REMOVE Sg + (@<Q) (NEGATE *-1 ArvKv OR NimKv OR KvAdv BARRIER CLB OR Oeldis) ;
REMOVE Pl + (@<Q) (NEGATE *-1 NimKv OR KvAdv BARRIER CLB OR Oeldis) ;

REMOVE (@<Q) (NEGATE *-1 ArvKv OR NimKv BARRIER CLB)
             (NEGATE -1 KvAdv)(NEGATE -2 KvAdv)(NEGATE -3 KvAdv) ;
REMOVE Sg + (@<Q) (NEGATE *-1 ArvKv OR KvAdv BARRIER CLB)
             (*-1 NimKv + Pl) ;
REMOVE (@<Q) (*-1 ArvKv BARRIER CLB LINK *1C (@<Q) BARRIER NimKv OR KvAdv OR ArvKv) ;
REMOVE Par + (@<Q) (-1 JaVoi)(-2 Par)(NEGATE -2 (@<Q)) ;
REMOVE (@<Q) (-1 Verb) ;
REMOVE (@<Q) (*-1 ArvKv BARRIER CLB OR NimKv OR KvAdv LINK 1 Adjühik) ;
REMOVE Par + (@<Q) (*-1 NimKv BARRIER CLB OR ArvKv LINK NOT 0 Nom OR Gen OR Par) ;
SELECT (@<Q) (NEGATE 0 Gen)(NEGATE -1 Scom)
	(*-1 NimKv OR ArvKv OR KvAdv BARRIER NotEestäiend OR CLB LINK 0 Nom LINK NOT 1 Scom) ; #TP +-1Com
SELECT (@<Q) (NEGATE -1 Scom)(NEGATE *1 (@<Q) BARRIER NotEestäiend)
	(*-1 NimKv OR ArvKv OR KvAdv BARRIER NotEestäiend OR CLB LINK 0 Nom LINK NOT 1 Scom) ; #TP eelmine löödud kaheks: kas järgm pole <Q või ise pole gen
REMOVE Digit + (@<Q) (-1 Y) ;

#VIII KAASSONAFRAASI KUULUVAD POHJAD

# 1) Leidub ühene kaassona
# a) tagasõna
# tagasona korral tuleb kontrollida, ega sona ei kuulu kvanorifraasi
SELECT (@P>) (1 Kpost)(NEGATE 0 (@<Q)) ;
# kvantorid kaassõnafraasis
SELECT (@P>) (0 NimKv OR ArvKv OR KvAdv)(1C (@<Q))(2 Kpost) ;
SELECT (@P>) (0 NimKv OR ArvKv OR KvAdv)(1 Eestäiend)(2C (@<Q))(3 Kpost) ;
# kahe _vihkamise_ minuti jooksul
REMOVE (@P>) (-1 NimKv OR ArvKv OR KvAdv)(0 Eestäiend)(1C (@<Q))(2 Kpost) ;

# b) eessõna
# sona peab ka eessonaga ühilduma
SELECT Term + (@<P) (*-1 Kpre + KTerm BARRIER CLBC) ;
SELECT All + (@<P) (*-1 Kpre + KAll BARRIER CLBC) ;
SELECT El + (@<P) (*-1 Kpre + KEl BARRIER CLBC)(NEGATE *1 El BARRIER CLBC) ;
SELECT Abl + (@<P) (*-1 Kpre + KAbl BARRIER CLBC) ;
SELECT Kom + (@<P) (*-1 Kpre + KKom BARRIER CLBC) ;

SELECT Par + (@<P) (*-1C Kpre + KPar BARRIER Par + (@<P) OR CLBC)
             (*1 NPpiir BARRIER Par + (@<P) OR CLBC) ;
SELECT Par + (@<P) (*-1C Kpre + KPar BARRIER Par + (@<P) OR CLBC)
             (NEGATE *1 Par + (@<P) BARRIER CLBC) ;
SELECT Par + (@<P) (*-1C Kpre + KPar BARRIER Par + (@<P) OR CLBC)
             (1 ("ja") OR ("ning")) ;

SELECT Gen + (@<P) (*-1C Kpre + KGen BARRIER Gen + (@<P) OR CLBC)
             (*1 NPpiir BARRIER Gen + (@<P) OR CLBC) ;
SELECT Gen + (@<P) (*-1C Kpre + KGen BARRIER Gen + (@<P) OR CLBC)
             (NEGATE *1 Gen + (@<P) BARRIER CLBC) ;
SELECT Gen + (@<P) (*-1C Kpre + KGen BARRIER Gen + (@<P) OR CLBC)
             (1 ("ja") OR ("ning")) ;

#2) Lauses ei leidu teisi kaassonafraasi kuuluvaid pohju
SELECT (@P>) (NEGATE *-1 (@P>) BARRIER CLBC OR Oeldis)
             (*1C Kpost BARRIER (@P>) OR CLBC) ;
SELECT (@<P) (NEGATE *1 (@<P) BARRIER CLBC OR Oeldis)
             (*-1C Kpre BARRIER (@<P) OR CLBC) ;

#3) Juba leidus postpositsiooni pohi
REMOVE (@P>) (1C (@P>)) ;
REMOVE (@P>) (*1C Kpost BARRIER CLB OR Sid|Kvm LINK -1C (@P>)) ;
REMOVE (@P>) (*1C Kpost BARRIER CLB LINK -1C (@P>)) (NEGATE 1 Sid|Kvm) ;
REMOVE (@P>) (*1C Kpost BARRIER CLB LINK *-1 (@P>) LINK NOT 0 Järeltäiend) 
             (NEGATE 1 Sid|Kvm) ;
# juba leidus prepositsiooni pohi
REMOVE (@<P) (*-1 Kpre BARRIER CLB OR Sid|Kvm LINK *1C (@<P)) ;
REMOVE (@<P) (*-1 Kpre BARRIER CLB)(*1C (@<P) BARRIER CLB OR Sid|Kvm) ;

# 4) Antud sona ja kaassona vahele jääb verb
REMOVE (@P>) (NEGATE *1 Kpost BARRIER CLB) ;
REMOVE (@P>) (*1 Oeldis BARRIER Kpost OR CLB) ;
REMOVE (@<P) (NEGATE *-1 Kpre BARRIER CLB) ;
REMOVE (@<P) (*-1 Oeldis BARRIER Kpre OR CLB) ;

#7) Koordinatsioon
# tuleb vältida: valdade ja väikelinnade esindajate seas

SELECT Gen + (@P>) (1 JaVoiK)(*2C Gen + (@P>) BARRIER Verb OR CLBC)
             (NEGATE 2 Eestäiend) ;
SELECT Gen + (@P>) (0 Gen)(1 JaVoiK)(*2C Gen + (@P>) BARRIER Verb OR CLB)
             (2 (@AN>) OR (@DN>) OR (@KN>) OR (@INFN>)) ;
SELECT Gen + (@P>) (1 JaVoiK)(*2C Gen + (@P>) BARRIER Verb OR CLB)(NEGATE 2 Gen) ;

SELECT Par + (@P>) (1 JaVoiK)(*2C Par + (@P>) BARRIER Verb OR CLB) ;
SELECT El + (@P>) (1 JaVoiK)(*2C El + (@P>) BARRIER Verb OR CLB) ;

SELECT (@P>) (1 Opr)(*2 Cpr LINK 1 Kpost) ;

SELECT Gen + (@<P) (NEGATE 0 Eestäiend)
             (*-2C Gen + (@<P) BARRIER Verb OR CLB LINK 1 JaVoi) ;
SELECT Par + (@<P) (NEGATE 0 Eestäiend)
             (*-2C Par + (@<P) BARRIER Verb OR CLB LINK 1 JaVoi) ;

SELECT Gen + (@<P) (-1 JaVoi)(-2 Gen)(-2C (@<P))(NEGATE 1 S) ;
SELECT SgPar + (@<P) (-1 SgPar)(-2 JaVoi)(-3 SgPar)(-3C (@<P)) ;

#8) Kaks omastavas käändes nimisona eessonafraasi
# Päike paistis läbi köögiukse klaasi ja sitskardina
# Mis korra kuus üle mu lagede suundusid
# Kui verb on intransitiivne, on viimane sona p_hi.
REMOVE (@<P) (-1 Kpre + KGen)(0 Gen ^ N)(1 Gen)(NEGATE 0 NimKv) ;
# Verbi intransitiivsust esialgu ei kontrolli.
SELECT (@<P) (-1 Kpre + KGen)(0 Gen + N)(NEGATE 1 N) ;
# üle ühe mu elu votmestseeni


# II SUBJEKTIGRAMMATIKA

# 1) Verbi vorm välistab subjekti olemasolu 
# subjekti ei saa olla, kui selles osalauses on öeldis impersonaalis
REMOVE (@SUBJ) (*-1C Imps BARRIER CLBC OR Sid|Kvm LINK 0C Oeldis) ;
REMOVE (@SUBJ) (*1C Imps BARRIER CLBC OR Sid|Kvm LINK 0C Oeldis) ;

# 2) Sõna ei ühildu öeldisega
# Erand: eks _me_ tee neid tükke vahel nüüdki
SELECT Ppers + (@SUBJ) + Nom (*1 Imper + Vps2sg BARRIER CLB)
                (*-1 ("eks") BARRIER CLB) ;

# kui osalause öeldiseks on verb 1. või 2. pöördes, siis subjektiks
# saavad olla ainult isikulised asesõnad (vastavalt verbi vormile
# "mina", "sina", "meie", "teie"), kõik muu subjektiks ei ole

REMOVE (@SUBJ) (NEGATE 0 ("mina") OR ("ma"))(*-1C Vps1sg BARRIER Z OR CLBC) ;
REMOVE (@SUBJ) (NEGATE 0 ("mina") OR ("ma"))(*1C Vps1sg BARRIER Z OR CLBC) ;

REMOVE (@SUBJ) (NEGATE 0 ("sina") OR ("sa"))(*-1C Vps2sg BARRIER Z OR CLBC) ;
REMOVE (@SUBJ) (NEGATE 0 ("sina") OR ("sa"))(*1C Vps2sg BARRIER Z OR CLBC) ;
# Viga tekkis: eks nad varsti tee jälle süsti

REMOVE (@SUBJ) (NEGATE 0 Pps1 OR ("kõik"))
               (*1C Vps1pl BARRIER Sid|Kvm OR CLBC) ;
REMOVE (@SUBJ) (NEGATE 0 Pps1 OR ("kõik"))
               (*-1C Vps1pl BARRIER Sid|Kvm OR CLBC) ;
REMOVE (@SUBJ) (NEGATE 0 Pps2)(*1C Vps2pl BARRIER Sid|Kvm OR CLBC) ;
REMOVE (@SUBJ) (NEGATE 0 Pps2)(*-1C Vps2pl BARRIER Sid|Kvm OR CLBC) ;

# Santiiv või pronoomen mitmuse nominatiivis ei saa olla subjektiks,
# kui osalauses leidub verb ainsuses
REMOVE S + (@SUBJ) + PlNom OR P + (@SUBJ) + PlNom
               (*-1 Sg + FinV BARRIER Sid|Kvm OR CLBC) ;
REMOVE S + (@SUBJ) + PlNom OR P + (@SUBJ) + PlNom
               (*1 Sg + FinV BARRIER Sid|Kvm OR CLBC) ;
REMOVE SgNom + (@SUBJ) (*-1 PlVerb + FinV BARRIER Sid|Kvm OR CLBC LINK NOT 0 SgVerb)
               (NEGATE *1 (@<Q) BARRIER CLBC)(NEGATE 1 Sid|Kvm) ; # TP +NOT0sg
REMOVE SgNom + (@SUBJ) (*-1 PlVerb + FinV BARRIER Sid|Kvm OR CLBC)
               (NEGATE *1 (@<Q) BARRIER CLBC)(1 Koma)(NEGATE *1 S BARRIER CLBC) ;

# sõna partitiivis ei ole subjekt, kui osalauses leidub verb mitmuses
REMOVE Par + (@SUBJ) (*-1C PlVerb BARRIER CLBC)  ;
REMOVE Par + (@SUBJ) (*1C PlVerb BARRIER CLBC)  ;
# osastavas käändes nimi-asesona ei ole subjekt, kui osalause verb on esimeses voi teises pöördes.
REMOVE Par + (@SUBJ) (*-1C Vps1sg BARRIER CLB) ;
REMOVE Par + (@SUBJ) (*-1C Vps1pl BARRIER CLB) ;
REMOVE Par + (@SUBJ) (*-1C Vps2sg BARRIER CLB) ;
REMOVE Par + (@SUBJ) (*-1C Vps1pl BARRIER CLB) ;
# infinitiivi vorm saab olla subjekt üksnes siis, kui öeldiseks on verb
# 3. pöörde mitmuses, seega infinitiivi vorm ei ole subjekt, kui osalauses
# leidub verbe 1., 2. pöördes või 3. pöörde mitmuses
REMOVE Inf + (@SUBJ) (*-1C Vps1 OR Vps2 OR Vps3pl BARRIER CLBC) ;
REMOVE Inf + (@SUBJ) (*1C Vps1 OR Vps2 OR Vps3pl BARRIER CLBC) ;

# kui "mina", "meie", "teie" ühildub öeldisega, siis ta on alus
SELECT ("mina" @SUBJ) + SgNom OR ("ma" @SUBJ) + SgNom (*-1 Vps1sg BARRIER CLB) ;
SELECT ("mina" @SUBJ) + SgNom OR ("ma" @SUBJ) + SgNom (*1 Vps1sg BARRIER CLB) ;
SELECT ("mina" @SUBJ) + PlNom OR ("ma" @SUBJ) + PlNom (*-1 Vps1pl BARRIER CLB) ;
SELECT ("mina" @SUBJ) + PlNom OR ("ma" @SUBJ) + PlNom (*1 Vps1pl BARRIER CLB) ;
SELECT ("sina" @SUBJ) + PlNom OR ("sa" @SUBJ) + PlNom (*-1 Vps2pl BARRIER CLB) ;
SELECT ("sina" @SUBJ) + PlNom OR ("sa" @SUBJ) + PlNom (*1 Vps2pl BARRIER CLB) ;

# 3) Unikaalsus
# kui osalauses leidub ühene subjekt, siis rohkem subjekte olla ei saa,
# välja Natud koordineeritud subjektid
REMOVE (@SUBJ) (*-1C Subjekt BARRIER Sid|Kvm OR CLBC)(NEGATE 0 Nimi) ;
# Vead tekivad selletõttu, et kuskil midagi valesti määratud.
# Töömees on _eestlane_ küll, ...
# nad hoidsid teda ka nagu minu _vanemad_ mind
REMOVE (@SUBJ) (*1C Subjekt BARRIER Sid|Kvm OR CLBC)(NEGATE 0 Nimi) ;
REMOVE (@SUBJ) (*-1C Subjekt BARRIER CLB LINK *1 FinV BARRIER Sid|Kvm) ;
# Langend Patroklos ja keeb tema ümber nüüd metsikuim _võitlus_
REMOVE (@SUBJ) (*1C Subjekt BARRIER CLBC LINK *-1 FinV BARRIER Sid|Kvm) ;

# 4) Lause ehitusest soltuvad reeglid

# kui kõrvuti paiknevad kaks nime nominatiivis ja mõlemad võivad olla
# subjektiks, siis esimene ei ole subjekt
REMOVE (@SUBJ) (0 Nom)(1C Sprop + Nom)(1 Subjekt)(NEGATE 0 Pindef OR Ppers) ;
# Santiiv või pronoomen partitiivi mitmuses on subjekt, kui osalauses
# pole muud ühest subjekti ja öeldiseks on verb "olema"
SELECT SP + PlPar + (@SUBJ) (*-1 Olema BARRIER Verb OR CLB)
               (NEGATE *1 Verb BARRIER CLB)
               (NEGATE *-1 Subjekt BARRIER CLB)
               (NEGATE *1C Subjekt BARRIER CLB)
               (NEGATE *-1 NimKv OR KvAdv OR ("üks") BARRIER CLB) ;
# Viga tekkis enne: et on _teisigi_ Partei vaenlasi
# TP: oli: (NEGATE *-1C Subjekt BARRIER CLB)

# sõna partitiivis pole subjekt, kui talle järgneb da-infinitiivne öeldis
REMOVE Par + (@SUBJ) (1 Inf)(1C Oeldis) ; # TP Par viidud Subj juurde, oli 0-ting
# kui kõrvuti on nominatiivis ase- ja nimisõna, siis asesõna ei ole subjekt
REMOVE P + (@SUBJ) + SgNom (1 S + SgNom)(NEGATE -1 Olema)(NEGATE 0 Ppers) ;
REMOVE P + (@SUBJ) + SgNom (1 S + SgNom)(-1 Olema)
               (*1 NudTud BARRIER CLBC)(NEGATE 0 Ppers) ; 
# Kuid praegusel hetkel oli _see_ monoloogki katkenud.
# Aga: Veel enam aga oli see enesehüpnoos.
# Aga: Kui _sa_ siga alla ei anna, siis ..

# sõna partitiivis pole subjekt, kui öeldis on sellest sõnast paremal ja
# öeldisest paremal osalauses leidub nimi- või asesõna nominatiivis
# Kombinatsioon SubjPar Fv Obj(nom,gen) esineb va"ga hNa
REMOVE Par + (@SUBJ) (NEGATE *-1 Oeldis BARRIER CLB)
               (*1C Oeldis BARRIER Z OR CLB)
               (*1 SPNom BARRIER CLB LINK NOT 0 Aeg) ;

# da-infinitiivid
SELECT Inf + (@SUBJ) (NEGATE *-1 Inf BARRIER CLB)
           (NEGATE *1 Inf BARRIER CLB)
           (*-1 Suutelisusv OR Emotsioonv OR Sobivusv BARRIER CLB LINK 0C Oeldis) ;
SELECT Inf + (@SUBJ) (NEGATE *-1 Inf BARRIER CLB)
           (NEGATE *1 Inf BARRIER CLB)
           (*1 Suutelisusv OR Emotsioonv OR Sobivusv BARRIER CLB LINK 0C Oeldis) ;
SELECT Inf + (@SUBJ) (NEGATE *-1 Inf BARRIER CLB)
               (NEGATE *1 Inf BARRIER CLB)
               (*-1 Vajadusv BARRIER CLB OR SPNom LINK 0C Oeldis) ;
# et vältida: tuli mõte tegema hakata
SELECT Inf + (@SUBJ) (NEGATE *-1 Inf BARRIER CLB)
               (NEGATE *1 Inf BARRIER CLB)
               (*1 Vajadusv BARRIER CLB LINK 0C Oeldis) ;

SELECT Inf + (@SUBJ) (*-1 Vaja BARRIER CLBC) ;
SELECT Inf + (@SUBJ) (*1 Vaja BARRIER CLBC) ;

# da-infinitiiv on subjekt, kui osalauses: öeldiseks on "olema" (üksinda),
# leidub predikatiiv ja puudub nimi- või asesõnaline nominatiivne subjekt
SELECT Inf + (@SUBJ) (*-1C Olema + Main + FinV BARRIER CLB)
               (*-1C (@PRD) BARRIER CLB)
               (NEGATE *-1 SPNom BARRIER CLB)
               (NEGATE *1 SPNom BARRIER CLB) ;
#... aga voimatu oli seda täiesti välja lülitada
#... muidugi oli täiesti voimatu ütelda

# da-infinitiiv on subjekt, kui osalauses esineb järgmine konstruktsioon:
# eelmine sõna on predikatiiv nominatiivis, üleelmine on "olema" ning
# osalauses vasakul ei esine muid sõnu nominatiivis ega partitiivis

SELECT Inf + (@SUBJ) (-2 Olema)(-1C (@PRD))(-1 Nom)
               (NEGATE *-2 Nom BARRIER CLB)(NEGATE *-1 Par BARRIER CLB) ;
# Mul on raske uskuda vrs Laud on raske tosta
# da-infinitiiv pole subjekt, kui lauses leiduvad nii nimi- voi asesona nimetavas kui osastavas käändes
REMOVE Inf + (@SUBJ) (*-1 SP + SgNom BARRIER CLB OR Verb LINK *1C SP + Par) ;

# Nimetavas käändes sona on subjekt, kui lauses on ainult üks nominatiiv ja verb ei ole impersonaal jms.
SELECT Nom + (@SUBJ) (NEGATE 0 N OR Verb)
               (NEGATE *-1 Koma OR ("ja") OR ("ning") OR SPNom OR 
                        Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2)
               (NEGATE *1 Koma OR SPNom OR 
                        Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2)
               (NEGATE *1 Ncard + Nom BARRIER CLB LINK 1 SgPar LINK NOT 0 Aeg)
               (NEGATE 1 Inf) ;
SELECT Nom + (@SUBJ) (NEGATE 0 N)(*0 Verb BARRIER CLB)
               (NEGATE *-1 Koma OR ("ja") OR ("ning") OR SPNom OR 
                        Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2)
               (NEGATE *1 Koma OR SPNom OR 
                        Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2)
               (NEGATE *1 Ncard + Nom BARRIER CLB LINK 1 SgPar LINK NOT 0 Aeg)
               (NEGATE 1 Inf) ;
# Viga: Või siis kusagilt tihnikust ohvrile vibunool kõrri lasta
SELECT Nom + (@SUBJ) (NEGATE 0 N OR Nimi)
         (NEGATE *-1 Koma OR Cpr OR ("ja") OR ("ning") OR SPNom OR 
         Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2 BARRIER CLB) 
         (NEGATE *1 SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR 
                         Vps1 OR Vps2 BARRIER CLB)
         (*1 Koma BARRIER SP OR CLB)(*-1 FinV BARRIER CLB)
         (NEGATE -1 Partic)(NEGATE 1 Inf) ; 
SELECT (@SUBJ) + Sprop + Nom (NEGATE 1 Nimi)
               (NEGATE *-1 Koma OR ("ja") OR ("ning") OR SPNom OR 
               Inf + (@SUBJ) OR Imps OR Imper OR Vps1 OR Vps2 BARRIER CLB)
               (NEGATE *1 SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR 
                         Vps1 OR Vps2 BARRIER CLB)
               (*1 Koma BARRIER SP OR CLB)(*-1 FinV BARRIER CLB)
               (NEGATE -1 Partic)(NEGATE 1 Inf) ;      
# vaid on unustanud _kõik_ selle


SELECT Nom + (@SUBJ) (NEGATE 0 N)(*1 Koma BARRIER CLBC LINK NOT *1 SP BARRIER CLBC)
               (NEGATE *-1 Koma OR Ja OR SPNom OR Inf + (@SUBJ) OR Imps OR Imper
		 OR Vps12 BARRIER CLBC)
               (NEGATE *1 SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12
                   BARRIER CLBC)
               (*1 (@FMV) OR (@FCV) BARRIER CLBC)(NEGATE 1 Inf) ;
SELECT SgNom + (@SUBJ) (1C Vps3sg)
    (NEGATE *-1 Koma OR Ja OR SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12)   
    (NEGATE *1 Koma OR SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12) ;

SELECT S + (@SUBJ) + Nom 
   (NEGATE *-1 Ja OR SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12 BARRIER CLBC)
   (NEGATE *1 SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12 BARRIER CLBC)
   (*-1 (@FMV) ^ Prverb OR (@FCV) ^ Prverb BARRIER CLBC)
   (NEGATE 1 Inf)(NEGATE -1 Võrdlus)  ;

#Vead:
#Sinu numbrid on juba nii mustad, et ajavad juuksed peas püsti.
#Lennartil läheb punast marlborot sigaret sigareti järel
#Kas ei saaks tund aega
SELECT S + (@SUBJ) + Nom 
               (NEGATE *-1 Ja OR SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR
		 Vps12 BARRIER CLB)
               (NEGATE *1 SPNom OR Inf + (@SUBJ) OR Imps OR Imper OR Vps12
		 BARRIER CLB)
               (*1 (@FMV) OR (@FCV) BARRIER CLB)(NEGATE 1 Inf) ;

# Viga: Sõna Ingsots kord ilmus, kord kadus.

SELECT Nom + (@SUBJ) (NEGATE 0 (@ADVL))(NEGATE 1 Nimi)
               (*-1 FinV ^ Imps BARRIER CLB LINK NOT -1 J)
               (NEGATE *-1 Subjekt OR Tud OR Imper OR Inf BARRIER CLB)
               (NEGATE *1 Subjekt OR Tud OR Imper OR Inf BARRIER CLB) ;

#Viga: Viisid minema kõik kuld- ja hõberiistad.

# Kvantoradverb on subjekt, kui pole nimetavas käändes nimi- ega asesonu ega
# subjekti olemasolu välistavaid verbivorme.


SELECT (@SUBJ) (0 KvAdv)(1C (@<Q))
               (NEGATE *-1 Koma OR SPNomPar OR Inf + (@SUBJ) OR Imps OR Imper OR Ja)
               (NEGATE *1 Koma OR SPNomPar OR Inf + (@SUBJ) OR Imps OR Imper) ;
SELECT (@SUBJ) (0 KvPron + NomPar)(1C (@<Q))
               (NEGATE *-1 Koma OR SPNomPar OR Inf + (@SUBJ) OR Imps OR Imper OR Ja)
               (NEGATE *1 Koma OR SPNomPar OR Inf + (@SUBJ) OR Imps OR Imper) ;

SELECT Nom + (@SUBJ) (NEGATE 0 N)
               (NEGATE *-1 Koma OR SPNomPar OR Subjekt OR Imps OR Imper OR
                Ja OR Vps12)
               (NEGATE *1 Koma OR SPNomPar OR Subjekt OR Imps OR Imper OR Vps12)
               (NEGATE 1 Inf) ;
 
SELECT Nom + (@SUBJ) (-1 Neg)(1 Partic) ;

SELECT PlNom + (@SUBJ) (1 Vps3pl) (*1 Supiin + In BARRIER CLB) ;
SELECT ("see" @SUBJ) + Nom (1 Olema)(*1 SPNom BARRIER CLBC) ;
SELECT ("kes" @SUBJ) (-1 Koma)(NEGATE *1 Imps) ;

# Arvsõnad
# N nimetavas käändes on subjekt, kui verb on mitmuse 3. pöördes ja osalauses
# pole mitmuse nimetavat käänet.
SELECT N + (@SUBJ) + Nom (NEGATE 0 ("üks"))(*-1C Vps3pl BARRIER CLB)
                  (NEGATE *-1 PlNom OR Imps BARRIER CLB)
                  (NEGATE *1 PlNom OR Imps BARRIER CLB) ;
SELECT N + (@SUBJ) + Nom (NEGATE 0 ("üks"))(*1C Vps3pl BARRIER CLB)
                  (NEGATE *-1 PlNom OR Imps BARRIER CLB)
                  (NEGATE *1 PlNom OR Imps BARRIER CLB) ;
# 5) Koordinatsioon
# kui sõnade "nii" ja "kui ka" abil koordinatsioonis olevatest sõnadest üks
# ei ole subjekt, siis ei ole seda ka teine
REMOVE (@SUBJ) (-1 Nii)(1 ("kui"))(2 Ka)(NEGATE 3 Subjekt)(4 Verb) ;
REMOVE (@SUBJ) (-4 Nii)(NEGATE -3 Subjekt)(-2 ("kui"))(-1 Ka)(1 Verb) ;
# kui mitmuse partitiivis nimi- või asesõna on on koordineeritud (sidesõnaga)
# samas vormis ühese (kindla) subjektiga, siis ta on ka subjekt
SELECT (@SUBJ) (-2 SP + PlPar)(-2C Subjekt)
               (-1 JaVoi)(0 SP + PlPar)(1 CLB) ;
# kui fraas on tüüpi "suured apelsinid, väikesed õunad" ja "apelsinid" on
# subjekt, siis ka "õunad on subjekt"
SELECT (@SUBJ) (-4 A + PlNom)(-3 S + PlNom)(-2 Koma)
               (-1 A + PlNom)(0 S + PlNom)(-3C Subjekt) ;
SELECT S + (@SUBJ) + PlNom (-1 Adj)(-2 JaVoiK)(-3 PlNom)(-3C Subjekt) ;
SELECT S + (@SUBJ) + PlNom (-1 Eestäiend)(-2 Eestäiend)(-3 JaVoiK)
               (-4 PlNom)(-4C Subjekt) ;
SELECT PlNom + (@SUBJ) (NEGATE 0 Pdet)(-1 JaVoi ^ CLB)(-2 PlNom)(-2C Subjekt) ;
SELECT PlNom + (@SUBJ) (1 JaVoi ^ CLB)(2 PlNom)(2C Subjekt)  ;
SELECT SgNom + (@SUBJ) (-1 JaVoi ^ CLB)(-2 SgNom)(-2C Subjekt)  ;
SELECT SgNom + (@SUBJ) (1 JaVoi ^ CLB)(2 SgNom)(2C Subjekt)  ;

#6) Määrsonalised kvantorid
# määrsõnaline kvantor ei saa olla subjekt, kui paremas kontekstis
# ei leidu kvantori võimalikku laiendit partitiivis
REMOVE Adv + (@SUBJ) (NEGATE 1 Adv OR Par) ;
REMOVE Adv + (@SUBJ) (1 Adv)(NEGATE 2 Par) ;

#7) Asesona ise
REMOVE SP + (@SUBJ) (NEGATE 0 Nom OR Par) ;
REMOVE (@SUBJ) (0 Ise) ;

#8) Sona on sulgudes
SELECT Y + (@SUBJ) (-1 Opr)(1 Cpr)(-2C Subjekt) ;
REMOVE Y + (@SUBJ) (-1 Opr)(1 Cpr)(NEGATE -2 Subjekt) ;
# kui sõna taga on seda tähendav lühend, siis loetakse et nad on
# koordinatsioonis nagu ehk
REMOVE (@SUBJ) (*-1 Opr BARRIER Cpr OR FinV LINK *1 SP BARRIER Cpr)
               (*1 Cpr BARRIER Opr OR FinV) ;
REMOVE (@SUBJ) (*-1 Opr BARRIER Cpr OR FinV )
               (*1 Cpr BARRIER Opr OR FinV LINK *-1 SP BARRIER Opr) ;

# kuid sona ei tohi olla seal üksi
# need on enamasti järellisandid

# III Objektigrammatika

# 1) Teatud liiki öeldised pohjustavad da-infintiivse objekti
SELECT Inf + (@OBJ) (*-1 InfP ^ Inf BARRIER CLB LINK NOT 1 JaVoi) ;
SELECT Inf + (@OBJ) (*1 InfP BARRIER Koma OR CLB) ;
SELECT Inf + (@OBJ) (1 NudTud) ; # oli: (1 (A partic past)) ;
REMOVE Inf + (@OBJ) (NEGATE *-1 InfP BARRIER CLBC)
              (NEGATE *1 InfP BARRIER CLB) ;

# 2) öeldise vorm voi liik välistab objekti
# a) öeldis on intransitiivne
REMOVE (@OBJ) (NEGATE *-1 Verb BARRIER CLB)(NEGATE 1 Partic)
              (*1C Intr&YV BARRIER Verb ^ Intr&YV ^ EiAra OR CLB 
                   LINK NOT *1 Verb BARRIER CLBC) ;

REMOVE (@OBJ) (NEGATE *1 Verb BARRIER CLB)(NEGATE 1 Partic)
              (*-1C Intr&YV BARRIER Verb ^ Intr&YV ^ EiAra OR CLB 
                    LINK NOT *-1 Verb BARRIER CLB) ; 

# Vead: 
# Nagu võiks mõnda teist inimest, kes ei ole nii arukas, sellega ära petta.
# Ta tegi, ..., esikupeegli ees ... grimassi
# Ta võttis pakist, kus oli ..., ühe sigareti.

REMOVE (@OBJ) (NEGATE *1 Verb BARRIER CLB)(NEGATE 1 Partic)
              (*-1C Intr&YV BARRIER Verb ^ Intr&YV ^ EiAra OR CLB 
                    LINK -1 Ei LINK NOT *-1 Verb BARRIER CLB) ; 

REMOVE (@OBJ) (NEGATE 1 Partic)
              (*-1C Intr&YV BARRIER Verb ^ Intr&YV ^ EiAra OR CLB 
                    LINK NOT *-1 Verb BARRIER CLB)
              (*1C Intr&YV BARRIER Verb OR CLB LINK NOT *1 Verb BARRIER CLB) ; 

REMOVE (@OBJ)(0 SgNom)(*1C Intr&YV + Main BARRIER Verb ^ Intr&YV ^ EiAra OR CLBC)
             (NEGATE *1 Partic OR Inf BARRIER CLBC)(NEGATE *-1 Verb BARRIER CLBC) ;

# see oli olnud ...
REMOVE (@OBJ) (NEGATE *-1 Verb BARRIER CLB)
              (*1C Intr&YV BARRIER CLB OR Verb ^ Intr&YV ^ EiAra 
                   LINK *1C Intr&YV BARRIER CLB LINK NOT *1 Verb BARRIER CLB) ;
# Viga: Sammu sõitma hakkasime alles siis, ...

#oli see olnud ...
REMOVE (@OBJ) (*-1C Intr&YV BARRIER CLB LINK NOT *-1 Verb BARRIER CLB)
              (*1C Intr&YV BARRIER CLB LINK NOT *1 Verb BARRIER CLB) ;
REMOVE (@OBJ) (*-1C Intr&YV BARRIER CLB LINK -1  Ei LINK NOT *-1 Verb BARRIER CLB)
              (*1C Intr&YV BARRIER CLB LINK NOT *1 Verb BARRIER CLB) ;
# ei olnud säilinud midagi
# jäi korraks õhku rippuma nagu porikärbes
REMOVE (@OBJ) (*-1C Intr&YV BARRIER CLB OR Verb ^ Intr&YV ^ EiAra
                    LINK *-1C Intr&YV BARRIER CLB 
                    LINK NOT *-1 Verb BARRIER CLB)
              (NEGATE *1 Verb BARRIER CLB) ;

REMOVE (@OBJ) (*-1C Intr&YV BARRIER CLB
                    LINK *-1C Intr&YV BARRIER CLB
                    LINK -1 Ei 
                    LINK NOT *-1 Verb BARRIER CLB)
              (NEGATE *1 Verb BARRIER CLB) ;
REMOVE (@OBJ) (*-1C Intr&YV BARRIER CLB OR Verb ^ Intr&YV ^ EiAra
                    LINK *-1C Intr&YV BARRIER CLB OR Verb ^ Intr&YV ^ EiAra
                    LINK NOT *-1 Verb BARRIER CLB)
              (*1 Kvm BARRIER Verb OR CLB) ;

#b) öeldis on eitavas kones

REMOVE (@OBJ) (0 NimOm)(NEGATE *1 Kysi OR Tud BARRIER CLB)
              (*-1 Ei BARRIER CLB)
              (NEGATE *-1 Tud BARRIER CLB) ;
REMOVE (@OBJ) (0 NimOm)(NEGATE *1 Kysi OR Tud BARRIER CLB)
              (*1 Ei BARRIER CLB)
              (NEGATE *-1 Tud BARRIER CLB) ;
REMOVE Gen + (@OBJ) (NEGATE *1 Kysi BARRIER CLB)(*-1 Ei BARRIER CLB) ;
REMOVE Gen + (@OBJ) (NEGATE *1 Kysi BARRIER CLB)(*1 Ei BARRIER CLB) ;

#c) Isikuline tegumood välistab ainsuse nominatiivse objekti
REMOVE SgNom + (@OBJ) (NEGATE *-1 Imps OR Imper OR InfSupiin BARRIER CLBC)
              (NEGATE *1 Imps OR Imper OR InfSupiin BARRIER CLBC)
              (NEGATE 0 N OR Mis OR Koik) ;
# Viga: Palun see suurem sealt

#d) Umbisikuline tegumood voi käskiv koneviis välistab genitiivse objekti
REMOVE Gen + (@OBJ) (*-1C Imps BARRIER CLBC) ;
REMOVE Gen + (@OBJ) (*1C Imps BARRIER CLBC) ;
REMOVE Gen + (@OBJ) (*-1C Imper BARRIER CLBC) ;
REMOVE Gen + (@OBJ) (*1C Imper BARRIER CLBC) ;

#e) Verb nouab partitiivset objekti

SELECT (@OBJ) (*-1 PartObjVerb BARRIER CLBC)
              (0 Par ^ Aeg)
              (NEGATE *-1 Par + (@OBJ) OR Ära BARRIER CLBC )
              (NEGATE *1 Par + (@OBJ) OR Ära BARRIER CLBC) ;
#Viga: Kui ülempreester ei tahtnud loomulikult _sellist_ rahakat ning -...

SELECT (@OBJ) (*1 PartObjVerb BARRIER CLBC)
              (0 Par ^ Aeg)
              (NEGATE *-1 Par + (@OBJ) OR Ära BARRIER CLBC )
              (NEGATE *1 Par + (@OBJ) OR Ära BARRIER CLBC) ;

REMOVE (@OBJ) (NEGATE 0 Par OR Inf OR Pl)
              (*-1 PartObjVerb + Ps BARRIER CLB OR Verb ^ PartObjVerb 
                   LINK NOT *-1 Verb BARRIER CLB)
              (NEGATE *1 Verb BARRIER CLB)
              (NEGATE *-1 Tr OR Ära BARRIER CLB)(NEGATE *1 Tr OR Ära BARRIER CLB)
              (NEGATE 1 Partic)  ;

REMOVE (@OBJ) (NEGATE 0 Par OR Inf OR Pl)
              (*1 PartObjVerb + Ps BARRIER CLB OR Verb ^ PartObjVerb 
                   LINK NOT *1 Verb BARRIER CLB)
              (NEGATE *-1 Verb BARRIER CLB)
              (NEGATE *-1 Tr OR Ära BARRIER CLB)(NEGATE *1 Tr OR Ära BARRIER CLB)
              (NEGATE 1 Partic)  ;

#3) Erijuhud

# Kui objekt ja subjekt on samas vormis, siis tavaliselt subjekt eelneb objektile
REMOVE (@OBJ) (0 (@SUBJ) + PlNom)
              (NEGATE *-1 Subjekt BARRIER CLB)
              (*1 FinV BARRIER CLB LINK *1 (@OBJ) + PlNom BARRIER Sid|Kvm OR CLB) ;
# Esimeses kahes isikus isikulised asesonad ei ole nom objektid 
REMOVE (@OBJ) (0 Ppers + Pps1 + Nom OR Ppers + Pps2 + Nom) ;
# kui osalauses leidub kaks sona, mis voivad olla nii objektiks kui ka
# subjektiks, üks nominatiivne ja teine partitiivne, siis nominatiivne ei ole
# objekt

REMOVE (@OBJ) (0 (@SUBJ) + Nom)(NEGATE 1 Partic)
              (NEGATE *-1 InfSupiin BARRIER CLB)
	      (NEGATE *-1 InfSupiin BARRIER CLB)
              (*1 (@OBJ) + Par ^ AtrAdv BARRIER CLB) ;

# kombinatsioon Obj(nom/gen) Fv SubjNom ei ole võimalik

REMOVE Gen + (@OBJ) (NEGATE 1 Oeldis OR CLB)
              (NEGATE *-1 Oeldis OR NudTud BARRIER CLB)
              (*2C Oeldis BARRIER CLB LINK *1 SPNom BARRIER Kvm OR CLB) ;

REMOVE SgNom + (@OBJ) (NEGATE 0 Mis)(NEGATE *1 Ise BARRIER CLB)
              (NEGATE *-1 Oeldis BARRIER CLB)
              (*1C Oeldis BARRIER CLB LINK *1 SPNom BARRIER Sid|Kvm OR CLB) ;
# On voimalik: ta sai aru, kui suure _asja_ ta on ette votnud
# Juba tempel ise on püstitatud ...

#Arvsõnad ei ole genitiivsed objektid
REMOVE N + Gen + (@OBJ) (NEGATE 0 ("üks")) ;
#Tud-vormile ei eelne gen objekti
REMOVE Gen + (@OBJ) (1 Tud) ;
#      Nt tema lukustatud kabinetis


#Kui nud-, tud- v_i des-vormile eelneb vahetult partitiiv , siit ta on objekt
SELECT (@OBJ) (0 Par)(1 NudTudDes ^ Intr&YV)(NEGATE 0 (@<P) OR Aeg OR Pdem)
              (NEGATE *-1 Neg BARRIER CLB) ;

SELECT Pdem + Par + (@OBJ) (1 NudTudDes ^ Intr&YV)(NEGATE 0 (@<P) OR Aeg)
              (NEGATE *-1 Neg BARRIER CLB)(*1 NPpiir BARRIER Par OR CLB) ;

# Osastavas käändes sõna enne partitsiipi on suure tõenäosusega sihitis,
# v.a. määrused
SELECT S + Par + (@OBJ) (NEGATE 0 (@ADVL))(1 Partic ^ Intr&YV) ;

# Kui lauses pole verbi, ei ole ka nominatiivset objekti
REMOVE Nom + (@OBJ) (NEGATE *1 Verb)(NEGATE *-1 Verb) ;

#Kaassonafraasis ei ole objekti
REMOVE (@OBJ) (*-1 Kpre BARRIER (@<P) OR CLB)
              (*1 (@<P) BARRIER Kpre OR CLB) ;
# Kui omastav ja osastav kääne on korvuti ja molemad voivad olla objektid, siis
# omastav ei ole objekt, v.a erandid
REMOVE Gen + (@OBJ) (1 (@OBJ) + Par)(NEGATE 1 (@Q>) OR (@<P) OR (@P>))
              (NEGATE 2 Partic OR InfSupiin) ;
# Viga: Ta võttis südame rindu

REMOVE ("oma" @OBJ) (1 (@OBJ) + Par)(NEGATE 2 Partic OR InfSupiin) ;
REMOVE Gen + (@OBJ) (1 A + Par)(2 (@OBJ) + Par)(NEGATE 3 Partic OR InfSupiin) ;
REMOVE ("oma" @OBJ) (1 A + Par)(2 (@OBJ) + Par)(NEGATE 3 Partic OR InfSupiin) ;

#4) Unikaalsus
# kui osalauses ei leidu partitsiipe, gerundiive, supiine ega infinitiive
# ning leidub kindel objekt, kusjuures ei leidu rinnastavaid komasid ega
# sidesonu,  siis osalauses ei saa olla rohkem objekte


REMOVE (@OBJ)(NEGATE 0 Inf)
             (NEGATE *1 Partic OR SupInfGer BARRIER CLB)
             (NEGATE *-1 Partic OR SupInfGer BARRIER CLB)
             (*1C Objekt BARRIER Sid|Kvm OR CLB)  ;
#Viga tekkis siis, kui eelnevalt valesti määratud:
#Laseks ta nooli täis nagu Püha Sebastianuse
REMOVE (@OBJ)(NEGATE 0 Inf)
             (NEGATE *1 Partic OR SupInfGer BARRIER CLB)
             (NEGATE *-1 Partic OR SupInfGer BARRIER CLB)
             (*-1C Objekt BARRIER Sid|Kvm OR CLB)  ;

#või vaeglauses
#nemad hoidsid teda ka nagu minu vanemad mind
#pani ka noor kirjanik nõukogudemaad kiitvaid lauseid paberile

#riik sai miljardeid kroone kahjumit 



REMOVE (@OBJ)(*-1 Aux BARRIER CLB)
             (*1 Partic BARRIER CLB)
             (*1C Objekt BARRIER Sid|Kvm OR CLB)
             (NEGATE *-1 SupInfGer BARRIER CLB)(NEGATE *1 SupInfGer BARRIER CLB) ;
REMOVE (@OBJ)(*-1 Aux BARRIER CLB)
             (*-1 Partic BARRIER CLB)
             (*-1C Objekt BARRIER Sid|Kvm OR CLB)
             (NEGATE *-1 SupInfGer BARRIER CLB)(NEGATE *1 SupInfGer BARRIER CLB) ;
REMOVE (@OBJ)(*1 Aux BARRIER CLB)
             (*1 Partic BARRIER CLB)
             (*1C Objekt BARRIER Sid|Kvm OR CLB)
             (NEGATE *-1 SupInfGer BARRIER CLB)(NEGATE *1 SupInfGer BARRIER CLB) ;

# 5) Koordinatsioon
# kui koordinatsioonis olevatest samas vormis sonadest üks ei ole objekt,
# siis ei ole seda ka teine
REMOVE (@OBJ) (-1 Nii)(1 Kui)(2 Ka)(NEGATE 3 Objekt)(4 Verb) ;
REMOVE (@OBJ) (-4 Nii)(NEGATE -3 Objekt)(-2 Kui)(-1 Ka)(1 Verb) ;

REMOVE SP + SgNom + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgNom)(NEGATE 2 Objekt OR Eestäiend) ;
REMOVE SP + PlNom + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + PlNom)(NEGATE 2 Objekt OR Eestäiend) ;
REMOVE SP + SgGen + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgGen)(NEGATE 2 Objekt OR Eestäiend) ;
REMOVE SP + SgPar + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgPar)(NEGATE 2 Objekt OR Eestäiend) ;
REMOVE SP + PlPar + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + PlPar)(NEGATE 2 Objekt OR Eestäiend) ;
REMOVE SP + SgNom + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgNom)(NEGATE -2 Objekt) ;
REMOVE SP + PlNom + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + PlNom)(NEGATE -2 Objekt) ;
REMOVE SP + SgGen + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgGen)(NEGATE -2 Objekt) ;
REMOVE SP + SgPar + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgPar)(NEGATE -2 Objekt) ;
REMOVE SP + PlPar + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + PlPar)(NEGATE -2 Objekt) ;

SELECT SP + SgNom + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgNom)(2C Objekt) ;
SELECT SP + PlNom + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + PlNom)(2C Objekt) ;
SELECT SP + SgGen + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgGen)(2C Objekt) ;
SELECT SP + SgPar + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + SgPar)(2C Objekt) ;
SELECT SP + PlPar + (@OBJ) (1 JaVoi ^ CLBC)
              (2 SP + PlPar)(2C Objekt) ;
SELECT SP + SgNom + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgNom)(-2C Objekt) ;
SELECT SP + PlNom + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + PlNom)(-2C Objekt) ;
SELECT SP + SgGen + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgGen)(-2C Objekt) ;
SELECT SP + SgPar + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + SgPar)(-2C Objekt) ;
SELECT SP + PlPar + (@OBJ) (-1 JaVoi ^ CLBC)
              (-2 SP + PlPar)(-2C Objekt) ;

# 6) Määrsonalised kvantorid objektina
# määrsona ei ole objekt, kui osalauses paremal ei leidu sonu partitiivis
# voi partitiiivis sona ei ole selle määrsona (kvantori) laiendiks
REMOVE Adv + (@OBJ) (NEGATE 1 Adv)(NEGATE 1 Par)(NEGATE 1 Eestäiend) ;
REMOVE Adv + (@OBJ) (1 Eestäiend)(NEGATE 1 Par)(NEGATE 1 Adv)(NEGATE 2 Par) ;
REMOVE Adv + (@OBJ) (1 Adv)(NEGATE 2 Par) ;

#7)Asesona ise
REMOVE P + (@OBJ) (NEGATE 0 NGP) ;

#8) Sona on sulgudes
REMOVE (@OBJ) (*-1 OPar BARRIER CPar OR Oeldis LINK NOT -1 Objekt)
              (*1 CPar BARRIER OPar OR Oeldis) ;

#IV PREDIKATIIVIGRAMMATIKA


#1) Osalauses on leitud öeldise osa, mis ei ole predikatiivi lubav
REMOVE (@PRD) (*-1C IFV ^ Prverb BARRIER CLBC OR Kvm) ;
REMOVE (@PRD) (*1C IFV ^ Prverb BARRIER CLBC)(NEGATE *1 Dsh BARRIER CLBC)
              (NEGATE 1 Koma) ;
# Viga: See ei olnud siiski _ketserlus_, mis oli kirjutatud tema näkku, vaid
# hoopis intelligents
REMOVE (@PRD) (NEGATE 0 Nom)(*-1C IFV ^ Olema BARRIER CLBC) ;
REMOVE (@PRD) (NEGATE 0 Nom)(*1C IFV ^ Olema BARRIER CLBC) ;
REMOVE (@PRD) (*-1C (@FMV) ^ Prverb BARRIER Kvm OR CLBC) 
              (NEGATE *-1 Prverb BARRIER CLBC)(NEGATE *1 Prverb BARRIER CLBC) ;
REMOVE (@PRD) (*1C (@FMV)^ Prverb BARRIER CLBC)
              (NEGATE *-1 Prverb BARRIER CLBC)(NEGATE *1 Prverb BARRIER CLBC) ;

REMOVE (@PRD) (*-1 Koma BARRIER CLB LINK 1 Ger ^ Prverb)
              (NEGATE *1 Prverb BARRIER CLB) ;
# olevat Antti seejuures üsna jämedalt väljendunud, kasutades koige 
# ürgsemat _sorti_ soome-ugri sonavara

# 2) Kui lauses leidub sonadekombinatsioon on vaja, ei ole predikatiivi 
REMOVE (@PRD) (*-1 Vaja BARRIER CLBC) ;
REMOVE (@PRD) (*1 Vaja BARRIER CLBC) ;

SELECT Nom + (@PRD) (1 Olema + Inf + Main)(*-1 Subjekt BARRIER CLB) ;
SELECT A + Nom + (@PRD) (1 Olema + Inf + Main)(1 Subjekt) ;

#3) Sonade järjestus ei luba predikatiivi

REMOVE Nom + (@PRD) (1 S + Nom)(NEGATE 1 JärelKv) ;
REMOVE (@PRD) (0 Par)(1 S + Par)(NEGATE 1 JärelKv) ;
REMOVE (@PRD) (0 ParticPast)(1 InfSupiin) ;
REMOVE (@PRD) (0 ParticPast)(-1 Supiin) ;


#4) Adjektiine öeldistäide soltub nominatiivsete Santiivide olemasolust
SELECT A + Nom + (@PRD) (*-1 Olema BARRIER S + Nom)(NEGATE *1 S + Nom) ;
REMOVE (@PRD) (0 Adj)(*1 SP + SgNom BARRIER Kui OR Koma OR CLBC)
              (*-1 SP + SgNom BARRIER CLBC) ;
REMOVE (@PRD) (0 Adj)(*1 SP + PlNom BARRIER Kui OR Koma OR CLBC)
              (*-1 SP + PlNom BARRIER CLBC) ;
SELECT A + Nom + (@PRD) (*-1 Olema BARRIER S + Nom OR CLB)
              (NEGATE *1 S + Nom OR (@SUBJ) + Inf BARRIER CLB) ;

#5) Substantiivse öeldistäite korral peab leiduma ka alus
#  voi peab verb olema esimeses 2 pöördes

REMOVE SPNom + (@PRD) (NEGATE *-1 SPNom)
              (NEGATE *1 SPNom)(NEGATE *-1 Vps12) ;
REMOVE SP + (@PRD) (NEGATE *-1 SPNom OR SP + Par BARRIER CLB)
              (NEGATE *1 SPNom OR SP + Par BARRIER CLB)
              (NEGATE *-1 Vps12 BARRIER CLB) ;
REMOVE SP + SgNom + (@PRD) (1 Side)(2 SP + SgNom)
              (NEGATE *3 SPNom BARRIER CLB)
              (NEGATE *-1 SPNom OR Vps12 BARRIER CLB) ;
REMOVE SP + SgNom + (@PRD) (-1 Side)(-2 SP +  SgNom)
              (NEGATE *-3 SPNom OR Vps12 BARRIER CLB)
              (NEGATE *1 SPNom BARRIER CLB) ;
# Väljas oli ohtu voi öö.

# 6) Unikaalsus
# ei ole predikatiiv, kui osalauses juba leidub predikatiiv, mis ei ole
# antud sonaga koordinatsioonis "ja" voi "ning" abil
REMOVE (@PRD) (*1C (@PRD) BARRIER Sid|Kvm OR CLB)
              (NEGATE *1 Olema + Inf BARRIER CLB) ;
REMOVE (@PRD) (*-1C (@PRD) BARRIER Sid|Kvm OR CLB) ;
# Vead: Must huumor see ainus õige ongi
# Praegune on juba lõpp ise
REMOVE (@PRD) (*1C (@ORD) BARRIER CLB LINK *-1 Olema BARRIER CLB)
              (NEGATE *-1 Olema BARRIER CLB) ;

#7) Osastavas nimisonaline öeldistäide
REMOVE SP + Par + (@PRD) (NEGATE -1 Adj OR Pdem)(NEGATE 0 ("miski") + Par OR Pdem OR Pindef) ;
# TP: oli: ilma  OR Pdem OR Pindef
REMOVE Par + (@PRD) (*-1 SgNom BARRIER CLBC)(*1 SgNom  BARRIER CLBC) ;
# see oli teatavat _liiki_ hüpnoos
REMOVE SgPar + (@PRD) (*-1 Olema BARRIER CLB 
                                LINK *1 SgNom BARRIER CLB LINK 1 Inf) ;
# nii kange oli mu tahtmine alustada iseseisvalt vanemate _otsinguid_
REMOVE Par + (@PRD) (*-1 Olema BARRIER CLB LINK *1 Adj + Nom BARRIER CLB) ;
REMOVE PlPar + (@PRD) (NEGATE *-1 Pl + (@SUBJ)) ; # TP: oli: ilma (NEGATE *-1 Pl + (@SUBJ))



REMOVE (@PRD) (NEGATE 0 Inf)(*1 Inf ^ Olema BARRIER Kvm OR CLB)
              (NEGATE *1 (@SUBJ) + Inf BARRIER CLB) ;

#8) Koordinatsioon
SELECT SgNom + (@PRD) (*-1 JaVoi BARRIER CLB LINK -1C (@PRD) + SgNom) ;
SELECT PlNom + (@PRD) (*-1 JaVoi BARRIER CLB LINK -1C (@PRD) + PlNom) ;
SELECT Par + (@PRD) (*-1 JaVoi BARRIER CLB LINK -1C (@PRD) + Par) ;

REMOVE SPNom + (@PRD)  
              (*-1 JaVoiK BARRIER FinV OR CLB LINK -1 SPNom LINK NOT 0 (@PRD) ) ;
REMOVE SP + Par + (@PRD) 
              (*-1 JaVoiK BARRIER FinV OR CLB LINK -1 SP + Par LINK NOT 0 (@PRD)) ;
REMOVE A + SgNom + (@PRD) 
              (*-1 JaVoiK BARRIER FinV OR CLB LINK -1 A + SgNom LINK NOT 0 (@PRD)) ; 
REMOVE SgPar + (@PRD) (1 Side)(2 SgPar)(NEGATE 2 (@PRD))(3 Kvm) ;
# voib-olla ei olnud kassi vaates mingit _kutset_ ega märguannet.

#9) Erandjuhud
# adjektiiv partitiivis ei ole predikatiiv, kui eelmine on "midagi"
REMOVE (@PRD) (0 Adj)(0 Par)(-1 ("miski") + Par) ;
# infinitiiv pole öeldistäide, kui osalauses ei leidu alust.
REMOVE Inf + (@PRD) (NEGATE *-1 Subjekt BARRIER CLB)(NEGATE *1 Subjekt BARRIER CLB) ;
SELECT ("väärt" @PRD) (NEGATE *1 SP BARRIER CLBC)(*-1 Olema BARRIER CLBC) ;
# ta on seda väärt, et ... 
REMOVE A + Nom + (@PRD) (1 A + Nom)(NEGATE 1 (@PRD)) ;
# Oli _külm_ selge aprillipäev

# 10) Lauses leidub sihitis
# mineviku partitsiip ei ole predikatiiv, kui osalauses vasakul leidub
# kindel objekt, sellest paremal on "olema" ja nende vahel pole verbe

REMOVE NudTud + (@PRD) (*-1C Objekt BARRIER CLB LINK *1 Olema BARRIER Verb OR CLB) ;
# midagi sobivat oleksin endale ikka _leidnud_
REMOVE (@PRD) (*-1C Objekt LINK 1 Partic)(NEGATE 0 Partic)
              (NEGATE *-1 InfSupiin BARRIER CLBC)(NEGATE *1 InfSupiin BARRIER CLBC) ;
# Viga: see ei olnud siiski ketserlus, mis oli kirjutatud tema näkku,
# vaid lihtsalt intelligents
REMOVE (@PRD) (*1C Objekt LINK 1 Partic)(NEGATE 0 Inf)
              (NEGATE *-1 InfSupiin BARRIER CLBC)(NEGATE *1 InfSupiin BARRIER CLBC) ;

# VI ATRIBUUDIGRAMMATIKA

# 1. Adjektiivid

# 1.1. Eestäiend

# 1) Adjektiivne atribuut ühildub pohjaga käändes ja Nus
SELECT SgNom + (@AN>) (1 S + SgNom)  ;
SELECT SgNom + (@AN>) (NEGATE 0 (@PRD))
              (*1 S + SgNom BARRIER NPpiir OR CLBC) ;
# ta oli _lühike_, _kleenuke_ ja sinised tunked, partei vormiriietus, veel
# rohutasid ...
SELECT SgGen + (@AN>) (*1 SP + SgGen BARRIER NPpiir OR CLB) ;
SELECT SgPar + (@AN>) (*1 S + SgPar BARRIER NPpiir OR CLB) ;
SELECT Sg + IllAdt + (@AN>) (*1 IllAdt BARRIER NPpiir OR CLB) ;
SELECT SgIn + (@AN>) (*1 SgIn BARRIER NPpiir OR CLB) ;
SELECT SgEl + (@AN>) (*1 SgEl BARRIER NPpiir OR CLB) ;
SELECT SgAd + (@AN>) (*1 SgAd BARRIER NPpiir OR CLB) ;
SELECT SgAll + (@AN>) (*1 SgAll BARRIER NPpiir OR CLB) ;
SELECT SgAbl + (@AN>) (*1 S + SgAbl BARRIER NPpiir OR CLB) ;
SELECT SgTr + (@AN>) (*1 SgTr + S BARRIER NPpiir OR CLB) ;
SELECT SgGen + (@AN>) (*1 S + Sg + NiNaTaGa BARRIER NPpiir OR CLB) ;

SELECT PlNom + (@AN>) (1 PlNom)(NEGATE 1 Ppers OR Pdem OR Pindef) ;
SELECT PlNom + (@AN>) (*1 PlNom BARRIER NPpiir OR CLB)(NEGATE 0 (@PRD)) ;
SELECT PlGen + (@AN>) (*1 PlGen BARRIER NPpiir OR CLB) ;
SELECT PlPar + (@AN>) (*1 PlPar BARRIER NPpiir OR CLB) ;
SELECT Pl + IllAdt + (@AN>) (*1 Pl + IllAdt BARRIER NPpiir OR CLB) ;
SELECT PlIn + (@AN>) (*1 PlIn BARRIER NPpiir OR CLB) ;
SELECT PlEl + (@AN>) (*1 PlEl BARRIER NPpiir OR CLB) ;
SELECT PlAll + (@AN>) (*1 PlAll BARRIER NPpiir OR CLB) ;
SELECT PlAd + (@AN>) (*1 PlAd BARRIER NPpiir OR CLB) ;
SELECT PlAbl + (@AN>) (*1 PlAbl BARRIER NPpiir OR CLB) ;
SELECT PlTr + (@AN>) (*1 PlTr + S BARRIER NPpiir OR CLB) ;
SELECT PlGen + (@AN>) (*1 S + Pl + NiNaTaGa BARRIER NPpiir OR CLB) ;

SELECT G + (@AN>) (*1 S BARRIER CLB) ;
SELECT (@AN>) (NEGATE 0 Sg OR Pl OR (@PRD))(*1 S BARRIER NPpiir OR CLB) ;
SELECT (@AN>) (NEGATE 0 Täis)(NEGATE 0 Sg)(NEGATE 0 Pl)(1 S) ;
SELECT (@AN>) (NEGATE 0 Täis)(NEGATE 0 Sg)(NEGATE 0 Pl)(1 N) ;

REMOVE (@AN>) (0 Täis)(*1C JärelKv BARRIER CLB) ;
REMOVE (@AN>) (NEGATE 0 Sg)(NEGATE 0 Pl)(1 Pron)(NEGATE 1 Eestäiend) ;
SELECT Poolik + (@AN>)  ; #pärimis- ja muu õigus

SET FV = Oeldis ;

# 2) Lähiümbruses ei leidu nimisona, mida laiendada
# Adjektiivne eestäiend
REMOVE Nom + (@AN>) (NEGATE *1 S + Nom BARRIER CLB) ;
# Viga: mis lähenes, _tohutu_ ja _hirmuäratav_, automaadi tärisedes, ...
REMOVE Par + (@AN>) (NEGATE *1 S + Par BARRIER CLB) ;
# Viga: Kuid ülempreester ei tahtnud loomulikult sellist _rahakat_ - ning
# mis veelgi tähtam!- väga mõijuvõimast külalist minema lasta
REMOVE IllAdt + (@AN>) (NEGATE *1 S + IllAdt BARRIER CLB) ;
REMOVE In + (@AN>) (NEGATE *1 S + In BARRIER CLB) ;
REMOVE El + (@AN>) (NEGATE *1 S + El BARRIER CLB) ;
REMOVE All + (@AN>) (NEGATE *1 S + All BARRIER CLB) ;
REMOVE Ad + (@AN>) (NEGATE *1 S + Ad BARRIER CLB) ;
REMOVE Abl + (@AN>) (NEGATE *1 S + Abl BARRIER CLB) ;
REMOVE Tr + (@AN>) (NEGATE *1 S + Tr BARRIER CLB)(NEGATE -1 ("vähem") OR ("enam") OR ("rohkem")) ; #TP +-1
REMOVE Nom + (@AN>) (*1C FV BARRIER S + Nom OR CLB)(NEGATE 1 Nom)  ;
REMOVE Par + (@AN>) (*1C FV BARRIER S + Par OR CLB) ;
REMOVE IllAdt + (@AN>) (*1C FV BARRIER S + IllAdt OR CLB) ;
REMOVE In + (@AN>) (*1C FV BARRIER  S + In OR CLB) ;
REMOVE El + (@AN>) (*1C FV BARRIER S + El OR CLB) ;
REMOVE All + (@AN>) (*1C FV BARRIER S + All OR CLB) ;
REMOVE Ad + (@AN>) (*1C FV BARRIER S + Ad OR CLB) ;
REMOVE Abl + (@AN>) (*1C FV BARRIER S + Abl OR CLB) ;
REMOVE Tr + (@AN>) (*1C FV BARRIER S + Tr OR CLB) ;

REMOVE (@AN>) (NEGATE 0 Nom)(NEGATE *1 S BARRIER CLB) ;
#Viga: Näiteks selle _paksu_ ja _blondi_, keda sa ...

REMOVE (@AN>) (1 Võrdlus)(*1 S + Nom BARRIER CLB LINK NOT *1 S BARRIER CLB) ;
# tumm kui lõpnud lest

REMOVE (@AN>) (NEGATE 1 Adj)(NEGATE *1 S BARRIER CLB) ;
REMOVE (@AN>) (NEGATE 0 Nom)(*1 FV BARRIER S OR CLB) ;
REMOVE (@AN>) (NEGATE 1 Adj)(*1 FV BARRIER S OR CLB) ;

# 3)Koordinatsioonis lauseliikmed
SELECT SgNom + (@AN>) (1 JaVoiK)(2 Sg + Nom)(2C (@AN>)) ;
SELECT PlNom + (@AN>) (1 JaVoiK)(2 Pl + Nom)(2C (@AN>)) ;
SELECT SgGen + (@AN>) (1 JaVoiK)(2 Sg + Gen)(2C (@AN>)) ;
SELECT PlGen + (@AN>) (1 JaVoiK)(2 Pl + Gen)(2C (@AN>)) ;
SELECT SgPar + (@AN>) (1 JaVoiK)(2 Sg + Par)(2C (@AN>)) ;
SELECT PlPar + (@AN>) (1 JaVoiK)(2 Pl + Par)(2C (@AN>)) ;
SELECT SgNom + (@AN>) (1 JaVoiK)(2 Adv)(3 Sg + Nom)(3C (@AN>)) ;
SELECT PlNom + (@AN>) (1 JaVoiK)(2 Adv)(3 Pl + Nom)(3C (@AN>)) ;
SELECT SgGen + (@AN>) (1 JaVoiK)(2 Adv)(3 Sg + Gen)(3C (@AN>)) ;
SELECT PlGen + (@AN>) (1 JaVoiK)(2 Adv)(3 Pl + Gen)(3C (@AN>)) ;
SELECT SgPar + (@AN>) (1 JaVoiK)(2 Adv)(3 Sg + Par)(3C (@AN>)) ;
SELECT PlPar + (@AN>) (1 JaVoiK)(2 Adv)(3 Pl + Par)(3C (@AN>)) ;
SELECT IllAdt + (@AN>) (1 JaVoiK)(2 IllAdt)(2C (@AN>)) ;
SELECT In + (@AN>) (1 JaVoiK)(2 In)(2C (@AN>)) ;
SELECT El + (@AN>) (1 JaVoiK)(2 El)(2C (@AN>)) ;
SELECT All + (@AN>) (1 JaVoiK)(2 All)(2C (@AN>)) ;
SELECT Ad + (@AN>) (1 JaVoiK)(2 Ad)(2C (@AN>)) ;
SELECT Abl + (@AN>) (1 JaVoiK)(2 Abl)(2C (@AN>)) ;
SELECT Tr + (@AN>) (1 JaVoiK)(2 Tr)(2C (@AN>)) ;

# Käändumatud omadussõnad ja partitsiibid
SELECT Sg + (@AN>) (1 JaVoi)(NEGATE 2 Pl)(2C (@AN>)) ;
SELECT Pl + (@AN>) (1 JaVoi)(NEGATE 2 Sg)(2C (@AN>)) ;
SELECT (@AN>) (NEGATE 0 Pl)(NEGATE 0 Sg)(1 JaVoi)(NEGATE 2 Sg)(2C (@AN>)) ;
# paaris või paarituil kuupäevil

SELECT SgNom + (@AN>) (1 Koma)(2 ("kuid"))(3 Sg + Nom)(3C (@AN>)) ;
# tüse, kuid toimekas ja rumal mees

# Järgneb koma või sidesõna, kuid ei ole adjektiivset täiendit
REMOVE (@AN>) (1 Koma + CLBC)(NEGATE 2 (@AN>) OR Adv OR Side)
              (NEGATE *3 Partic BARRIER CLB) ;
# mul on sellega kiire, neiuke
REMOVE (@AN>) (1 JaVoi)(NEGATE 2 (@AN>) OR Adv)(1 CLBC)
                   (NEGATE *3 Partic BARRIER CLB) ;
REMOVE Nom + (@AN>) (1 Koma)(1 CLBC)
                  (*2 S + Nom BARRIER (@AN>) OR CLB) ;
#1.2. Järeltäiend

#1) Konkreetsed fraasid 
#Miski
SELECT SgGen + (@<AN) (*-1 ("miski") + Gen BARRIER Sid|Kvm OR CLB OR FV) ;
SELECT SgPar + (@<AN) (*-1 ("miski") + Par BARRIER Kvm OR FV OR CLB) ;
SELECT ("muu" @<NN) + SgPar (-1 ("miski")) ;
# midagi muud sellist ülearust

# 2) Lähiümbruses ei leidu nimisona, mida laiendada

REMOVE Nom + (@<AN) (NEGATE *-1 S + Nom BARRIER CLB) ;
REMOVE Gen + (@<AN) (NEGATE *-1 S + Gen BARRIER CLB) ;
# Viga: üle ühe mu elu võtmestseeni, mis sellest, et _tumma_ ja näiliselt
# _kõrvalise_
REMOVE Par + (@<AN) (NEGATE *-1 S + Par BARRIER CLB) ;
REMOVE IllAdt + (@<AN) (NEGATE *-1 S + IllAdt BARRIER CLB) ;
REMOVE In + (@<AN) (NEGATE *-1 S + In BARRIER CLB) ;
REMOVE El + (@<AN) (NEGATE *-1 S + El BARRIER CLB) ;
# Vead: erinevus kõigist teistest selles piirkonnas _kõneldavaist_.
# ohverdamisviisidest, vanadest, mil ..., ja _uutest_, mil ...
REMOVE All + (@<AN) (NEGATE *-1 S + All BARRIER CLB) ;
REMOVE Ad + (@<AN) (NEGATE *-1 S + Ad BARRIER CLB) ;
REMOVE Abl + (@<AN) (NEGATE *-1 S + Abl BARRIER CLB) ;
REMOVE Tr + (@<AN) (NEGATE *-1 S + Tr BARRIER CLB) ;
REMOVE Nom + (@<AN) (*-1 FV BARRIER S + Nom OR CLB) ;
REMOVE Gen + (@<AN) (*-1 FV BARRIER S + Gen OR CLB) ;
REMOVE Par + (@<AN) (*-1 FV BARRIER S + Par OR CLB) ;
REMOVE IllAdt + (@<AN) (*-1 FV BARRIER S + IllAdt OR CLB) ;
REMOVE In + (@<AN) (*-1 FV BARRIER S + In OR CLB) ;
REMOVE El + (@<AN) (*-1 FV BARRIER S + El OR CLB) ;
REMOVE All + (@<AN) (*-1 FV BARRIER S + All OR CLB) ;
REMOVE Ad + (@<AN) (*-1 FV BARRIER S + Ad OR CLB) ;
REMOVE Abl + (@<AN) (*-1 FV BARRIER S + Abl OR CLB) ;
REMOVE Tr + (@<AN) (*-1 FV BARRIER S + Tr OR CLB) ;
REMOVE (@<AN) (0 NiNaTaGa) ;
REMOVE (@<AN) (*-1 NPpiir BARRIER S) ;
REMOVE (@<AN) (*-1 FV BARRIER S) ;
REMOVE (@<AN) (*-1 Pdet BARRIER S OR CLB) ;
REMOVE (@<AN) (*-1 Pdem BARRIER S OR CLB) ;
REMOVE (@<AN) (*-1 Ppos BARRIER S OR CLB) ;

#3) Adjektiiv ei ole järeltäiend, kui pole komaga põhjast eraldatud
# Erand: Miks sa soola nii jämeda ostsid
# Lahus olev järeltäiend läheb nagunii vigaseks
REMOVE (@<AN) (NEGATE 0 Gen)(*-1 S BARRIER Koma OR Koolon OR CLB) ;
# Viga: amputeeritud jalg, nagu ajakirjanikulgi parem.


# 2. Asesõnad
# 2.1. Eestäiend

SELECT ("ise" @NN>) (0 Nom)(1 Ppers) ;
REMOVE ("ise" @NN>) (NEGATE 0 Gen)(NEGATE *1 FinV OR Ppers BARRIER CLB) ;

# 2.2. Järeltäiend
# Asesõnad (enamus on üleval pool, et ei segaks aluse-sihitise reegleid)
SELECT ("ise" @<NN) + Nom (-1 Nom) ;
SELECT ("ise" @<NN) + Gen (-1 Gen) ;
SELECT ("ise" @<NN) + Par (-1 Par)(-1C Objekt) ;
SELECT ("ise" @<NN) + IllAdt (-1 IllAdt) ;
SELECT ("ise" @<NN) + In (-1 In) ;
SELECT ("ise" @<NN) + El (-1 El) ;
SELECT ("ise" @<NN) + Ad (-1 Ad) ;
SELECT ("ise" @<NN) + All (-1 All) ;
SELECT ("ise" @<NN) + Abl (-1 Abl) ;
SELECT ("ise" @<NN) + Tr (-1 Tr) ;
SELECT ("ise" @<NN) (0 NiNaTaGa)(-1 Gen) ;
SELECT ("ise" @<NN) + Nom (*-1 Ppers BARRIER CLBC) ;

REMOVE ("ise" @<NN) + Par (NEGATE -1 Par) ;
SELECT ("ise" @<NN) + Nom (*-1 Vps1sg BARRIER CLB) ;
SELECT ("ise" @<NN) + Nom (*-1 Vps2sg BARRIER CLB) ;
SELECT ("ise" @<NN) + Nom (*-1 Vps1pl BARRIER CLB) ;
SELECT ("ise" @<NN) + Nom (*-1 Vps2pl BARRIER CLB) ;
REMOVE ("ise" @<NN) + El (NEGATE *-1 El BARRIER CLB) ;
REMOVE ("ise" @<NN) + All (NEGATE *-1 All BARRIER CLB) ;
REMOVE ("ise" @<NN) + Ad (NEGATE *-1 Ad BARRIER CLB)   ;
REMOVE ("ise" @<NN) (0 NiNaTaGa)(NEGATE *-1 Gen BARRIER CLB)  ;
SELECT Pdem + (@<NN) + SgPar (*-1 ("miski") BARRIER Kvm OR FV OR CLB) ;

# Konkreetsed asesonad järeltäiendina
# Koik
SELECT ("kõik" @<NN) (-1 Ppers) ;
SELECT ("kõik" @<NN) + SgNom (-1 Pdem + SgNom) ;
SELECT ("kõik" @<NN) + SgPar (-1 Pdem + SgPar) ;
SELECT ("kõik" @<NN) + SgEl (-1 Pdem + SgEl) ;
SELECT ("kõik" @<NN) (-1 Pdem) ;

# 3. Nimisõnad, Arvsõnad ja ülejäänud asesõnad

# 3.1. Eestäiend
# 1) Konkreetsed fraasid
# Nimed:

REMOVE (@NN>) + Scom + Nom (NEGATE 0 Nimi OR Cap)(NEGATE 1 Cap) ; # TP 

SELECT (@NN>) (0 Nimi + Nom)(1 Nimi) ;
# Vead: Munak Nobeli preemiat vastu võtmas ;
# tormab noor Aleksander Pärsia kuningas Dareiose poole
# mängis Lennart Arnoldile ninanipsu sellega  
SELECT (@NN>) (0 Nimi)(1 Poeg) ;
SELECT (@NN>) (0 Poeg)(-1 Nimi)(1 Nimi) ;
SELECT (@NN>) (0 Y + Cap)(1 Nimi) ;
# U. Munak

# Poolikud liitsõnad (kuld- ja hõberiistad)
SELECT (@NN>) (0 Poolik)(2 S) ;

# Nud
SELECT N + (@NN>) + SgNom (1 N + SgNom) ;
# Viga: kell on peaaegu üksteist null-null
SELECT N + (@NN>) + SgPar (1 N + SgPar) ;
REMOVE Ncard + (@NN>) (1 (@<Q))(2 LLZ OR CLB) ; # TP
REMOVE Ncard + (@NN>) (1 (@<Q))(2 (@<Q))(3 LLZ OR CLB) ; # TP
SELECT N + (@NN>) + SgGen (1 N) ;


# Nsona mitmuses on täiend, kui ühildub pohisonaga
SELECT PlNom + (@NN>) (0 ArvKv)
              (*1 SP + PlNom BARRIER NPpiir OR CLB) ;
SELECT PlGen + (@NN>) (0 ArvKv)(*1 SP + PlGen BARRIER NPpiir OR CLB) ;
SELECT PlPar + (@NN>) (0 ArvKv)(*1 SP + PlPar BARRIER NPpiir OR CLB) ;
SELECT PlIllAdt + (@NN>) OR Adit + (@NN>) (0 ArvKv)
              (*1 SP + IllAdt BARRIER NPpiir OR CLB) ;
SELECT PlAll + (@NN>) (0 ArvKv)(*1 SP + PlAll BARRIER NPpiir OR CLB) ;
SELECT PlAbl + (@NN>) (0 ArvKv)(*1 SP + PlAbl BARRIER NPpiir OR CLB) ;
SELECT PlIn + (@NN>) (0 ArvKv)(*1 SP + PlIn BARRIER NPpiir OR CLB) ;
SELECT PlEl + (@NN>) (0 ArvKv)(*1 SP + PlEl BARRIER NPpiir OR CLB) ;
SELECT PlAd + (@NN>) (0 ArvKv)(*1 SP + PlAd BARRIER NPpiir OR CLB) ;
SELECT PlTr + (@NN>) (0 ArvKv)(*1 SP + PlTr BARRIER NPpiir OR CLB) ;
SELECT PlGen + (@NN>) (0 ArvKv)
              (*1 SP + NiNaTaGa + Pl BARRIER NPpiir OR CLB) ;
SELECT ("üks" @NN>) + N (*1 S BARRIER CLB)
              (*1 NPpiir BARRIER El OR CLB)(NEGATE -1 N) ;
SELECT ("üks" @NN>) + N (*1 S BARRIER CLB)
              (NEGATE *1 El BARRIER CLB)(NEGATE -1 N) ;

SELECT ("kell" @NN>) (1 N) ;
REMOVE ("kes" @NN>) (NEGATE 0 Gen)(-1 Koma) ;

# 2) Pole, mida täiendada


REMOVE (@NN>) OR (@DN>) (NEGATE 0 Pron)(NEGATE 1 Sid|Kvm)
			(NEGATE *1 SP OR N BARRIER NPpiir OR FV OR CLBC) ; # TP (NEGATE 1 Sid|Kvm) 9676
REMOVE (@NN>) OR (@DN>) (0 Ppers)
			(NEGATE *1 SP OR N BARRIER NPpiir OR FV OR CLBC) ;
#REMOVE (@NN>) (NEGATE *1 HeadPPQ)(*-1C NPpiir *R+1)(NEGATE *R+1 SPN) **CLBC)
# Viga: palun see suurem sealt

#REMOVE (@NN>) (NEGATE *1 HeadPPQN<) **CLB)
# Viga: Näiteks selle paksu ja blondi, keda ...
# Nt Mässisin _juuksetutti_ ümber s_rme
REMOVE (@NN>) (*1C Kpre BARRIER SP OR N 
                   LINK *1 (@<P) LINK NOT *1 SP OR N BARRIER CLB) ;
REMOVE (@NN>) (*1C Ger BARRIER SP OR N OR CLB) ;

#Võrdlus

REMOVE (@NN>) (NEGATE 0 Pron)(*1 Võrdlus BARRIER S OR CLB LINK NOT -1 Võrre)
              (NEGATE *-1 Nii BARRIER CLB) ;
REMOVE P + (@NN>) (*1 Võrdlus BARRIER S OR Pron OR CLB 
                              LINK NOT -1 Võrre)
              (NEGATE *-1 Nii BARRIER CLB) ;

#Kvantorifraasis
REMOVE (@NN>) (1C JärelKv)(2C FV)(NEGATE *-1 S BARRIER CLB) ;
REMOVE (@NN>) (1C JärelKv)(NEGATE *2 SP BARRIER CLB)
              (NEGATE *-1 KvAdv OR NimKv OR ArvKv  BARRIER CLBC) ;

# 3) Partitsiipide ees olevad sõnad
REMOVE (@NN>) (0 Sise)(1 Partic)(NEGATE 2 Sise) ;
REMOVE All + (@NN>) (1 Partic)(NEGATE *1 All BARRIER CLB) ;
REMOVE El + (@NN>) (1 Partic)(NEGATE *1 El BARRIER CLB) ;

# 4) Käänded


# Osastav kääne ei ole täiend, v.a. kasvu, tõugu, liiki jne
REMOVE Par + (@NN>) (NEGATE 0 Parameeter)(NEGATE 1 Par) ;

# Seesütlev kääne ja kohakaassona ei ole seotud
# Ma seisin esikus peegli ees
REMOVE In + (@NN>) (1C (@<P))(2 SiseKaas)
	      (NEGATE *3 SP BARRIER NPpiir OR CLB) ;
REMOVE (@NN>) (0 Sise)(1 NudTud)(NEGATE 2 Sise) ; # oli: (1 (A partic past))

REMOVE (@NN>) (0 Sise)(1 Adj)(NEGATE 0 Pron) ;
# Viga: kirjudes rättides prisked naised
# tohutu kiiskavvalgest betoonist püramiidne ehitis
REMOVE (@NN>) (0 Sise)(1 Pdem)(NEGATE 0 Pron) ;

# Allatiiv
REMOVE S + (@NN>) + All (NEGATE 1 Deverbaal) ;
REMOVE P + (@NN>) + All (NEGATE *1 All BARRIER CLB)(NEGATE 1 ("lisa") + S) ;

# Adessiiv
REMOVE S + (@NN>) + Ad  ;
REMOVE P + (@NN>) + Ad (NEGATE *1 Ad BARRIER CLB) ;

# Ablatiiv
REMOVE S + (@NN>) + Abl  ;
REMOVE P + (@NN>) + Abl (NEGATE *1 Abl BARRIER CLB) ;

# Inessiiv
REMOVE P + (@NN>) + In (NEGATE *1 In BARRIER CLB) ;

# Translatiiv
# Nimisona translatiivis ei saa saa olla eestäiendiks
REMOVE S + (@NN>) + Tr (NEGATE 1 Deverbaal) ;
REMOVE P + (@NN>) + Tr (NEGATE *1 Tr BARRIER CLB) ;

#Essiiv
REMOVE SP + Es + (@NN>)  ;

# 5) Koordinatsioon
# üks koordinatsioonis olijaist ei ole eestäiend

REMOVE SgNom + (@NN>) (1 JaVoiK)(*2 SP + SgNom BARRIER CLB LINK NOT 0 (@NN>)) ;
#mõni täies varustuses rändrüütel või suurte saabastega hiigelahv
REMOVE PlNom + (@NN>) (1 JaVoiK)(*2 SP + PlNom BARRIER CLB LINK NOT 0 (@NN>)) ;
REMOVE Gen + (@NN>) (1 JaVoiK)(*2 SP + Gen BARRIER CLB LINK NOT 0 (@NN>)) ;
# REMOVE PlGen + (@NN>) (1 JaVoiK)(*2 SP + PlGen BARRIER CLB LINK NOT 0 (@NN>)) ; TP: võib olla sg ja pl
REMOVE Par + (@NN>) (1 JaVoiK)(*2 SP + Par BARRIER CLB LINK NOT 0 (@NN>)) ;

REMOVE El + (@NN>) (1 JaVoiK)(*2 S + El BARRIER CLB LINK NOT 0 (@NN>)) ;
REMOVE In + (@NN>) (1 JaVoiK)(*2 S + In BARRIER CLB LINK NOT 0 (@NN>)) ;
REMOVE IllAdt + (@NN>) (1 JaVoiK)(*2 S + IllAdt BARRIER CLB LINK NOT 0 (@NN>)) ;
REMOVE Gen + (@NN>) (1 JaVoi)(*2 S + NiNaTaGa BARRIER CLB OR Gen + (@NN>) LINK NOT 0 (@NN>)) ; # TP OR Gen + (@NN>)

REMOVE (@NN>) (1 JaVoiK)(*2 FV BARRIER Eestäiend OR CLB) ;
# räägiti kirjandusklassikast, raadiost kuulati ...

#3.2 Järeltäiend või lisand

#1) Eespool pole, mida täiendada
REMOVE (@<NN)(NEGATE 0 Ise OR Koik)(NEGATE *-1 SPN BARRIER CLB) ;
# Viga tekkis loetelus, kus ja ees oli koma 
REMOVE (@<NN)(NEGATE 0 Ise OR Koik)(*-1 FV BARRIER SPN OR CLB) ;
# Viga: Mida ta teeb, range koolidirektor, kohalik vaimuhiiglane, ...
REMOVE (@<NN)(NEGATE 0 Ise OR Koik)(*-1 Adv BARRIER CLB OR S LINK -1 Adv) ;
REMOVE (@<NN)(NEGATE *-1 NotEestäiend BARRIER FV OR CLB)
             (NEGATE 0 Ise OR Koik)
             (NEGATE *1 S BARRIER CLB) ;
# Eespool on ainult kaassonafraas
# Nt lamas nüüd kabeturniiri asemel haiglas.
REMOVE (@<NN)(*-1 Kpost BARRIER SPN OR CLB LINK *-1C (@P>) BARRIER CLB 
                  LINK NOT *-1 SPN BARRIER FV OR CLB)(NEGATE 0 Ise) ;

#2) Kvantorifraas ja elatiivne täiend
SELECT El + (@<NN) (-1 ("osa"))(NEGATE -1 El) ;
SELECT El + (@<NN) (*-1 ("osa") BARRIER Verb OR CLB LINK NOT 0 El)
              (NEGATE -1 Määrus) ;
SELECT PlEl + (@<NN) (*-1 ArvKv BARRIER CLB LINK NOT 0 Pl  
                              LINK *1C JärelKv BARRIER FV OR Subjekt OR CLB) ;
SELECT PlEl + (@<NN) (*-1 ArvKv BARRIER FV OR Subjekt OR CLB LINK NOT 0 El) ;

#3) Koordinatsioon
# Komadega eraldatud
REMOVE SgNom + (@<NN) (-1 Koma)(-2 SgNom)(NEGATE -2 (@<NN))(NEGATE 1 Koma) ;
REMOVE PlNom + (@<NN) (-1 Koma)(-2 PlNom)(NEGATE -2 (@<NN))(NEGATE 1 Koma) ;
REMOVE SgGen + (@<NN) (-1 Koma)(-2 SgGen)(NEGATE -2 (@<NN))
       (NEGATE *1 S BARRIER CLB) ;
REMOVE PlGen + (@<NN) (-1 Koma)(-2 PlGen)(NEGATE -2 (@<NN))
       (NEGATE *1 S BARRIER CLB) ;
REMOVE SgGen + (@<NN) (-1 Koma)(-2 SgGen)(NEGATE -2 (@<NN))(1 JaVoi ^ CLB) ;
REMOVE PlGen + (@<NN) (-1 Koma)(-2 PlGen)(NEGATE -2 (@<NN))(1 JaVoi ^ CLB) ;

# ... jne


# Sidesonadega eraldatud
REMOVE SgNom + (@<NN) (*-1 JaVoi BARRIER Sid|Kvm OR CLB LINK -1 SP + SgNom LINK NOT 0 (@<NN)) ;
REMOVE PlNom + (@<NN) (*-1 JaVoi BARRIER CLB LINK -1 SP + PlNom LINK NOT 0 (@<NN)) ;
REMOVE SgGen + (@<NN) (*-1 JaVoi BARRIER CLB LINK -1 SP + SgGen LINK NOT 0 (@<NN)) ;
REMOVE PlGen + (@<NN) (*-1 JaVoi BARRIER CLB LINK -1 SP + PlGen LINK NOT 0 (@<NN)) ;
REMOVE S + Par + (@<NN) (*-1 JaVoi BARRIER CLB LINK -1 SP + Par LINK NOT 0 (@<NN)) ;

REMOVE IllAdt + (@<NN) (-1 JaVoi)(-2 IllAdt)(NEGATE -2 (@<NN)) ;
REMOVE In + (@<NN) (-1 JaVoi)(-2 In)(NEGATE -2 (@<NN)) ;
REMOVE El + (@<NN) (-1 JaVoi)(-2 El)(NEGATE -2 (@<NN)) ;
REMOVE All + (@<NN) (-1 JaVoi)(-2 All)(NEGATE -2 (@<NN)) ;
REMOVE Ad + (@<NN) (-1 JaVoi)(-2 Ad)(NEGATE -2 (@<NN)) ;
REMOVE Abl + (@<NN) (-1 JaVoi)(-2 Abl)(NEGATE -2 (@<NN)) ;
REMOVE Tr + (@<NN) (-1 JaVoi)(-2 Tr)(NEGATE -2 (@<NN)) ;


REMOVE Kom + (@<NN) (*-1 S + Kom BARRIER Verb OR CLB LINK NOT 0 (@<NN) 
                           LINK *1 JaVoi BARRIER CLB) ;
REMOVE Kom + (@<NN) (*-1 S + Gen + Määrus BARRIER S + Gen OR CLB
                           LINK 1 JaVoi ) ;
REMOVE Kom + (@<NN) (-1 JaVoi)(-2 Gen)(NEGATE -2 (@<NN)) ;

# nii ... kui ka ...
REMOVE (@<NN) (*-4 Nii BARRIER CLB)(NEGATE -3 (@<NN))(-2 Kui)(-1 Ka) ;

SELECT S + Nom + (@<NN) (1 JaVoiK)
              (*-1C S + Nom + (@<NN) BARRIER NotEestäiend OR CLB 
                    LINK 1 Koma) ;


# kui järgneb koma ja verb, on hoopis järellisand voi üte
SELECT SgNom + (@<NN) (-1 Koma)(-2 SgNom)(1 Koma)(2 Verb) ;
SELECT PlNom + (@<NN) (-1 Koma)(-2 PlNom)(1 Koma)(2 Verb) ;
SELECT SgGen + (@<NN) (-1 Koma)(-2 SgGen)(1 Koma)(2 Verb) ;
SELECT PlGen + (@<NN) (-1 Koma)(-2 PlGen)(1 Koma)(2 Verb) ;

#4) kui-järellisand

SELECT Nom + (@<NN) (*-1 Kui BARRIER NotEestäiend OR Võrre OR CLB 
                       LINK -1 Nom + SP)
                  (NEGATE *-1 Nii BARRIER CLB) ;
# väldib: oli püsivam vihaobjekt kui Euraasia
SELECT Gen + (@<NN) (*-1 Kui BARRIER NotEestäiend OR Võrre OR CLB LINK -1 Gen)
                  (NEGATE *-1 Nii BARRIER CLB) ;

SELECT Par + (@<NN) (*-1 Kui BARRIER NotEestäiend OR Võrre OR CLB LINK -1 Par)
                  (NEGATE *-1 Nii BARRIER CLB) ;

SELECT Adit + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 IllAdt)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT IllAdt + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 IllAdt)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT In + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 In)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT El + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 El)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT All + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 All)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT Ad + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 Ad)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT Abl + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 Abl)
                  (NEGATE *-1 Nii BARRIER CLB) ;
SELECT Tr + (@<NN) (*-1 Kui BARRIER FV OR Subjekt OR Võrre OR CLB LINK -1 Tr)
                  (NEGATE *-1 Nii BARRIER CLB) ;
# sinule kui keiser Julianuse eluloo kirjutajale

#5) Veidi karmimad reeglid vasaku konkteksti kohta

# Eespool on verb ja sellest tagapool ei ole midagi täiendada
REMOVE (@<NN) (*-1C FV BARRIER S OR Ppers OR Koolon OR CLB OR ("üks"))
              (NEGATE 0 Ise OR Koik) ;
REMOVE (@<NN) (*-1C FV BARRIER HeadPPQ OR CLB)(NEGATE 0 Ise) ;
REMOVE (@<NN) (-1 Partic)(-2C Määrus) 
              (*-3 FV BARRIER S OR CLB) ;
# kipun ülehindama mulle eraldatud _minutite_ kestust
REMOVE (@<NN) (*-1 SupInfGer BARRIER S OR CLB) ;
# kipun ülehindama mulle eraldatud _minutite_ kestust



REMOVE (@<NN) (NEGATE *-1 HeadPPQ BARRIER CLB)
              (NEGATE *-1 NimEeslaiend BARRIER CLB)(NEGATE 0 Ise) ;

# Isikulist asesona ei saa täiendada, kui just ühildumist ei ole

REMOVE Nom + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Nom) ;
REMOVE Nom + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Nom 
                       LINK NOT *-1 SP BARRIER CLB) ;
REMOVE Gen + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Gen) ;
REMOVE Gen + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Gen
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE Par + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Par) ;
REMOVE Par + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Par
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE Adit + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>)) ;
REMOVE Adit + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT *-1 SP BARRIER CLB) ;

REMOVE IllAdt + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>)) ;
REMOVE IllAdt + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT *-1 SP BARRIER CLB) ;

REMOVE In + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR In) ;
REMOVE In + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 In 
			LINK NOT *-1 SP BARRIER CLB) ;

REMOVE El + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR El) ;
REMOVE El + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 El
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE All + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR All) ;
REMOVE All + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 All
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE Ad + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Ad) ;
REMOVE Ad + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Ad
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE Abl + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Abl) ;
REMOVE Abl + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Abl
                       LINK NOT *-1 SP BARRIER CLB) ;
REMOVE Tr + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (@NN>) OR Tr) ;
REMOVE Tr + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 Tr
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE Term + (@<NN) (*-1 PersPron BARRIER NotEestäiend OR CLB LINK NOT 0 (term)
                       LINK NOT *-1 SP BARRIER CLB) ;

REMOVE (@<NN) (-1 ("kes") + Par OR ("mis") + Par)(-2 Koma) ;

# 6) Koolonitega loetelud
SELECT S + Nom + (@<NN) (*-1 Koolon BARRIER NotEestäiend LINK -1 S + Nom)
                    (1 JaVoiK) ;
SELECT S + Nom + (@<NN) (*-1 Koolon BARRIER NotEestäiend LINK -1C JärelKv)(1 JaVoiK) ;
SELECT S + (@<NN) (*-1 Koolon BARRIER NotEestäiend LINK -1 SP)(1 JaVoiK) ;

#6) Järellisand peab ühilduma pohjaga. 

# Nimetav

SELECT SgNom + (@<NN) (*-1 Sid|Kvm BARRIER NotEestäiend OR CLB
                          LINK -1C SgNom + (@<NN))(1 JaVoiK OR Kvm) ;
REMOVE Nom + (@<NN) (NEGATE 0 Ise OR Nimi)(*-1 FV BARRIER S + Nom OR OPar OR CLB) ;
# Viga: mütsatades vastu uuskeele sõna kaksisoism

REMOVE Nom + (@<NN) (NEGATE 0 Ise OR Nimi)(NEGATE *-1 OPar OR SPNom BARRIER CLB) ;
#Peab olema põhisõnast komadega vms eraldatud
#REMOVE Nom + (@<NN) (NEGATE 0 Nimi OR Ise OR Cap)(*-1 Verb *R+1)(NEGATE *R+1 Kvm) **CLB)
# Vead: näidates ainsat sõna ingsots
# millel oli kiri Võidu sigaretid
#Omastav
REMOVE Gen + (@<NN) (NEGATE *-1 S + Gen BARRIER FV OR CLB)
                  (NEGATE *1 S + Kom BARRIER FV OR CLB) ;
REMOVE Gen + (@<NN) (1 S) ;
REMOVE Gen + (@<NN) (NEGATE *-1 Kvm BARRIER CLB)(NEGATE *1 Kom BARRIER CLB) ;
REMOVE Gen + (@<NN) (NEGATE *-1 S + Gen BARRIER FV OR CLB)
                  (*1 S + Kom BARRIER FV OR CLB OR JaVoiK) ;

#Peab olema põhisõnast komadega vms eraldatud
REMOVE Gen + (@<NN) (*-1 Verb BARRIER Kvm OR CLB)(NEGATE *1 S + Kom BARRIER CLB) ;
# Osastav
REMOVE SgPar + (@<NN) (NEGATE *-1 S + Par BARRIER FV OR CLB) ;
REMOVE PlPar + (@<NN) (NEGATE *-1 S + Par BARRIER FV OR CLB)(NEGATE 1 Koma) ;
# Viga siis, kui loetelu vahel kõrvallause
# Erand: Epp, meie parimaid tantsijaid, ...
#Peab olema põhisõnast komadega vms eraldatud
REMOVE Par + (@<NN) (NEGATE *-1 Kvm BARRIER FV OR CLB) ;

# 7) Käänded
# Järellisand peab olema komade vahel
REMOVE Nom + (@<NN) (NEGATE 0 N OR NimKv)(-1 Koma)(NEGATE 1 JaVoiK)(NEGATE 1 Kvm) ;
# Viga: loetelu ..., _kataloogiäri_ AS Telemedia Eesti
REMOVE Tr + (@<NN) (NEGATE -1 S) ;
REMOVE Kom + (@<NN)(-1 All) ;
# riputas kaladele peoga soola

# 4. Määrsõnad
# 4.1. Eestäiend

REMOVE (@DN>) (NEGATE 1 S)
              (NEGATE 0 Täna OR KohamäärIlAl OR KohamäärInAd OR KohamäärElAb) ;


# siia, sinna jne on atribuut, kui järgneb sisse- voi alaleütlev
SELECT (@DN>) (0 KohamäärIlAl) (*1C S + SgAll BARRIER FV OR CLB OR SP) ;
SELECT (@DN>) (0 KohamäärIlAl) 
              (*1C S + IllAdt OR S + SgIllAdt BARRIER FV OR SP OR CLB) ;
# siin, seal jne on atribuut, kui järgneb sees- voi alalütlev
SELECT (@DN>) (0 KohamäärInAd) (*1C S + SgIn BARRIER FV OR SP OR CLB) ;
SELECT (@DN>) (0 KohamäärInAd) (*1C S + SgAd BARRIER FV OR SP OR CLB) ;
# järgnev peab olema sg sest ... et siin röövlitel teed täkestada
# siit, selt jne on atribuut, kui järgneb seest- voi alaltütlev
SELECT (@DN>) (0 KohamäärElAb) (*1C S + SgEl BARRIER FV OR S OR CLB) ;
SELECT (@DN>) (0 KohamäärElAb) (*1C S + SgAbl BARRIER FV OR S OR CLB) ;

#4.2. Järeltäiend 

# kes tahes, mis tahes
SELECT ("tahes" @<DN) (-1 Pron) ;
REMOVE (@<DN) (NEGATE -1 SP) ;
REMOVE (@<DN)(0 YlesAlla)(-1 El) ;
REMOVE (@<DN)(0 Kaasa)(-1 Kom) ;
REMOVE (@<DN)(-1 Pron)(NEGATE -1 Koik)(NEGATE 0 ("tahes")) ;

#5. Kaassõna
#5.1. Eestäiend
# kaassonad pole eestäiendid kui tagapool ei leidu nimisonu:
REMOVE Kpre + (@KN>) (*1C (@<P) BARRIER CLB OR FV 
                       LINK NOT *1 S BARRIER CLB OR FV) ;
REMOVE Kpost + (@KN>) (NEGATE *1 S BARRIER CLB OR FV) ;
REMOVE (@KN>) (*1C (@<P) BARRIER S OR CLB LINK *1C JärelKv BARRIER S OR CLB 
                         LINK NOT *1 S BARRIER CLB) ;
REMOVE Kpost + (@KN>) (*1 Pron BARRIER S OR CLB) ;
REMOVE Kpre + (@KN>) (*1 (@<P) BARRIER S OR CLB LINK *1 Pron BARRIER S OR CLB)  ;
REMOVE Kpost + (@KN>) (*1 Adv BARRIER S OR CLB) ;
REMOVE Kpre + (@KN>) (*1 (@<P) BARRIER S OR CLB LINK *1 Adv BARRIER S OR CLB)  ;

#Erandlik pool
#SELECT ("pool" @KN>) + Kpost (1 Par)(NEGATE 0 N) ;
#REMOVE ("pool" @KN>) (NEGATE 1 Par) ;
#ükskord jõin tema pool teed.

#5.2. Järeltäiend

REMOVE Kpre + (@<KN) (NEGATE *-1 S BARRIER CLB OR FV) ;
REMOVE Kpost + (@<KN) (*-1C (@P>) BARRIER CLB LINK NOT *-1 S BARRIER CLB OR FV) ;
REMOVE Kpost + (@<KN) (*-1C (@P>) BARRIER CLB LINK NOT *-1 S BARRIER CLB OR FV) ;
REMOVE (@<KN) (NEGATE *-1 NotEestäiend BARRIER CLB OR FV) ;

# Sisekaassõnad ei täienda tavaliselt sisekohakäändeid
# Ma seisin esikus peegli ees
REMOVE (@<KN) (0 SiseKaas)(-1C (@P>))(-2 In)(NEGATE *-3 SP BARRIER NPpiir OR CLB) ;

#6. Infinitiivid
#6.1. Eestäiend
REMOVE (@INFN>)(NEGATE *1 S BARRIER CLB OR FV OR Pron OR Adv) ;
# DeverbaalSantiivi laiendina talitleb supiinitarind adverbiaalatribuudina.
REMOVE Supiin + (@INFN>) (NEGATE *1 Deverbaal BARRIER CLB OR FV OR Pron) ;

#6.2. Järeltäiend
# Eespool on ainult eestäiendid
REMOVE (@<INFN)(NEGATE *-1 NotEestäiend OR S BARRIER FV OR CLB) ;

# Supiinitarind paikneb harilikult oma pohisona ees, üksikjuhtudel
# voib see asetseda ka viimase järel.
REMOVE Supiin + (@<INFN) (NEGATE *-1 Deverbaal BARRIER CLBC OR NPpiir) ;

#VII ADVERBIAALIGRAMMATIKA

SELECT (@ADVL) (0 Poolik)(2 Adv) ;



#1) Nimisona kolmes esimeses käändes
#a) erandjuhud
# see on seda väärt
SELECT Par + (@ADVL) (1 ("väärt")) ;
# viie meetrine
SELECT Gen + (@ADVL) (1 Adjühik) ;

# Nimisona kolmes esimeses käändes ei ole määrus, kui ta ei ole kas aja, hulga- voi Võrdlusmäärus ega 
# lauselaiend
REMOVE Nom + (@ADVL)(0 SP)(NEGATE 0 Aeg OR Arvsõna OR NimKv OR Lauselaiend)
             (NEGATE 1 Hiljem)
             (NEGATE -1 Iga OR JärgArv)
             (NEGATE *-1 Terve OR Võrdlus BARRIER CLB OR FV) ;
#Vead: 
# Ta vottis istet samas reas kus _Winstongi_
# päevadel, mis ma abahakiiride juures oline
# Mis see meie asi on (2x)
# läheme jala ja kõik
# kõiki oleks tulnud kõnetada seltsimees

REMOVE Nom + (@ADVL) (0 SP)(NEGATE 0 Aeg OR Arvsõna OR NimKv OR Lauselaiend)
             (NEGATE 1 Hiljem)
             (NEGATE -1 Iga OR JärgArv)
             (*-1 Võrdlus + CLB BARRIER CLB OR FV) ;
REMOVE Nom + (@ADVL) (0 SP)(NEGATE 0 Aeg OR Arvsõna OR NimKv OR Lauselaiend)
             (NEGATE 1 Hiljem)
             (NEGATE -1 Iga OR JärgArv)
             (*-1 Võrdlus + Kui BARRIER CLB OR FV LINK 1 Ka) ;

REMOVE Nom + (@ADVL) (0 SP)(NEGATE 0 Aeg OR Arvsõna OR NimKv OR Lauselaiend)
             (NEGATE 1 Hiljem)
             (NEGATE -1 Iga OR JärgArv)
             (*-1 Võrdlus BARRIER CLB OR FV OR JaVoi LINK 1 SPNom LINK NOT 0 (@NN>)) ;


REMOVE Par + (@ADVL) (NEGATE -1 JärgArv)(NEGATE 0 Tervitus OR NimKv OR ("kord") OR Aeg)
              (NEGATE 0 Arvsõna OR Viisimäärus OR KedaMida OR ("see"))
              (NEGATE 1 Võrre)(NEGATE *-1 Võrdlus BARRIER CLBC) ;
REMOVE Par + (@ADVL) (NEGATE -1 JärgArv)(NEGATE 0 Tervitus OR NimKv OR ("kord") OR Aeg)
              (NEGATE 0 Arvsõna OR Viisimäärus OR KedaMida OR ("see"))
              (NEGATE 1 Võrre)(*-1 Võrdlus + CLB BARRIER CLB) ;

REMOVE ("see" @ADVL) + Par (NEGATE 1 KompKvAdv)(NEGATE 1 Võrre)(NEGATE 1 ("et"))
              (NEGATE *-1 Võrdlus BARRIER CLB) ;
REMOVE Par + (@ADVL) (NEGATE -1 JärgArv)(NEGATE 0 Tervitus OR NimKv OR ("kord") OR Aeg)
              (NEGATE 0 Arvsõna OR Viisimäärus OR KedaMida OR ("see"))
              (NEGATE 1 Võrre)(*-1 Võrdlus BARRIER CLB LINK 1 SPNom) ;

REMOVE Par + (@ADVL)(0 KedaMida)(1C Subjekt) ;
SELECT N + Gen + (@ADVL)(1 Adjühik) ;
SELECT Gen + (@ADVL)(1 SpetsAdj)(1C Adj) ;
SELECT Gen + (@ADVL)(1 JaVoi)(2 S + Gen)(3 SpetsAdj) ;


# Omastava käände korral tuleb jälgida, et järgnev sona
# ei oleks pikkune-tüüpi omadussona

REMOVE Sprop + Gen + (@ADVL) (NEGATE 1 Tud OR Ger OR Inf OR Tav)
               (NEGATE *-1 Võrdlus BARRIER CLB) ;
REMOVE Gen + (@ADVL) (NEGATE -1 JärgArv)(NEGATE 0 NimKv OR ("kord") OR Aeg OR Arvsõna)
              (NEGATE *1 NiNaTaGa BARRIER NPpiir OR CLB OR Side)
              (NEGATE 1 NudTudDes OR Inf OR Tav)(NEGATE *-1 Võrdlus BARRIER CLB) ;

# Mitmuses omastavas käändes nimisona pole määrus, kui ei järgne pikune-tüüpi omadussona
REMOVE PlGen + (@ADVL)(NEGATE 1 Sid|Kvm)(NEGATE 1 NudTudDes)(NEGATE 1 Inf)(NEGATE 1 Tav) ;
REMOVE PlGen + (@ADVL)(1 Sid|Kvm)(NEGATE *1 NiNaTaGa BARRIER CLB) ;

# Nimisonalised kvantorid ei ole määrused:
REMOVE Nom + (@ADVL)(0 NimKv)(NEGATE 0 Aeg)(NEGATE 1 Hiljem)
                (NEGATE -1 Iga)(NEGATE -1 JärgArv)
                (NEGATE *-1 Võrdlus BARRIER CLBC)(NEGATE *1 JärelKv BARRIER CLBC) ;

# Üksikud juhud:
REMOVE ("miski" @ADVL) + Par ;
REMOVE ("oma" @ADVL) (0 NGP)(NEGATE 1 Tav)(NEGATE 1 Tud)(NEGATE 1 Inf) ;


# Ma-tegevusnimi on määrus, kui osalauses leidub vastav verb
SELECT Supiin + (@ADVL) (*-1C FV + SupIllAdvVerb BARRIER Sid|Kvm OR CLB) ;

# Lauselaiendid ja tervitused:
REMOVE A + Nom + (@ADVL) (NEGATE 0 Lauselaiend) ;
SELECT (@ADVL) (0 Lauselaiend)(-1 <<<)(1 Koma) ;
SELECT (@ADVL) (0 Lauselaiend)(-2 <<<)(-1 ("väga"))(1 Koma) ;
SELECT (@ADVL) (0 Lauselaiend)(-1 <<<)(1 Küll) ;
SELECT (@ADVL) (0 Lauselaiend)(-1 Kvm)(1 Küll) ;
REMOVE A + Nom + (@ADVL) (0 Lauselaiend)(NEGATE 1 Koma) ;


# nimisona seestütlevas on määrus,  kui järgneb keskvorre
SELECT El + (@ADVL) (*1 Võrre BARRIER FV OR CLB) ;
SELECT Term + (@ADVL) (1 Täis) ;
# omadussona neljas viimases käändes on määrus
SELECT (@ADVL) (0 Adj + NiNaTaGa) ;
SELECT A + Gen + (@ADVL) (1 JaVoi)(2 Adj + NiNaTaGa) ;
# omadussona omastavas pole määrus, kui paremal ei ole neljas viimases käändes omadussona.
REMOVE A + Gen + (@ADVL) (NEGATE *1 Adj + NiNaTaGa) ;
# adverb enne partitsiipi on määrus
SELECT Adv + (@ADVL) (1 Partic) ;
SELECT Digit + (@ADVL) (-1 OPar)(1 CPar) ;
REMOVE Digit + (@ADVL) (1 Fst)(*1 S BARRIER CLB) ;
REMOVE Digit + (@ADVL) (1 Fst)(2 ("a") + Y) ; 
# Kui numbrina kirjutatud Nu järel on punkt ja see ei ole lause lopp.

SELECT QuotPresPs + (@ADVL) (NEGATE -1 Tud)(*0 VatAdvVerb BARRIER CLB) ;

AFTER-SECTIONS

# SUBJEKTI HEURISTILISED REEGLID 
# Need reeglid on kas nii uued, et on korralikult testimata, voi on nende tekitatud vigade hulk suur.
REMOVE Inf + (@SUBJ) (NEGATE *-1 TeisedDaInfv OR Emotsioonv OR Sobivusv 
                        OR Vajadusv OR Suutelisusv BARRIER CLBC)
		   (NEGATE *1 TeisedDaInfv OR Emotsioonv OR Sobivusv 
                        OR Vajadusv OR Suutelisusv BARRIER CLBC) ;

# Subjekt eelneb predikatiivile
SELECT S + (@SUBJ) (NEGATE 0 (@OBJ) OR (@ADVL)) (1 FinV)
                (*2 SPNom + (@PRD) BARRIER CLBC) ;

# Nimetavas käändes sõnale järgneb kohe järgmine osalause
# Nt. Mees, kes ....
SELECT Nom + (@SUBJ) (NEGATE 0 N)(1 Koma OR OPar)(NEGATE *-1 Verb BARRIER CLB)
               (NEGATE *1 SP OR Verb BARRIER CLB)(NEGATE *-1 Dsh) ; # TP +Dsh

# Veidi laiendatud unikaalsuse printsiip, mis ei Nesta sidesõnade ja
# kvm-dega, sest leitud alus ja sõna on erinevates käänetes
REMOVE (@SUBJ) (NEGATE 0 Nom)(*-1C Subjekt + Nom)
                  (*-1 FinV BARRIER CLBC LINK NOT *-1 FinV) ;
REMOVE (@SUBJ) (NEGATE 0 Nom)(*1C Subjekt + Nom)
                  (*1 FinV BARRIER CLBC LINK NOT *1 FinV) ;
# Lause alguses on potentsiaalne subjekt, mis on nimetavas käändes,
# sõna ise on osastavas ja paremas kontekstis on infinitiiv.
 REMOVE Par + (@SUBJ) (*1 Inf BARRIER CLB)(*-1 Subjekt + Nom BARRIER CLB) ;
#Kui sõna on mitmuse nominatiivis ja järgneb vahetul verb pl3 ja
#rohkem mitmuse nominatiive osalauses pole
#vigu tekib poolikutes osalausetes. 
SELECT PlNom + (@SUBJ) (1 Vps3pl) (NEGATE *2 SP + PlNom)
                  (NEGATE *-1 Imps BARRIER CLB)
                  (NEGATE *1 Imps BARRIER CLB)
                  (NEGATE *-1 Subjekt BARRIER CLB) ;
# Tüüplause Ad Intr&YVVerb SUBJ
SELECT (@SUBJ) (NEGATE 0 Inf)
                  (*-1 In OR Ad BARRIER CLB 
                       LINK NOT *-1 SP BARRIER CLB)
                  (*-1 In OR Ad BARRIER Subjekt OR CLB LINK 1 Intr&YV)
                  (NEGATE *1 Subjekt BARRIER CLB)
                  (NEGATE *-1 Inf OR Tud BARRIER CLB)
                  (NEGATE *1 Inf OR Tud BARRIER CLB) ;
SELECT (@SUBJ) (NEGATE 0 Inf)(*-1 In OR Ad BARRIER CLB 
                               LINK NOT *-1 SP BARRIER CLB)
                  (*-1 In OR Ad BARRIER SPNom OR CLB LINK 1 Intr&YV)
                  (NEGATE *1 SPNom BARRIER CLB)
                  (NEGATE *-1 Tud BARRIER CLB)
                  (NEGATE *1  Tud BARRIER CLB)(1C (@<INFN)) ;

SELECT (@SUBJ) (*-1 In OR Ad BARRIER CLB LINK NOT *-1 SP BARRIER CLB)
               (*-1 In OR Ad BARRIER CLB OR SPNom LINK 1 Intr&YV)
               (NEGATE *-1 Tud OR Imper BARRIER CLB)
               (NEGATE *1  Tud OR Imper BARRIER CLB)
               (*1C Objekt BARRIER CLB LINK *1 Inf BARRIER CLB) ;

SELECT Nom + (@SUBJ) (NEGATE 0 N)(NEGATE *-1 Koma)(NEGATE *1 Koma)
               (NEGATE *-1 Subjekt)(NEGATE *1 Subjekt)
               (NEGATE 1 Inf)(NEGATE *-1 Imps)(NEGATE *1 Imps)
               (NEGATE *-1 Imper)(NEGATE *1 Imper)(NEGATE *-1 Vps12)(NEGATE *1 Vps12) ;

SELECT Nom + (@SUBJ) (NEGATE 0 Määrus)
               (NEGATE *-1 Koma BARRIER CLB)
               (*1 Koma BARRIER CLB LINK *1 SP BARRIER CLB)
               (NEGATE *-1 Subjekt BARRIER CLB)(NEGATE *1 Subjekt BARRIER CLB)
               (*1 CLB ^ Ja)(*-1 FinV BARRIER CLB)(NEGATE 1 Inf)
               (NEGATE *-1 Imps BARRIER CLB)(NEGATE *1 Imps BARRIER CLB)
               (NEGATE *-1 Imper BARRIER CLB)(NEGATE *1 Imper BARRIER CLB)
               (NEGATE *-1 Vps12 BARRIER CLB)(NEGATE *1 Vps12 BARRIER CLB)
               (NEGATE -1 Partic) ;
# välja jätta või maha tõmmata on lihtne
 SELECT Inf + (@SUBJ) (*1C (@PRD) + Adj)
          (NEGATE *-1 SPNom BARRIER CLB)(NEGATE *1 SPNom BARRIER CLB) ;

#Kui sõna võib olla kvantori laiend ja kvantor ise võib olla alus, siis
#kustuta kvantori märgend
#Laual oli kuhi musti toidunõusid
REMOVE (@SUBJ)(0 JärelKv)(NEGATE 0 Nom)
        (*-1 NimKv + Subjekt OR ArvKv + Subjekt OR KvAdv + Subjekt BARRIER CLB LINK NOT 1 Scom) ;

# Koordinatsioon
SELECT PlNom + (@SUBJ) (NEGATE 0 Pdet)(-1 JaVoiK)(-2 PlNom)(-2C Subjekt) ;
SELECT PlNom + (@SUBJ) (1 JaVoiK)(2 PlNom)(2C Subjekt) ;
SELECT SgNom + (@SUBJ) (-1 JaVoiK)(-2 SgNom)(-2C Subjekt)
               (NEGATE *1 Imps BARRIER CLBC) ;
SELECT SgNom + (@SUBJ) (1 JaVoiK)(2 SgNom)(2C Subjekt) ;
SELECT Nom + (@SUBJ) (*-1 JaVoi BARRIER Head OR CLBC LINK -1C Subjekt + Nom) ;
SELECT Nom + (@SUBJ) (*-1 Koma BARRIER Head LINK -1C Subjekt + Nom)
                      (*1 JaVoi BARRIER CLBC LINK *1 Subjekt BARRIER CLBC) ;

SELECT (@SUBJ) (0 KvAdv)(*-1 Olema BARRIER CLB)
               (NEGATE *-1 Subjekt BARRIER CLB)(NEGATE *1 Subjekt BARRIER CLB) ;

AFTER-SECTIONS

# osalause algab kes-või-mis, millele järgneb verbi sg 3 ja pole
# mineviku partitsiipi


SELECT ("kes" @SUBJ) OR ("mis" @SUBJ) (-1 JaVoiK)(0 Sg)(1 Vps3sg)
                   (NEGATE *1 Imps BARRIER CLB) ;
SELECT ("kes" @SUBJ) OR ("mis" @SUBJ) (-1 JaVoiK)(0 Pl)(1 Vps3pl)
                   (NEGATE *1 Imps BARRIER CLB) ;


REMOVE S + (@SUBJ) (*-1C Inf + (@SUBJ) BARRIER CLB) ;
REMOVE (@SUBJ)   (0 (@PRD))(NEGATE 0 Määrus)(NEGATE 0 Objekt)
                 (*-1 Prverb BARRIER (@PRD) OR NotEestäiend OR CLBC
                  LINK *-1C Subjekt BARRIER (@PRD) OR NotEestäiend OR CLBC) ;
SELECT Ppers + Pl + Pps3 + (@SUBJ) + Nom (*1 Vps3pl BARRIER CLB)  ;
REMOVE S + Par + (@SUBJ) (*-1 FV + ParObjVerb BARRIER CLB)(NEGATE *1 Par BARRIER Kvm OR CLB) ;
REMOVE S + Par + (@SUBJ) (*1 FV + ParObjVerb BARRIER CLB)(NEGATE *1 Par BARRIER Kvm OR CLB) ;
REMOVE S + Par + (@SUBJ) (*-1 FV + Trans BARRIER CLB)(NEGATE *1 Par BARRIER Kvm OR CLB) ;
REMOVE S + Par + (@SUBJ) (*1 FV + Trans BARRIER CLB)(NEGATE *1 Par BARRIER Kvm OR CLB) ;


# uued lisareeglid

END
